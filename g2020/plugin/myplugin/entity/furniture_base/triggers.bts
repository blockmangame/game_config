ENTITY_ENTER
{
}

ENTITY_TOUCH_ALL{
}

EDIT_OBJECT_ACTION{
    If($action == "startEdit"){
        CallTrigger(object = $obj1, event = "START_EDIT_ENTITY", obj2 = $obj2, params = $params)
    }ElseIf($action == "stopEdit"){
        CallTrigger(object = $obj1, event = "STOP_EDIT_ENTITY", obj2 = $obj2, params = $params)
    }Else{
        CallTrigger(object = $obj1, event = "EXECUTION_EDIT_ACTION", obj2 = $obj2, params = $params, action = $action)
    }
}

START_EDIT_ENTITY{
    CallTrigger(object = $obj1, event = "ADD_EDIT_BUFF")
    SetObjectVar(obj = $obj2, key = "lastInteractionUiObjId", value = GetObjectID(object = $obj1))
}

EXECUTION_EDIT_ACTION{
    $decEditBuff = GetConfigVar(obj = $obj1, key = "editBuff")
    If(not $decEditBuff){
        Return()
    }
    $buff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $decEditBuff) -- 不在编辑状态下玩家不能编辑房子的家具
    If(not $buff){
        Return()
    }
    $event = nil
    If($action == "beginMove"){
        $event = "DEC_INTERACTION_MOVE_TOUCH_BEGIN"
    }ElseIf($action == "endMove"){
        $event = "DEC_INTERACTION_MOVE_TOUCH_END"
    }ElseIf($action == "sale"){
        $event = "DEC_INTERACTION_SALE"
    }ElseIf($action == "leftRotation"){
        $event = "DEC_INTERACTION_LEFT_ROTATION"
    }ElseIf($action == "rightRotation"){
        $event = "DEC_INTERACTION_RIGHT_ROTATION"
    }ElseIf($action == "exit"){
        $event = "DEC_INTERACTION_EXIT_EDIT"
    }
    CallTrigger(object = $obj1, event = $event, objID = GetObjectID(object = $obj1), params = $params)
}

STOP_EDIT_ENTITY{
    CallTrigger(object = $obj1, event = "REMOVE_EDIT_BUFF")
}

ENTITY_CLICK{
    $editBuff = GetEntityTypeBuff(entity = $obj2, key = "fullName", value = "myplugin/edit_house_buff") -- 不在编辑状态下玩家不能编辑房子的家具
    If(not $editBuff){
        Return()
    }
    $lastInteractionUiObjId = GetObjectVar(obj = $obj2, key = "lastInteractionUiObjId")
    If($lastInteractionUiObjId){
        UpdateEntityEditContainer(player = $obj2, entityId = $lastInteractionUiObjId, show = false)
    }
    StartTimer(rep = false, object =$obj1, obj1 =$obj1, obj2 = $obj2, time = 1, event = "SHOW_EDIT_UI")
}   

SHOW_EDIT_UI{
    UpdateEntityEditContainer(player = $obj2, entityId = GetObjectID(object = $obj1), show = true)
}

DEC_INTERACTION_MOVE_TOUCH_BEGIN{
    PlayAction(
        entity = $obj1,
        includeSelf = true,
        actionName = "jump",
        actionTime = -1
    )
}

DEC_INTERACTION_MOVE_TOUCH_END{
    -- entityPos = $entityPos, entityYaw = $entityYaw, entityPitch = $entityPitch, entityScale = $entityScale)
    $dict = NewDictionary($params or {})
    SetEntityPosition(entity = $obj1, map = GetEntityMap(entity = $obj1), pos = DictionaryGet(dict = $dict, key = "entityPos") or GetEntityPosition(entity = $obj1)
        , ry = DictionaryGet(dict = $dict, key = "entityYaw") or GetEntityYaw(entity = $obj1), rp = DictionaryGet(dict = $dict, key = "entityPitch") or GetEntityPitch(entity = $obj1))
    SetEntityRoll(entity = $obj1, rotationRoll = DictionaryGet(dict = $dict, key = "entityRoll") or GetEntityPosition(entity = $obj1))
    $obj1.lastSideNormal = DictionaryGet(dict = $dict, key = "lastSideNormal") or Table(x = 0,y = 0,z = 0)
    StartTimer(rep = false, object =$obj1, obj1 =$obj1, time = 2, yaw = DictionaryGet(dict = $dict, key = "entityYaw") or GetEntityYaw(entity = $obj1), event = "ROTATION_BODY_YAW")
}

DEC_INTERACTION_SALE{
    $owner = GetObjectVar(obj = GetObjectVar(obj = $obj1, key = "house"), key = "owner")
    $houseVars = GetObjectVar(obj = $owner, key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            DictionarySet(dict = $dict, key = "curHouseVarsIndex", value = $index)
            $curHousedict = $dict
            Break()
        }
    }
    $furnitures = DictionaryGet(dict = $curHousedict, key = "houseInsideFurnishing")
    ForLoop(from = 1, to = ArraySize(array = $furnitures), key = "index"){
        $fDict = NewDictionary(ArrayGet(array = $furnitures, index = $index))
        If(DictionaryGet(dict = $fDict, key = "furnishingId") == GetObjectID(object = $obj1)){
            $cfg = DictionaryGet(dict = $fDict, key = "cfg")
            $sellMoney = GetEntityCfg(fullName = $cfg, key = "sellMoney")
            If(DictionaryGet(dict = $fDict, key = "isOldFurnishing")){
                $sellMoney = GetEntityCfg(fullName = $cfg, key = "sellByDiscountMoney")
            }
            AddCurrency(entity = $owner, coinName = "green_currency", count = $sellMoney)
            CallTrigger(event = "ENTITY_ADD_DINGDONG_BUFF", object = $owner, buffKey = "getMoneyTipSoundBuff")
            ArrayRemove(array = $furnitures, index = $index)
            Break()
        }
    }
    $index = DictionaryGet(dict = $curHousedict, key = "curHouseVarsIndex")
    DictionarySet(dict = $curHousedict, key = "curHouseVarsIndex", value = nil)
    ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $curHousedict))

    UpdateEntityEditContainer(player = $owner, entityId = GetObjectID(object = $obj1), show = false)
    SetObjectVar(obj = $owner, key = "lastInteractionUiObjId", value = nil)

    KillEntity(entity = $obj1)
}

DEC_INTERACTION_LEFT_ROTATION{
    CallTrigger(object = $obj1, event = "POS_AROUND", params = $params, scale = GetConfigVar(obj = $obj1, key = "rotationYawModlus"))
}

DEC_INTERACTION_RIGHT_ROTATION{
    CallTrigger(object = $obj1, event = "POS_AROUND", params = $params, scale = -GetConfigVar(obj = $obj1, key = "rotationYawModlus"))
}

POS_AROUND{
    $dict = NewDictionary($params or {})
    $sideNormal = $obj1.lastSideNormal or Table(x = 0,y = 0,z = 0)
    $sideNormalX = GetPosX(pos = $sideNormal)
    $sideNormalY = GetPosY(pos = $sideNormal)
    $sideNormalZ = GetPosZ(pos = $sideNormal)
    If($sideNormalX == 0 and $sideNormalY == 0 and $sideNormalZ == 0){ -- First In The Floor
        $sideNormalY = 1
    }
    $yaw = GetEntityYaw(entity = $obj1)
    $pitch = GetEntityPitch(entity = $obj1)
    $imcYaw = 0
    $imcPitch = 0
    If($sideNormalY == -1 or $sideNormalY == 1){
        $imcYaw = $sideNormalY == -1 and -$scale or $scale
        SetEntityYaw(entity = $obj1, rotationYaw = $yaw + $imcYaw)
        StartTimer(rep = false, object =$obj1, obj1 =$obj1, time = 2, yaw = $yaw + $imcYaw, event = "ROTATION_BODY_YAW")
    }ElseIf($sideNormalX == -1 or $sideNormalX == 1){
        $imcPitch = $sideNormalX == 1 and -$scale or $scale
        SetEntityPitch(entity = $obj1, rotationPitch = $pitch + $imcPitch)
    }Else{
        $imcPitch = $sideNormalZ == 1 and -$scale or $scale
        SetEntityPitch(entity = $obj1, rotationPitch = $pitch + $imcPitch)
    }
}

ROTATION_BODY_YAW{
    SetBodyYaw(entity = $obj1, yaw = $yaw)
}

DEC_INTERACTION_EXIT_EDIT{
    UpdateEntityEditContainer(player = $obj2, entityId = GetObjectID(object = $obj1), show = false)
}

ADD_EDIT_BUFF{
    $decEditBuff = GetConfigVar(obj = $obj1, key = "editBuff")
    If(not $decEditBuff){
        Return()
    }
    $buff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $decEditBuff) -- 不在编辑状态下玩家不能编辑房子的家具
    If(not $buff){
        AddEntityBuff(entity = $obj1, cfg = $decEditBuff)
    }
}

REMOVE_EDIT_BUFF{
    $decEditBuff = GetConfigVar(obj = $obj1, key = "editBuff")
    If(not $decEditBuff){
        Return()
    }
    $buff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $decEditBuff) -- 不在编辑状态下玩家不能编辑房子的家具
    If(not $buff){
        Return()
    }
    RemoveEntityBuff(entity = $obj1, buff = $buff)
}

DEC_INTERACTION_TEST{

}