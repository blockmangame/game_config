RIDE_ON_INDEX_1(){
    CallTrigger(object = $obj1, obj2 = $obj2, event = "RIDE_ON", targetIndex = 1)
}
RIDE_ON_INDEX_2(){
    CallTrigger(object = $obj1, obj2 = $obj2, event = "RIDE_ON", targetIndex = 2)
}
RIDE_ON_INDEX_3(){
    CallTrigger(object = $obj1, obj2 = $obj2, event = "RIDE_ON", targetIndex = 3)
}
RIDE_ON_INDEX_4(){
    CallTrigger(object = $obj1, obj2 = $obj2, event = "RIDE_ON", targetIndex = 4)
}
RIDE_ON
{
    If(not $targetIndex){
        $targetIndex = 1
    }
    $owner = GetOwner(entity = $obj1)
    $canDrive = false
    If($owner)
    {
        $ownerTeamId = GetEntityTeam(entity = $owner)
        $driverTeamId = GetEntityTeam(entity = $obj2)
        If($owner == $obj2)
        {
            $canDrive = true
        }
        ElseIf($ownerTeamId and $driverTeamId )
        {
            If($ownerTeamId ~= 0 and $driverTeamId~= 0 and $ownerTeamId == $driverTeamId)
            {
                $canDrive = true
            }
            Else
            {
                ShowTip(entity = $obj2, tipType = 2, textKey = "condition_with_equip_other_car", keepTime = 40)
            }
        }
    }

    If($canDrive)
    {
        CallTrigger(object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "ADD_TARGET_ATTACH_POINT_INDEX_BUFF")
        If($obj2.otherItem){
            --ClearRide(entity = $obj2.createdTrolley)
			--EquipItem(entity = $obj2, item = $obj2.equipItem)
            ClearItemUseByKey(entity = $obj2, key = "typeIndex", valueArray = NewArray(1, 2, 4, 5))
            StartTimer(rep = false, time = 1, object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "RIDE_ON_CAR", targetIndex = $targetIndex)
        }
        Else
        {
            CallTrigger(obj1 = $obj2, event = "PLAYER_CLEAR_RIDE")
            RideOn(entity = $obj2, target = $obj1, targetIndex = $targetIndex)
        }
        StartTimer(rep = false, time = 1, object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "CLAC_IS_ON_BLOCK_RIDE_FILED")
    }
}

ADD_TARGET_ATTACH_POINT_INDEX_BUFF{
    $targetAttachPointIndexBuff = GetConfigVar(obj = $obj1, key = $obj2.role == @adult and "targetAttachPointIndexBuffByAdult" or "targetAttachPointIndexBuffByChild")
    If($targetAttachPointIndexBuff and not GetEntityTypeBuff(entity = $obj2, key = "fullName", value = $targetAttachPointIndexBuff)){
        AddEntityBuff(entity = $obj2, cfg = $targetAttachPointIndexBuff)
    }
}

RIDE_ON_CAR
{
    CallTrigger(object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "ADD_TARGET_ATTACH_POINT_INDEX_BUFF")
    CallTrigger(obj1 = $obj2, event = "PLAYER_CLEAR_RIDE")
    RideOn(entity = $obj2, target = $obj1, targetIndex = $targetIndex)
    StartTimer(rep = false, time = 1, object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "CLAC_IS_ON_BLOCK_RIDE_FILED")
}

CLAC_IS_ON_BLOCK_RIDE_FILED{
    If(not $obj2.lastInBlockRideRegion){
        Return()
    }
    If(not IsPosInRegion(pos = GetEntityPosition(entity = $obj1), region = $obj2.lastInBlockRideRegion)){
        Return()
    }
    $blockRideEntity = GetRegionOwner(region = $obj2.lastInBlockRideRegion)
    $door = $blockRideEntity.door
    $house = $door.house
    $map = GetEntityMap(entity = $house)
    $ry = GetEntityYaw(entity = $house.door)
    $regionCenter = GetRegionCenter(region = $house.region)
    $pos = {x = GetPosX(pos = $regionCenter), y = GetPosY(pos = GetRegionMin(region = GetObjectVar(obj = $house, key = "region"))), z = GetPosZ(pos = $regionCenter)}
    $pos = ComputeV3(
        p1 = $pos,
        p2 = V3RotationYaw(
            vector = GetConfigVar(obj = $house, key = "entityTeleportToOutsidePosOffset") or Table(x = 0, y = 0.5, z = 3),
            yaw = $ry
        ),
        op = "add"
    )
    SetEntityPosition(entity = $carrier or $obj1, map = $map, ry = $ry or 0, pos = $pos)
    ChangeCameraView(player = $obj2, yaw = $ry, pitch = 30, distance = 5, smooth = 1, offset = {x = 0, y = 0, z = 0})
    $obj2.lastInBlockRideRegion = nil
}

ENTITY_RIDE_ON
{
    CallTrigger(object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "ADD_TARGET_ATTACH_POINT_INDEX_BUFF")
    $obj2.onCar = true
    $hidePlayer = GetConfigVar(obj = $obj1, key = "hidePlayer")
    If($hidePlayer)
    {
        $obj2.driveHideBuff = AddEntityBuff(entity = $obj2, cfg = "myplugin/driveHide_buff")
    }
	StartTimer(rep = false, time = 1, object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "CLAC_IS_ON_BLOCK_RIDE_FILED")

    $loadSectionMaxInterval = GetConfigVar(obj = $obj1, key = "loadSectionMaxInterval")
    If($loadSectionMaxInterval)
    {
        SetLoadSectionMaxInterval(entity = $obj2, value = $loadSectionMaxInterval)
    }
}

REMOVE_TARGET_ATTACH_POINT_INDEX_BUFF{
    $targetAttachPointIndexBuff = GetConfigVar(obj = $obj1, key = $obj2.role == @adult and "targetAttachPointIndexBuffByAdult" or "targetAttachPointIndexBuffByChild")
    If($targetAttachPointIndexBuff and GetEntityTypeBuff(entity = $obj2, key = "fullName", value = $targetAttachPointIndexBuff)){
        RemoveEntityBuff(entity = $obj2, buff = GetEntityTypeBuff(entity = $obj2, key = "fullName", value = $targetAttachPointIndexBuff))
    }
}

ENTITY_RIDE_OFF
{
    CallTrigger(object = $obj1, obj1 = $obj1, obj2 = $obj2, event = "REMOVE_TARGET_ATTACH_POINT_INDEX_BUFF")
    $obj2.onCar = false
    If($obj2.driveHideBuff)
    {
        RemoveEntityBuff(entity = $obj2, buff = $obj2.driveHideBuff)
    }

    $loadSectionMaxInterval = GetConfigVar(obj = $obj1, key = "loadSectionMaxInterval")
    If($loadSectionMaxInterval)
    {
        SetLoadSectionMaxInterval(entity = $obj2, value = 3500)
    }
}

START_HORN{
    $hornSoundBuff = GetConfigVar(obj = $obj1, key = "hornSoundBuff")
    If(not $hornSoundBuff){
        Return()
    }
    $soundBuffMap = NewDictionary(GetConfigVar(obj = $obj1, key = "hornSoundBuff") or {})
    $soundBuffCfg = DictionaryGet(dict = $soundBuffMap, key = "buff")
    If(not $soundBuffCfg){
        Return()
    }
    $soundBuffTime = DictionaryGet(dict = $soundBuffMap, key = "time")
    $soundBuff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $soundBuffCfg)
    AddEntityBuff(entity = $obj1, cfg = $soundBuffCfg, buffTime = $soundBuffTime)
}

STOP_HORN{
}

MOVE_BEGIN_FORWARD_BACK{
    $forwardBuffCfg = GetConfigVar(obj = $obj1, key = "carForwardMovementBuff")
    $backBuffCfg = GetConfigVar(obj = $obj1, key = "carBackMovementBuff")
    $entityYaw = GetEntityYaw(entity = $obj1) % 360
    $entityPos = GetEntityPosition(entity = $obj1)
    If($obj1.entityLastPos and $obj1.entityLastPos ~= $entityPos){
        $forwardBuff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $forwardBuffCfg)
        $backBuff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $backBuffCfg)
        $pointYaw = V3AngleXZ(vector = ComputeV3(op = "sub", p1 = $entityPos, p2 = $obj1.entityLastPos))
        $subYaw = $pointYaw - $entityYaw
        If($subYaw > 90 or $subYaw < -90){
            If(not $backBuff and $key){
                AddEntityBuff(entity = $obj1, cfg = $backBuffCfg)
            }
            If($forwardBuff){
                RemoveEntityBuff(entity = $obj1, buff = $forwardBuff)
            }
        }Else{
            If(not $forwardBuff and $key){
                AddEntityBuff(entity = $obj1, cfg = $forwardBuffCfg)
            }
            If($backBuff){
                RemoveEntityBuff(entity = $obj1, buff = $backBuff)
            }
        }
    }
    $obj1.entityLastPos = $entityPos
    $obj1.movementTimer = StartTimer(rep = false, object =$obj1, obj1 =$obj1, key = $key, time = 10, event = "MOVE_BEGIN_FORWARD_BACK")
}

MOVE_END_FORWARD_BACK{
    $buffCfg = GetConfigVar(obj = $obj1, key = "carForwardMovementBuff")
    $buff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $buffCfg)
    If($buff){
        RemoveEntityBuff(entity = $obj1, buff = $buff)
    }
    $buffCfg = GetConfigVar(obj = $obj1, key = "carBackMovementBuff")
    $buff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = $buffCfg)
    If($buff){
        RemoveEntityBuff(entity = $obj1, buff = $buff)
    }
    If($obj1.movementTimer){
        StopTimer(timer = $obj1.movementTimer)
        $obj1.movementTimer = nil
    }
    $obj1.entityLastPos = nil
}

START_FORWARD{
    CallTrigger(object = $obj1, obj1 = $obj1, key = "forward", event = "MOVE_BEGIN_FORWARD_BACK")
}

STOP_FORWARD{
    CallTrigger(object = $obj1, obj1 = $obj1, event = "MOVE_END_FORWARD_BACK")
}

START_BACK{
    CallTrigger(object = $obj1, obj1 = $obj1, key = "back", event = "MOVE_BEGIN_FORWARD_BACK")
}

STOP_BACK{
    CallTrigger(object = $obj1, obj1 = $obj1, event = "MOVE_END_FORWARD_BACK")
}