WEAR_EQUIPMEN {
    $typeIndex = GetConfigVar(obj = $item, key = "typeIndex")
    If($typeIndex == 3)
    {
        -- on car
        $carName = GetConfigVar(obj = $item, key = "carName")
        CallTrigger(event = "EQUIP_CAR", obj1 = $obj1, carName = $carName)
    }
}

TAKEOFF_EQUIPMEN {
    $typeIndex = GetConfigVar(obj = $item, key = "typeIndex")
    If($typeIndex == 3)
    {
        $carName = GetConfigVar(obj = $item, key = "carName")
        CallTrigger(event = "TAKEOFF_EQUIP_CAR", obj1 = $obj1, carName = $carName)
    }
}

INTERACTION_FEED {
    -- If(not IsPlayer(entity = $obj1))
    -- {
    --     Return()
    -- }
    
    $item = GetEntityHandItem(entity = $obj2)
    If(not $item)
    {
        Return()
    }
    $state = GetConfigVar(obj = $item, key = "state")
    $itemname = GetConfigVar(obj = $item, key = "itemname")
    $action = [
        [
            $obj1,
            GetConfigVar(obj = $item, key = "feededAction"),
            GetConfigVar(obj = $item, key = "feededActionTime")
        ],
        [
            $obj2,
            GetConfigVar(obj = $item, key = "feedAction"),
            GetConfigVar(obj = $item, key = "feedActionTime")
        ]
    ]
    ConsumeItem(item = $item, num = 1)
    CallTrigger(event = "FUNC_STATE_ADD_INTERACTLIST", entities = [$obj1, $obj2])
    Foreach(array = $action, key = "act")
    {
        PlayAction(
            entity = ArrayGet(array = $act, index = 1),
            includeSelf = true,
            actionName = ArrayGet(array = $act, index = 2),
            actionTime = ArrayGet(array = $act, index = 3)
        )
    }
    ShowBubbleMsg(
        entity = $obj1,
        textKey = "thank_sb_food",
        textP1 = GetEntityName(entity = $obj2),
        textP2 = $itemname
    )
    CallTrigger(
        object = $obj1,
        event = "FUNC_STATE_RELEASE",
        state = $state
    )
}
------------------------------------------------------------------------------------------------------------------------------ 分割线
CREATE_OWNER_HOUSE{
    ------------------------------------------ 当前选择存档 $curSelectHouse
    $curSelectHouse = {}
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    If(not $houseVars or ArraySize(array = $houseVars) == 0){ -- 第一次进入游戏的玩家
        $houseCfg = "myplugin/house1"
        $houseVars = [
                {
                    houseCfg = $houseCfg,
                    houseSelectColor = GetEntityCfg(fullName = $houseCfg, key = "defaultColor"),
                    houseInsideFurnishing = Copy(value = GetEntityCfg(fullName = $houseCfg, key = "houseDefaultFurniture") or []),
                    houseInsideRegionColors = Copy(value = GetEntityCfg(fullName = $houseCfg, key = "houseDefaultInsideMapColorTb") or {}),
                    isCurEquip = "enable",
                    houseName = GetEntityCfg(fullName = $houseCfg, key = "houseDefaultName") or "",
                    houseType = GetEntityCfg(fullName = $houseCfg, key = "houseType") or "",
                    houseId = Concat(p1 = GetTime(), p2 = GetGameTime(), p3 = Random(min = 0, max = 999999)),
                    houseInsideDoor = GetEntityCfg(fullName = $houseCfg, key = "insideDoor") or "myplugin/house_base_inside_door"
                }
            ]
        SetObjectVar(obj = $obj1, key = "houseVars", value = $houseVars)
    }

    $dict = {}
    Foreach(array = $houseVars, key = "house"){
        $dict = NewDictionary($house)
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            Break()
        }
    }
    $obj1.curHouseCfg = DictionaryGet(dict = $dict, key = "houseCfg")
    ------------------------------------------ 获得当前地图上的空出的区域，创建房子 * 
    -- 区域 <region------owner(需要使用GetRegionOwner)> 房子 <house-------owner> 玩家，可通过区域获取区域上的房子，通过房子获得所属玩家。 反向同理
    $regions = GetAllRegion(map = GetEntityMap(entity = $obj1))
    Foreach(array = $regions, key = "region"){
        If(not GetRegionOwner(region = $region) and GetRegionConfig(region = $region, key = "isHouseRegion")){
            CallTrigger(object = $obj1, event = "CREATE_HOUSE_OUTSIDE", region = $region, 
                houseCfg = DictionaryGet(dict = $dict, key = "houseCfg"),
                houseColor = DictionaryGet(dict = $dict, key = "houseSelectColor"))
            Break()
        }
    }
    ------------------------------------------ 玩家进入游戏就进入自己房间
    $house = GetObjectVar(obj = $obj1, key = "house")
    If(not $house){
        KickOutPlayer(entity = $obj1) -- 踢出服务器多分配的玩家
    }
    $houseInsideMap = GetObjectVar(obj = $house, key = "houseInsideMap")
    $yaw = 0
    If($obj1.curHouseCfg){
        $yaw = GetEntityCfg(fullName = $obj1.curHouseCfg, key = "insideMapRotationYaw")
    }
    SetEntityPosition(map = $houseInsideMap, entity = $obj1, ry = $yaw,
       pos = GetMapInitPos(map = $houseInsideMap) or GetEntityPosition(entity = $obj1))
    CallTrigger(object = $obj1, event = "SHOW_TITLE_BAR")
}
------------------------------------------------------------------------------------------------------------------------------ 分割线

ENTITY_ENTER
{
    CallTrigger(event = "CREATE_OWNER_HOUSE", object = $obj1)
    CallTrigger(event = "CHECK_IN_PARTY", object = $obj1)
    UpdateUIData(player = $obj1, ui = "circleProgress", data = {})
    OperationWindows(win = "workTask", isOpen = true, player = $obj1)
    CallTrigger(event = "INIT_WORK_TASK", object = $obj1) --初始化打工任务
    If(IsPlayer(entity = $obj1))
    {
        $playerName = GetEntityName(entity = $obj1)

        If($obj1.nickName == "defaultNickName")
        {
            $obj1.nickName = $playerName
        }

        If($obj1.nickName ~= $playerName)
        {
            SetEntityName(entity = $obj1, name = $nickName)
        }

        $obj1.isReadyReward = false
        $obj1.requestFamilyPlayer = nil

        CallTrigger(event = "ADD_MONEY", object = $obj1)
        If($obj1.inPartyOwnerId == 0)   -- not in party
        {
            $obj1.role = @adult
            CallTrigger(event = "SHOW_SELECT_ROLE", object = $obj1)
        }
        Else
        {
            If($obj1.role == @adult)
            {
                CallTrigger(object = $obj1, event = "CHANGE_PLAYER_SCALE_ADULT")
            }
            ElseIf($obj1.role == @child)
            {
                CallTrigger(object = $obj1, event = "CHANGE_PLAYER_SCALE_CHILD")
            }
        }
		
		CallTrigger(event = "SET_DRESS_ARCHIVE", object = $obj1)
        CallTrigger(event = "INIT_PLAYER_DRESS", object = $obj1)

        LoadPlayerAppDress(player = $obj1, event = "HTTP_PURCHASED_DRESS_CALL_BACK")

        -- test data
        --SetSkin(entity = $obj1, skinName = "custom_wing", skinValue = 25)
        --SetSkin(entity = $obj1, skinName = "custom_scarf", skinValue = 3)
        --SetSkin(entity = $obj1, skinName = "clothes_tops", skinValue = 20)
        --SetSkin(entity = $obj1, skinName = "custom_shoes", skinValue = 40)
        --SetSkin(entity = $obj1, skinName = "clothes_pants", skinValue = 35)
        --SetSkin(entity = $obj1, skinName = "custom_hair", skinValue = 5)
        --SetSkin(entity = $obj1, skinName = "custom_face", skinValue = 5)
        --SetSkin(entity = $obj1, skinName = "custom_glasses", skinValue = 3)
        --SetSkin(entity = $obj1, skinName = "custom_hat", skinValue = 5)
        --SetSkin(entity = $obj1, skinName = "custom_tail", skinValue = 5)

        CallTrigger(event = "FUNC_LOAD_NEW_WORKS")
        CallTrigger(event = "FUNC_LOAD_EXCELLENT_WORKS")
    }
}

OPEN_DANCE
{
    CallTrigger(event = "CHANGE_DANCE_SKILL", obj1 = $obj1, newSkill = $obj1.danceUseItem)
    AddSkill(entity = $obj1, name = "myplugin/open_dance")
}

CLOSE_DANCE
{
    CallTrigger(event = "CHANGE_DANCE_SKILL", obj1 = $obj1, oldSkill = $obj1.danceUseItem)
    RemoveSkill(entity = $obj1, name = "myplugin/open_dance")
}

------------------------------------------------------------------------------------------------------------------------------ 分割线
TEST_DATA{ -- 保存进数据库的数据伪代码 -- 旧 11-30
    Table(
        houseVars = [ -- all house 当前entity的所有房子
            [ --   arr[1] = houseCfg, arr[2] = houseSelectColor, 
                -- arr[3] = houseInsideFurnishing, arr[4] = houseInsideRegionColors, arr[5] = enable 当前选中房子标识/ disable 未选中
                -- arr[6] = curHouseName, arr[7] = curHouseType, arr[8] = ID, arr[9] = 房子内部门
                "myplugin/xxx...", -- houseCfg 
                "myplugin/xxx...", -- houseSelectColor，是buff来着 -- 外部颜色不影响内部
                [ -- houseInsideFurnishing , 房子内部物品摆放
                    -- pos是相对坐标，每个map都有一个initPos，坐标都是这个initPos+pos
                    -- 绝对坐标也行
                    [{x = "xx", y = "xx", z = "xx"},0,"myplugin/xxx...","id","true/false"],
                    [{x = "xx", y = "xx", z = "xx"},0,"myplugin/xxx...","id","true/false"]
                    -- {pos,yaw,cfg,全局唯一id:furnishingId（当前obj的id）,是否是旧家具，下线后则会将当前玩家全部房子的全部家具设置成true，即是旧家具}
                ],
                [ -- houseInsideRegionColors 房子内部区域颜色
                    ["regionkey", ["myplugin/xxx...","myplugin/xxx...","myplugin/xxx..."]], -- 第一个key是对应的region key，通过这个key获取这个区域region/获取到配置
                    ["regionkey", ["myplugin/xxx...","myplugin/xxx...","myplugin/xxx..."]] -- 第二个数组对应的是上面获取到的region的childRegionKey属性中对应
                                                                                        -- 该region的子region使用的方块！
                ],
                "enable", -- cur select house 有这个标志就是 当前选择/出生的房子
                "houseName", -- cur house name 当前房子的名字
                "houseType", -- cur house type 当前房子的类型
                "houseKey", -- cur house id 房子唯一ID，Concat(p1 = GetTime(), p2 = GetGameTime(), p3 = Random(min = 0, max = 999999))
                "houseInsideDoor" -- cur house InsideDoor
            ],
            [
                -- ... 同上，不是当前选择则第五个参数为disable
            ]
        ]
    )
    Table(Demo = [[[],[]],[[],[]]])
    Table( -- 保存进数据库的数据伪代码 -- 新 11-30
        houseVars = [ -- all house 当前entity的所有房子
            {
                houseCfg = "myplugin/xxx...", -- houseCfg 
                houseSelectColor = "myplugin/xxx...", -- houseSelectColor，是buff来着 -- 外部颜色不影响内部
                houseInsideFurnishing = [ -- houseInsideFurnishing , 房子内部物品摆放
                    -- pos是相对坐标，每个map都有一个initPos，坐标都是这个initPos+pos
                    -- 绝对坐标也行
                    {pos = {x = "xx", y = "xx", z = "xx"},yaw = 0,cfg = "myplugin/xxx...",
                        furnishingId = "id",isOldFurnishing = "true/false", saveSkinList = [{skinName = "xxx", skin = "xxx"}]}
                    -- {pos,yaw,cfg,全局唯一id:furnishingId（当前obj的id）,
                    -- isOldFurnishing 是否是旧家具，下线后则会将当前玩家全部房子的全部家具设置成true，即是旧家具
                    -- saveSkinList 特殊家具需要保存皮肤列表（需要在该家具中配一个saveSkinList 要保存的皮肤列表）}
                ],
                houseInsideRegionColors = [ -- houseInsideRegionColors 房子内部区域颜色
                    ["regionkey", ["myplugin/xxx...","myplugin/xxx...","myplugin/xxx..."]], -- 第一个key是对应的region key，通过这个key获取这个区域region/获取到配置
                    ["regionkey", ["myplugin/xxx...","myplugin/xxx...","myplugin/xxx..."]] -- 第二个数组对应的是上面获取到的region的childRegionKey属性中对应
                                                                                        -- 该region的子region使用的方块！
                ],
                isCurEquip = "enable", -- cur select house 有这个标志就是 当前选择/出生的房子
                houseName = "houseName", -- cur house name 当前房子的名字
                houseType = "houseType", -- cur house type 当前房子的类型
                houseId = "houseKey", -- cur house id 房子唯一ID，Concat(p1 = GetTime(), p2 = GetGameTime(), p3 = Random(min = 0, max = 999999))
                houseInsideDoor = "houseInsideDoor" -- cur house InsideDoor
            },{}

        ]
    )
}

CREATE_HOUSE_OUTSIDE{
    $yaw = GetRegionConfig(region = $region, key = "regionHouseYaw")
    $housePosOffset = GetEntityCfg(fullName = $houseCfg, key = "housePosOffset") or {x = 0, y = 0, z = 0}
    --Test("$housePosOffset--------------",$region,GetRegionCenter(region = $region),$housePosOffset,$yaw,V3RotationYaw(vector = $housePosOffset, yaw = $yaw),
        --ComputeV3(p1 = GetRegionCenter(region = $region), op = "add", p2 = V3RotationYaw(vector = $housePosOffset, yaw = $yaw)))
    $house = CreateNpc(map = GetEntityMap(entity = $obj1), cfgName = $houseCfg)
    $pos = ComputeV3(p1 = GetRegionCenter(region = $region), op = "add", p2 = V3RotationYaw(vector = $housePosOffset, yaw = $yaw))
    SetEntityPosition(entity = $house, pos = $pos, map = GetEntityMap(entity = $house))
    SetEntityYaw(entity = $house, rotationYaw = $yaw)
    SetBodyYaw(entity = $house, yaw = $yaw)

    AddEntityBuff(entity = $house, cfg = GetConfigVar(obj = $house, key = "houseCreateBuff"), buffTime = 200)
    -- 改变房子外部颜色，通过加buff
    AddEntityBuff(entity = $house, cfg = $houseColor)

    SetRegionOwner(region = $region, owner = $house)
    SetObjectVar(obj = $house, key = "owner", value = $obj1)

    SetObjectVar(obj = $obj1, key = "house", value = $house)
    SetObjectVar(obj = $house, key = "region", value = $region)
}

REMOVE_HOUSE_OUTSIDE{
    $house = GetObjectVar(obj = $obj1, key = "house")
    $region = GetObjectVar(obj = $house, key = "region")
    SetRegionOwner(region = $region, owner = nil)
    KillEntity(entity = $house)
}

ENTITY_LEAVE{ 
    ------------------------------------------ 玩家退出，清掉房子 邮箱 门，重置区域owner为nil
    CallTrigger(object = $obj1, event = "REMOVE_HOUSE_OUTSIDE")
    ------------------------------------------ 更新全部家具为旧家具
    CallTrigger(object = $obj1, event = "UPDATE_FURNITURE_STATUS")
    ----------------------------------------- 玩家退出，保存身上穿搭
    CallTrigger(object = $obj1, event = "SAVE_PLAYER_DRESS")
    ---------------------------------------玩家退出，删除掉暂存在数据库“dressArchive”字段中的bm装扮信息
    CallTrigger(object = $obj1, event = "REMOVE_BM_DRESS")
    CallTrigger(object = $obj1, event = "CHECK_LEAVE_PARTY")
}
UPDATE_FURNITURE_STATUS{
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars") or {}
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        $furnitures = DictionaryGet(dict = $dict, key = "houseInsideFurnishing")
        ForLoop(from = 1, to = ArraySize(array = $furnitures or {}), key = "fIndex"){
            $fDict = NewDictionary(ArrayGet(array = $furnitures, index = $fIndex))
            DictionarySet(dict = $fDict, key = "isOldFurnishing", value = true)
            DictionarySet(dict = $fDict, key = "furnishingId", value = 0)
            ArraySet(array = $furnitures, index = $fIndex, value = DictionaryToTable(dict = $fDict))
        }
        ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $dict))
    }
}

UPDATE_HOUSE{
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "isCurEquip") ~= "enable" and $houseKey == DictionaryGet(dict = $dict, key = "houseId")){
            DictionarySet(dict = $dict, key = "curHouseVarsIndex", value = $index)
            $newHouseDict = $dict
        }
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            DictionarySet(dict = $dict, key = "curHouseVarsIndex", value = $index)
            $curHouseDict = $dict
        }
    }
    If(not $newHouseDict){
        DictionarySet(dict = $curHouseDict, key = "houseSelectColor", value = $curSelectKey)
        $index = DictionaryGet(dict = $curHouseDict, key = "curHouseVarsIndex")
        DictionarySet(dict = $dict, key = "curHouseVarsIndex", value = nil)
        ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $curHouseDict))
        CallTrigger(object = $obj1, event = "CHANGE_HOUSE_COLOR", oldHouseColor = $oldSelectKey, houseColor = $curSelectKey)
    }Else{
        DictionarySet(dict = $curHouseDict, key = "isCurEquip", value = "disable")
        $index = DictionaryGet(dict = $curHouseDict, key = "curHouseVarsIndex")
        DictionarySet(dict = $curHouseDict, key = "curHouseVarsIndex", value = nil)
        ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $curHouseDict))
        DictionarySet(dict = $newHouseDict, key = "isCurEquip", value = "enable")
        DictionarySet(dict = $newHouseDict, key = "houseSelectColor", value = $curSelectKey)
        $index = DictionaryGet(dict = $newHouseDict, key = "curHouseVarsIndex")
        DictionarySet(dict = $newHouseDict, key = "curHouseVarsIndex", value = nil)
        ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $newHouseDict))
        CallTrigger(object = $obj1, event = "CHANGE_HOUSE", 
            newHouse = DictionaryGet(dict = $newHouseDict, key = "houseCfg"), houseColor = $curSelectKey)
    }
    ShowGenericListDisplayBox(entity = $obj1, closeWin = true)
}

CHANGE_HOUSE_COLOR{ -- 换色
    $curHouse = GetObjectVar(obj = $obj1, key = "house")
    $oldBuff = GetEntityTypeBuff(entity = $curHouse, key = "fullName", value = $oldHouseColor)
    If($oldBuff){
        RemoveEntityBuff(entity = $curHouse, buff = $oldBuff)
    }
    AddEntityBuff(entity = $curHouse, cfg = $houseColor) -- 通过buff换skin

    -- 换色特效
    $effectBuff = GetEntityTypeBuff(entity = $curHouse, key = "fullName", value = GetConfigVar(obj = $house, key = "houseCreateBuff"))
    If($effectBuff){
        RemoveEntityBuff(entity = $curHouse, buff = $effectBuff)
    }
    AddEntityBuff(entity = $curHouse, cfg = GetConfigVar(obj = $curHouse, key = "houseCreateBuff"), buffTime = 200)
}

CHANGE_HOUSE{ -- 换房子/存档
    $region = GetObjectVar(obj = GetObjectVar(obj = $obj1, key = "house"), key = "region")
    $obj1.curHouseCfg = $newHouse
    ------------------------------------------ 改变房子(actor)，直接杀掉原来的，新建新的
    CallTrigger(object = $obj1, event = "REMOVE_HOUSE_OUTSIDE")
    ShowTip(entity = $obj1, tipType = 1, textKey = "house_change")

    CallTrigger(object = $obj1, event = "CREATE_HOUSE_OUTSIDE", region = $region, houseCfg = $newHouse, houseColor = $houseColor)
}



-------------------------------------- ui/btn/event
HOUSE_TAB_EVENT{
    If(not $key or "0" == $key){
        $listInfoTb = {}
        Foreach(array = GetConfigVar(obj = $obj1, key = "houseStore") or {}, key = "houseFullName"){
            $coinImg = ArrayGet(array = @CURRENT_ID_IMAGE, index = (GetEntityCfg(fullName = $houseFullName, key = "coinId") or 3) + 1)
                        or "set:jail_break.json image:jail_break_currency"
            ArrayAppend(array = $listInfoTb, value = {
                image = GetEntityCfg(fullName = $houseFullName, key = "defaultColorImage") or "",
                text = GetEntityCfg(fullName = $houseFullName, key = "houseType") or "",
                coinImg = $coinImg or "",
                btnShowText = GetEntityCfg(fullName = $houseFullName, key = "buyMoney") or 0,
                context = {houseCfg = $houseFullName}
            })
        }
        ShowGenericListDisplayBox(entity = $obj1, infoTb = {
            uiWin = "genericListDisplayBox3",
            childInfoTb = {
                listInfo = {
                    infoTb = $listInfoTb
                },
                btnInfo = {
                    btnText = "ui_house_buy_house",
                    event = "SURE_BUY_HOUSE"
                }
            }
        }, isOpenChild = true)
        Return()
    }
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars") or {}
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "houseId") == $key){
            $curSelectDict = $dict
            Break()
        }
    }
    -- $curSelectHouse = $houseVar
    If(not $curSelectDict){
        Return()
    }
    -- color
    $selectGvInfo = {}
    $worldAllColor = GetWorldVar(key = "allColor")
    $curHouseColor = GetEntityCfg(fullName = DictionaryGet(dict = $curSelectDict, key = "houseCfg"), key = "allColor")
    Foreach(array = $curHouseColor, key = "entity_color"){
        Foreach(array = $worldAllColor, key = "world_color"){
            If(ArrayGet(array = $entity_color, index = 1) == ArrayGet(array = $world_color, index = 1)){
                ArrayAppend(array = $selectGvInfo, value = {
                    rightImg = ArrayGet(array = $world_color, index = 3),
                    leftImg = ArrayGet(array = $entity_color, index = 2),
                    key = ArrayGet(array = $world_color, index = 2),
                    curSelect = ArrayGet(array = $world_color, index = 2) == DictionaryGet(dict = $curSelectDict, key = "houseSelectColor")
                        and true or false
                })
            }
        }
    } 
    ShowGenericListDisplayBox(entity = $obj1, infoTb = {
        uiWin = "genericListDisplayBox2",
        childInfoTb = {
            upperLayerKey = $key,
            titleTb = {text = DictionaryGet(dict = $curSelectDict, key = "houseName"), event = "EDIT_HOUSE_NAME", 
                context = {houseKey = DictionaryGet(dict = $curSelectDict, key = "houseId"), houseName = DictionaryGet(dict = $curSelectDict, key = "houseName")}},
            selectInfo = {
                selectTitleText = "ui_house_choice_color",
                selectButtomText = DictionaryGet(dict = $curSelectDict, key = "houseType"),
                selectGvInfo = $selectGvInfo
            },
            btnInfo = {
                btnType = 2,
                btnList = [
                    {text = "ui_house_sell_house", event = "SELL_HOUSE", context = { houseKey = $key }},
                    {text = "ui_house_use_house", event = "UPDATE_HOUSE", context = { houseKey = $key },
                        curSelect = DictionaryGet(dict = $curSelectDict, key = "isCurEquip") == "enable"}
                ] 
            }
        }
    }, isOpenChild = true)
}

EDIT_HOUSE_NAME{
    -- 房子改名
    If(not $titleEditText or not $upperLayerKey){
        Return()
    }
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "houseId") == $upperLayerKey){
            DictionarySet(dict = $dict, key = "houseName", value = $titleEditText)
            ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $dict))
            Break()
        }
    }
    $mail = GetObjectVar(obj = GetObjectVar(obj = $obj1, key = "house"), key = "mail")
    SetEntityName(entity = $mail, name = $titleEditText)
    ShowGenericListDisplayBox(entity = $obj1, closeWin = true)
    StartTimer(rep = false, time = 1, object = $obj1, obj1 = $obj1, mail = $mail, event = "UPDATE_HOUSE_TAB")
}

UPDATE_HOUSE_TAB{
    CastSkill(skill = "/click", from = $obj1, packet = {targetID = GetObjectID(object = $mail)})
}

SURE_BUY_HOUSE{
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars") or {}
    If(not $houseCfg or ArraySize(array = $houseVars) >= (GetConfigVar(obj = $obj1, key = "maxHouseVarsSize") or 8)){
        -- 买房子 成功则关闭界面，失败则不关闭 -- 达到房子上限也不给买
        ShowTip(entity = $obj1, tipType = 1, textKey = "ui_buy_failure")
        Return()
    }
    ------------- 花钱买房 -- 此处判断是否购买失败，失败直接返回
    $buyMoney = GetEntityCfg(fullName = $houseCfg, key = "buyMoney") or 0
    $coinId = GetEntityCfg(fullName = $houseCfg, key = "coinId")
    If(3 ~= $coinId){
        BuyGoods(player = $obj1, uniqueId = 20201001,  coinId = $coinId, price = $buyMoney, houseCfg = $houseCfg, event = "BUY_HOUSE_BY_APP" )
        Return()
    }
    If(not PayCurrency(entity = $obj1, coinName = "green_currency", count = $buyMoney)){
        ShowTip(entity = $obj1, tipType = 2,textKey = "buy_house_not_enough_money", time = 40)
        Return()
    }
    CallTrigger(object = $obj1, event = "GIVE_HOUSE", houseCfg = $houseCfg)
}

BUY_HOUSE_BY_APP{
    $dictValue = NewDictionary($response)
    $houseCfg = DictionaryGet(dict = $dictValue, key = "houseCfg")
    $obj1 = DictionaryGet(dict = $dictValue, key = "player")
    If(not $isSucceed){
        ShowTip(entity = $obj1, tipType = 2,textKey = "buy_house_not_enough_money", time = 40)
        Return()
    }
    CallTrigger(object = $obj1, event = "GIVE_HOUSE", houseCfg = $houseCfg)
}

GIVE_HOUSE{
    ------------- 给房子
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars") or {}
    $houseId = Concat(p1 = GetTime(), p2 = GetGameTime(), p3 = Random(min = 0, max = 999999))
    $curSelect = GetEntityCfg(fullName = $houseCfg, key = "defaultColor")
    ArrayAppend(array = $houseVars, value = { -- 购买成功后拼接一个新的房子数据放进entity的houseVars中 -- TEST CODE
            houseCfg = $houseCfg,
            houseSelectColor = $curSelect,
            houseInsideFurnishing = Copy(value = GetEntityCfg(fullName = $houseCfg, key = "houseDefaultFurniture") or []),
            houseInsideRegionColors = Copy(value = GetEntityCfg(fullName = $houseCfg, key = "houseDefaultInsideMapColorTb") or {}),
            isCurEquip = "disable",
            houseName = GetEntityCfg(fullName = $houseCfg, key = "houseDefaultName") or "",
            houseType = GetEntityCfg(fullName = $houseCfg, key = "houseType") or "",
            houseId = $houseId,
            houseInsideDoor = GetEntityCfg(fullName = $houseCfg, key = "insideDoor") or "myplugin/house_base_inside_door"
        }
    )
    ShowTip(entity = $obj1, tipType = 1, textKey = "ui_buy_success")
    ShowGenericListDisplayBox(entity = $obj1, closeWin = true)
    CallTrigger(object = $obj1, event = "UPDATE_HOUSE", houseKey = $houseId, curSelectKey = $curSelect)
}

SELL_HOUSE{
    $curSelectDict = nil
    Foreach(array = GetObjectVar(obj = $obj1, key = "houseVars") or {}, key = "houseVar"){
        $dict = NewDictionary($houseVar)
        If(DictionaryGet(dict = $dict, key = "houseId") == $houseKey){
            $curSelectDict = $dict
            Break()
        }
    }
    If(not $curSelectDict){
        Return()
    }
    -- showImage
    $worldAllColor = GetWorldVar(key = "allColor")
    $curHouseColor = GetEntityCfg(fullName = DictionaryGet(dict = $curSelectDict, key = "houseCfg"), key = "allColor")
    Foreach(array = $curHouseColor, key = "entity_color"){
        Foreach(array = $worldAllColor, key = "world_color"){
            If(ArrayGet(array = $world_color, index = 2) == DictionaryGet(dict = $curSelectDict, key = "houseSelectColor") and 
                ArrayGet(array = $world_color, index = 1) == ArrayGet(array = $entity_color, index = 1)){
                $showImage = ArrayGet(array = $entity_color, index = 2)
                Break()
            }
        }
    }
    $sellHouseMoney = 0
    $sellHouseCfg = DictionaryGet(dict = $curSelectDict, key = "houseCfg")
    $sellHouseMoney = $sellHouseMoney + GetEntityCfg(fullName = $sellHouseCfg, key = "sellMoney")
    $sellHouseFurnitures = DictionaryGet(dict = $curSelectDict, key = "houseInsideFurnishing") or {}
    Foreach(array = $sellHouseFurnitures, key = "furniture"){
        $fDict = NewDictionary($furniture)
        $cfg = DictionaryGet(dict = $fDict, key = "cfg")
        $sellMoney = GetEntityCfg(fullName = $cfg, key = "sellMoney")
        If(DictionaryGet(dict = $fDict, key = "isOldFurnishing")){
            $sellMoney = GetEntityCfg(fullName = $cfg, key = "sellByDiscountMoney")
        }
        $sellHouseMoney = $sellHouseMoney + $sellMoney
    }          
    -- showTextTb
    $showTextTb = {textWeightList = [
        {keyText = "ui_house_sell_house_money", valueText = $sellHouseMoney},
        {keyText = "ui_house_sell_house_type", valueText = DictionaryGet(dict = $curSelectDict, key = "houseType")}
    ]}
    ShowGenericListDisplayBox(entity = $obj1, infoTb = {
        uiWin = "genericShowPage",
        childInfoTb = {
            upperLayerKey = $houseKey,
            titleTb = {text = DictionaryGet(dict = $curSelectDict, key = "houseName"), event = "EDIT_HOUSE_NAME", 
                context = {houseKey = DictionaryGet(dict = $curSelectDict, key = "houseId"), houseName = DictionaryGet(dict = $curSelectDict, key = "houseName")}},
            showImage = $showImage,
            showTextTb = $showTextTb,
            btnInfo = {
                btnType = 2,
                btnList = [
                        {text = "ui_sure", event = "SELL_HOUSE_SURE", context = { houseKey = $houseKey}},
                        {text = "ui_cancel", event = "HOUSE_TAB_EVENT", context = { key = $houseKey}}
                    ]
            }
        }
    }, isOpenChild = true)
}

SELL_HOUSE_SURE{
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    If(ArraySize(array = $houseVars) <= 1){ -- 最后一套房不能出售
        ShowTip(entity = $obj1, tipType = 1, textKey = "ui_house_sell_cant_sell")
        Return()
    }
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "houseId") == $houseKey){
            $curSelectDict = $dict
            ArrayRemove(array = $houseVars, index = $index)
            Break()
        }
    }
    $newHouseVar = ArrayGet(array = $houseVars, index = 1)
    $newHouseDict = NewDictionary($newHouseVar)
    DictionarySet(dict = $newHouseDict, key = "isCurEquip", value = "enable")
    ArraySet(array = $houseVars, index = 1, value = DictionaryToTable(dict = $newHouseDict))
    CallTrigger(object = $obj1, event = "CHANGE_HOUSE", 
            newHouse = DictionaryGet(dict = $newHouseDict, key = "houseCfg"), houseColor = DictionaryGet(dict = $newHouseDict, key = "houseSelectColor"))

    -- 出售成功，给玩家钱
    $sellHouseCfg = DictionaryGet(dict = $curSelectDict, key = "houseCfg")
    AddCurrency(entity = $obj1, coinName = "green_currency", 
        count = GetEntityCfg(fullName = $sellHouseCfg, key = "sellMoney"))
    $sellHouseFurnitures = DictionaryGet(dict = $curSelectDict, key = "houseInsideFurnishing") or {}
    Foreach(array = $sellHouseFurnitures, key = "furniture"){
        $fDict = NewDictionary($furniture)
        $cfg = DictionaryGet(dict = $fDict, key = "cfg")
        $sellMoney = GetEntityCfg(fullName = $cfg, key = "sellMoney")
        If(DictionaryGet(dict = $fDict, key = "isOldFurnishing")){
            $sellMoney = GetEntityCfg(fullName = $cfg, key = "sellByDiscountMoney")
        }
        $coinName = GetEntityCfg(fullName = $cfg, key = "coinName")
        AddCurrency(entity = $obj1, coinName = $coinName, count = $sellMoney)
    }

    ShowGenericListDisplayBox(entity = $obj1, closeWin = true)
}

BUY_STORE{
    If(not $cfg){
        Return()
    }
    $buyMoney = GetEntityCfg(fullName = $cfg, key = "buyMoney")
    $coinId = GetEntityCfg(fullName = $cfg, key = "coinId")
    Foreach(array = @DECORATION_STORE, key = "decorationTb"){
        $decorationDic = NewDictionary($decorationTb)
        If(DictionaryGet(dict = $decorationDic, key = "fullName") == $cfg){
            $buyMoney = ToNumber(DictionaryGet(dict = $decorationDic, key = "buyMoney") or 0)
            $coinId = ToNumber(DictionaryGet(dict = $decorationDic, key = "coinId") or 3)
        }
    }
    If(3 ~= $coinId){
        BuyGoods(player = $obj1, uniqueId = 20201002,  coinId = $coinId, price = $buyMoney, cfg = $cfg, event = "BUY_STORE_BY_APP" )
        Return()
    }
    If(not PayCurrency(entity = $obj1, coinName = "green_currency", count = $buyMoney)){
        ShowTip(entity = $obj1, tipType = 2,textKey = "buy_store_not_enough_money", time = 40)
        Return()
    }
    CallTrigger(object = $obj1, event = "GIVE_STORE", cfg = $cfg)
}

BUY_STORE_BY_APP{
    $dictValue = NewDictionary($response)
    $cfg = DictionaryGet(dict = $dictValue, key = "cfg")
    $obj1 = DictionaryGet(dict = $dictValue, key = "player")
    If(not $isSucceed){
        ShowTip(entity = $obj1, tipType = 2,textKey = "buy_store_not_enough_money", time = 40)
        Return()
    }
    CallTrigger(object = $obj1, event = "GIVE_STORE", cfg = $cfg)
}

GIVE_STORE{
    ShowGenericActorShowStore(entity = $obj1, closeWin =true)
    
    $map = GetObjectVar(obj = GetObjectVar(obj = $obj1, key = "house"), key = "houseInsideMap")
    $npc = CreateNpc(pos = GetEntityPosition(entity = $obj1), map = $map, cfgName = $cfg, ry = 0, rp = 0, owner = $obj1)
    SetObjectVar(obj = $npc, key = "house", value = GetObjectVar(obj = $obj1, key = "house"))
    
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            DictionarySet(dict = $dict, key = "curHouseVarsIndex", value = $index)
            $curHousedict = $dict
            Break()
        }
    }
    ArrayAppend(array = DictionaryGet(dict = $curHousedict, key = "houseInsideFurnishing"), value = {
        pos = GetEntityPosition(entity = $npc), yaw = GetEntityYaw(entity = $npc),cfg = $cfg,
        furnishingId = GetObjectID(object = $npc),isOldFurnishing = false
    })
    $index = DictionaryGet(dict = $curHousedict, key = "curHouseVarsIndex")
    DictionarySet(dict = $curHousedict, key = "curHouseVarsIndex", value = nil)
    ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $curHousedict))

    -- StartTimer(rep = false, object =$obj1, obj1 =$obj1, npc = $npc, time = 1, event = "CLICK_BUY_STORE")
}

CLICK_BUY_STORE{
    CastSkill(skill = "/click", from = $obj1, packet = {targetID = GetObjectID(object = $npc)})
}

SAVE_FURNITURE{
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            DictionarySet(dict = $dict, key = "curHouseVarsIndex", value = $index)
            $curHousedict = $dict
            Break()
        }
    }
    $furnitures = DictionaryGet(dict = $curHousedict, key = "houseInsideFurnishing")
    ForLoop(from = 1, to = ArraySize(array = $furnitures or {}), key = "fIndex"){
        $fDict = NewDictionary(ArrayGet(array = $furnitures, index = $fIndex))
        $npc = GetObject(objID = DictionaryGet(dict = $fDict, key = "furnishingId"))
        If($npc){
            DictionarySet(dict = $fDict, key = "pos", value = GetEntityPosition(entity = $npc))
            DictionarySet(dict = $fDict, key = "yaw", value = GetEntityYaw(entity = $npc))
            DictionarySet(dict = $fDict, key = "pitch", value = GetEntityPitch(entity = $npc))
            -- 如果是衣架，有saveSkinList，则需要保存当前衣架的皮肤
            If(GetConfigVar(obj = $npc, key = "saveSkinList")){
                $saveSkinList = {}
                Foreach(array = GetConfigVar(obj = $npc, key = "saveSkinList"), key = "skinName"){
                    ArrayAppend(array = $skinList, value = {
                        skinName = $skinName,
                        skin = GetSkin(entity = $npc, skinName = $skinName)
                    })
                }
                DictionarySet(dict = $fDict, key = "saveSkinList", value = $saveSkinList)
            }
        }
        ArraySet(array = $furnitures, index = $fIndex, value = DictionaryToTable(dict = $fDict))
    }
    $index = DictionaryGet(dict = $curHousedict, key = "curHouseVarsIndex")
    DictionarySet(dict = $curHousedict, key = "curHouseVarsIndex", value = nil)
    ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $curHousedict))
}

SHOW_TITLE_BAR{
    If($obj1.inPartyOwnerId ~= 0){
        Return()--in party
    }
    $editBuff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = "myplugin/edit_house_buff")
    If($editBuff){
        RemoveEntityBuff(entity = $obj1, buff = $editBuff)
    }
    
    $lastInteractionUiObjId = GetObjectVar(obj = $obj1, key = "lastInteractionUiObjId")
    If($lastInteractionUiObjId){
        UpdateEntityEditContainer(player = $obj1, entityId = $lastInteractionUiObjId, show = false)
        SetObjectVar(obj = $obj2, key = "lastInteractionUiObjId", value = nil)
    }

    CallTrigger(object = $obj1, event = "SAVE_FURNITURE")

    $infoTb = {
        btnInfo = {
            btnList = [
                {text = "ui_edit", btnType = 5, event = "EDIT_HOUSE"},
                {text = "ui_lock", btnType = 2, event = "HOUSE_LOCK", 
                    curSelect = GetObjectVar(obj = GetObjectVar(obj = $obj1, key = "house"), key = "isLock")}
            ]
        }
    }

    ShowTitleBarPage(entity = $obj1, infoTb = {})
    ShowTitleBarPage(entity = $obj1, infoTb = $infoTb, updateWin = true)
}

EDIT_HOUSE{
    ShowGenericActorShowStore(entity = $obj1, closeWin =true)
    $editBuff = GetEntityTypeBuff(entity = $obj1, key = "fullName", value = "myplugin/edit_house_buff")
    If(not $editBuff){
        AddEntityBuff(entity = $obj1, cfg = "myplugin/edit_house_buff")
    }

    $infoTb = {
        btnInfo = {
            btnList = [
                {text = "ui_dec_shop", btnType = 1, event = "SHOW_DEC_STORE"},
                {text = "ui_cancel", btnType = 4, event = "SHOW_TITLE_BAR"}
            ]
        }
    }
    ShowTitleBarPage(entity = $obj1, infoTb = {})
    ShowTitleBarPage(entity = $obj1, infoTb = $infoTb, updateWin = true)
}

HOUSE_LOCK{
    $house = GetObjectVar(obj = $obj1, key = "house")
    $isLock = GetObjectVar(obj = $house, key = "isLock")
    SetObjectVar(obj = $house, key = "isLock", value = not $isLock) -- 给房门上锁
    ShowTip(entity = $obj1, tipType = 2,textKey = "house_lock", time = 40)
    If($isLock){ -- 上锁状态则重置为未上锁状态
        ShowTip(entity = $obj1, tipType = 2,textKey = "house_unlock", time = 40)
        CallTrigger(object = $obj1, event = "SHOW_TITLE_BAR") 
    }
}

UPDATE_EDIT_STORE_SELL_TAB{
    Foreach(array = GetConfigVar(obj = $obj1, key = "editStoreSellTab") or {}, key = "topTab"){
        $dic = NewDictionary($topTab)
        ArrayAppend(array = $btnListTb, value = {
            text = DictionaryGet(dict = $dic, key = "text"),
            curSelect = $titleKey == DictionaryGet(dict = $dic, key = "titleKey"),
            event = DictionaryGet(dict = $dic, key = "event"),
            context = {
                titleKey = DictionaryGet(dict = $dic, key = "titleKey"),
                nextCfgKey = DictionaryGet(dict = $dic, key = "nextCfgKey")
            }
        })
    }
}

SHOW_DEC_STORE{
    ShowGenericActorShowStore(entity = $obj1, infoTb = {})
    ShowTitleBarPage(entity = $obj1, closeWin = true)
    If(not $titleKey){
        $titleKey = "allDec"
    }
    If(not $nextCfgKey){
        $nextCfgKey = "decStoreList"
    }
    $btnListTb = {}
    CallTrigger(object = $obj1, event = "UPDATE_EDIT_STORE_SELL_TAB", titleKey = $titleKey, nextCfgKey = $nextCfgKey, btnListTb = $btnListTb) 

    $listInfoTb = {}
    $closeEvent = {}
    Foreach(array = GetConfigVar(obj = $obj1, key = $nextCfgKey) or {}, key = "bottomBtn"){
        $dic = NewDictionary($bottomBtn)
        If(DictionaryGet(dict = $dic, key = "nextCfgKey")){
            ArrayAppend(array = $listInfoTb, value = {
                text = DictionaryGet(dict = $dic, key = "text"),
                image = DictionaryGet(dict = $dic, key = "image"),
                event = DictionaryGet(dict = $dic, key = "event"),
                context = {
                    titleKey = DictionaryGet(dict = $dic, key = "titleKey"),
                    nextCfgKey = DictionaryGet(dict = $dic, key = "nextCfgKey"),
                    childRegionKey = DictionaryGet(dict = $dic, key = "childRegionKey")
                }
            })
        }
        If(DictionaryGet(dict = $dic, key = "closeEvent")){
            $closeEvent = {
                event = DictionaryGet(dict = $dic, key = "closeEvent"),
                context = DictionaryGet(dict = $dic, key = "context")
            }
        }
    }

    $infoTb = {
        btnInfo = {
            btnType = 3,
            btnList = $btnListTb
        },
        listInfo = {
            uiType = 3,
            infoTb = $listInfoTb
        },
        eventInfo = {
            closeEventInfo = $closeEvent
        }
    }
    StartTimer(object =$obj1, obj1 =$obj1, infoTb = $infoTb, time = 1, event = "UPDATE_ACTOR_SHOW_STORE")
}

UPDATE_ACTOR_SHOW_STORE{
    ShowGenericActorShowStore(entity = $obj1, infoTb = $infoTb, updateWin =true)
}

UPDATE_STORE_LIST{
    $dicInfoTb = NewDictionary(GetConfigVar(obj = $obj1, key = $nextCfgKey) or {})
    If(DictionaryGet(dict = $dicInfoTb, key = "storeInfo")){
        $dic = NewDictionary(DictionaryGet(dict = $dicInfoTb, key = "storeInfo"))
        $storeId = DictionaryGet(dict = $dic, key = "storeId")
        Foreach(array = @DECORATION_STORE, key = "decorationTb"){
            $decorationDic = NewDictionary($decorationTb)
            $type = DictionaryGet(dict = $decorationDic, key = "type")
            If(DictionaryGet(dict = $decorationDic, key = "storeId") == $storeId and "entity" == $type){
                $fullName = DictionaryGet(dict = $decorationDic, key = "fullName")
                ArrayAppend(array = $listInfoTb, value = {
                    text = DictionaryGet(dict = $decorationDic, key = "buyMoney") or GetEntityCfg(fullName = $fullName, key = "buyMoney") or "",
                    image = DictionaryGet(dict = $decorationDic, key = "image") or GetEntityCfg(fullName = $fullName, key = "image") or "",
                    actorScale = DictionaryGet(dict = $decorationDic, key = "showActorScale") or GetEntityCfg(fullName = $fullName, key = "showActorScale") or 1,
                    coinImg = ArrayGet(array = @CURRENT_ID_IMAGE, 
                        index = (DictionaryGet(dict = $decorationDic, key = "coinId") or GetEntityCfg(fullName = $fullName, key = "coinId") or 3) + 1) 
                        or "set:jail_break.json image:jail_break_currency",
                    cfg = $fullName,
                    showName = DictionaryGet(dict = $decorationDic, key = "showName"),
                    showDesc = DictionaryGet(dict = $decorationDic, key = "showDesc"),
                    event = DictionaryGet(dict = $dic, key = "event") or "BUY_STORE",
                    context = {
                        cfg = $fullName
                    }
                })
            }
            If(DictionaryGet(dict = $decorationDic, key = "storeId") == $storeId and "block" == $type){
                $fullName = DictionaryGet(dict = $decorationDic, key = "fullName")
                ArrayAppend(array = $listInfoTb, value = {
                    text = DictionaryGet(dict = $decorationDic, key = "buyMoney") or GetBlockConfigByName(block = $fullName, key = "buyMoney") or 0,
                    image = DictionaryGet(dict = $decorationDic, key = "image") or GetBlockConfigByName(block = $fullName, key = "image") or "",
                    coinImg = ArrayGet(array = @CURRENT_ID_IMAGE, 
                        index = (DictionaryGet(dict = $decorationDic, key = "coinId") or GetBlockConfigByName(block = $fullName, key = "coinId") or 3) + 1) 
                        or "set:jail_break.json image:jail_break_currency",
                    event = "BUY_HOUSE_REGION_COLOR",
                    context = {
                        colorBlock = $fullName,
                        childRegionKey = $childRegionKey
                    }
                })
            }
        }
    }
    If(DictionaryGet(dict = $dicInfoTb, key = "closeInfo")){
        $dic = NewDictionary(DictionaryGet(dict = $dicInfoTb, key = "closeInfo"))
        ArrayAppend(array = $closeEvent, value = {
            event = DictionaryGet(dict = $dic, key = "closeEvent"),
            context = DictionaryGet(dict = $dic, key = "context")
        })
    }
    If(DictionaryGet(dict = $dicInfoTb, key = "rightInfo")){
        $dic = NewDictionary(DictionaryGet(dict = $dicInfoTb, key = "rightInfo"))
        ArrayAppend(array = $rightEvent, value = {
            event = DictionaryGet(dict = $dic, key = "rightEvent"),
            context = DictionaryGet(dict = $dic, key = "context")
        })
    }
    If(DictionaryGet(dict = $dicInfoTb, key = "leftInfo")){
        $dic = NewDictionary(DictionaryGet(dict = $dicInfoTb, key = "leftInfo"))
        ArrayAppend(array = $leftEvent, value = {
            event = DictionaryGet(dict = $dic, key = "leftEvent"),
            context = DictionaryGet(dict = $dic, key = "context")
        })
    }
}

SHOW_DEC_STORE_LIST{
    ShowGenericActorShowStore(entity = $obj1, infoTb = {})
    ShowTitleBarPage(entity = $obj1, closeWin = true)
    If(not $titleKey){
        $titleKey = "allDec"
    }
    If(not $nextCfgKey){
        $nextCfgKey = "kitchenStore"
    }
    $btnListTb = {}
    CallTrigger(object = $obj1, event = "UPDATE_EDIT_STORE_SELL_TAB", titleKey = $titleKey, nextCfgKey = $nextCfgKey, btnListTb = $btnListTb) 

    $listInfoTb = {}
    $closeEvent = {}
    $leftEvent = {}
    $rightEvent = {}
    CallTrigger(object = $obj1, event = "UPDATE_STORE_LIST", nextCfgKey = $nextCfgKey, childRegionKey = $childRegionKey,
        listInfoTb = $listInfoTb, closeEvent = $closeEvent, leftEvent = $leftEvent, rightEvent = $rightEvent) 
    $infoTb = {
        btnInfo = {
            btnType = 3,
            btnList = $btnListTb
        },
        listInfo = {
            uiType = 2,
            infoTb = $listInfoTb
        },
        eventInfo = {
            closeEventInfo = ArrayGet(array = $closeEvent, index = 1),
            leftEventInfo = ArrayGet(array = $leftEvent, index = 1),
            rightEventInfo = ArrayGet(array = $rightEvent, index = 1)
        }
    }
    StartTimer(object =$obj1, obj1 =$obj1, infoTb = $infoTb, time = 1, event = "UPDATE_ACTOR_SHOW_STORE")
}

BUY_INSIDE_DOOR{
    If(not $cfg){
        Return()
    }
    $buyMoney = GetEntityCfg(fullName = $cfg, key = "buyMoney")
    $coinId = GetEntityCfg(fullName = $cfg, key = "coinId")
    Foreach(array = @DECORATION_STORE, key = "decorationTb"){
        $decorationDic = NewDictionary($decorationTb)
        If(DictionaryGet(dict = $decorationDic, key = "fullName") == $cfg){
            $buyMoney = ToNumber(DictionaryGet(dict = $decorationDic, key = "buyMoney") or 0)
            $coinId = ToNumber(DictionaryGet(dict = $decorationDic, key = "coinId") or 3)
        }
    }
    If(3 ~= $coinId){
        BuyGoods(player = $obj1, uniqueId = 20201003,  coinId = $coinId, price = $buyMoney, cfg = $cfg, event = "BUY_DOOR_BY_APP" )
        Return()
    }
    If(not PayCurrency(entity = $obj1, coinName = "green_currency", count = $buyMoney)){
        ShowTip(entity = $obj1, tipType = 2,textKey = "buy_store_not_enough_money", time = 40)
        Return()
    }
    CallTrigger(object = $obj1, event = "GIVE_DOOR", cfg = $cfg)
}

BUY_DOOR_BY_APP{
    $dictValue = NewDictionary($response)
    $cfg = DictionaryGet(dict = $dictValue, key = "cfg")
    $obj1 = DictionaryGet(dict = $dictValue, key = "player")
    If(not $isSucceed){
        ShowTip(entity = $obj1, tipType = 2,textKey = "buy_store_not_enough_money", time = 40)
        Return()
    }
    CallTrigger(object = $obj1, event = "GIVE_DOOR", cfg = $cfg)
}

GIVE_DOOR{
    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            DictionarySet(dict = $dict, key = "houseInsideDoor", value = $cfg)
            ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $dict))
            Break()
        }
    }
    $house = GetObjectVar(obj = $obj1, key = "house")
    $insideMap = GetObjectVar(obj = $house, key = "houseInsideMap")

    $houseInsideDoor = GetObjectVar(obj = $house, key = "insideDoor")
    KillEntity(entity = $houseInsideDoor)
    CallTrigger(object = $house, event = "CREATE_INSIDE_DOOR", map = $insideMap, cfgName = $cfg)
}

BUY_HOUSE_REGION_COLOR{
    -- 如果换了墙纸或者地板，原有的不保留，当你没买     如果你原来买了A墙纸，你又换成了B，那么你要再花一遍钱去把A换回来
    $curInsideRegion = GetObjectVar(obj = $obj1, key = "curInsideRegion")
    If(not $curInsideRegion){
        Return()
    }

    If($colorBlock){
        $buyMoney = GetBlockConfigByName(block = $colorBlock, key = "buyMoney")
        $coinId = GetBlockConfigByName(block = $colorBlock, key = "coinId")
        Foreach(array = @DECORATION_STORE, key = "decorationTb"){
            $decorationDic = NewDictionary($decorationTb)
            If(DictionaryGet(dict = $decorationDic, key = "fullName") == $colorBlock){
                $buyMoney = ToNumber(DictionaryGet(dict = $decorationDic, key = "buyMoney") or 0)
                $coinId = ToNumber(DictionaryGet(dict = $decorationDic, key = "coinId") or 3)
            }
        }
        If(3 ~= $coinId){
            BuyGoods(player = $obj1, uniqueId = 20201004, coinId = $coinId, price = $buyMoney, 
                childRegionKey = $childRegionKey, colorBlock = $colorBlock, event = "BUY_REGION_COLOR_BY_APP" )
            Return()
        }

        If(not PayCurrency(entity = $obj1, coinName = "green_currency", count = $buyMoney)){
            ShowTip(entity = $obj1, tipType = 2,textKey = "buy_house_region_color_not_enough_money", time = 40)
            Return()
        }
    }
    
    CallTrigger(object = $obj1, event = "SET_REGION_COLOR", childRegionKey = $childRegionKey, colorBlock = $colorBlock)
}

BUY_REGION_COLOR_BY_APP{
    $dictValue = NewDictionary($response)
    $childRegionKey = DictionaryGet(dict = $dictValue, key = "childRegionKey")
    $colorBlock = DictionaryGet(dict = $dictValue, key = "colorBlock")
    $obj1 = DictionaryGet(dict = $dictValue, key = "player")
    If(not $isSucceed){
        ShowTip(entity = $obj1, tipType = 2,textKey = "buy_house_region_color_not_enough_money", time = 40)
        Return()
    }
    CallTrigger(object = $obj1, event = "SET_REGION_COLOR", childRegionKey = $childRegionKey, colorBlock = $colorBlock)
}

SET_REGION_COLOR{
    $curInsideRegion = GetObjectVar(obj = $obj1, key = "curInsideRegion")
    If(not $curInsideRegion){
        Return()
    }
    $curInsideRegionKey = GetRegionConfig(region = $curInsideRegion, key = "regionkey")
    $curInsideRegionChildReionArr = GetRegionConfig(region = $curInsideRegion, key = "childRegionKey")
    ForLoop(from = 1, to = ArraySize(array = $curInsideRegionChildReionArr), key = "index"){
        If(ArrayGet(array = $curInsideRegionChildReionArr, index = $index) == $childRegionKey){
            $curInsideRegionChildReionIndex = $index
            Break()
        }
    }
    If(not $curInsideRegionChildReionIndex){
        Return()
    }
    $childRegionArr = GetRegionConfig(region = $curInsideRegion, key = $childRegionKey) or {}
    
    $house = GetObjectVar(obj = $obj1, key = "house")
    $insideMap = GetObjectVar(obj = $house, key = "houseInsideMap")
    $destBlock = $colorBlock or GetRegionConfig(region = $curInsideRegion, key = "defaultBlock")
    -- Foreach(array = $childRegionArr, key = "cRegion"){
        -- CallTrigger(object = $house, event = "CHANGE_HOUSE_REGION_COLOR", map = $insideMap, region = $cRegion, destBlock = $destBlock)
    -- }
    CallTrigger(object = $house, event = "CHANGE_HOUSE_REGION_COLOR_BY_ALL_REGION", map = $insideMap, 
        childRegionKey = $childRegionKey, childRegionArr = $childRegionArr, destBlock = $destBlock)

    $houseVars = GetObjectVar(obj = $obj1, key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            DictionarySet(dict = $dict, key = "curHouseVarsIndex", value = $index)
            $curHousedict = $dict
            Break()
        }
    }
    $curSelectHouseRegionColor = DictionaryGet(dict = $curHousedict, key = "houseInsideRegionColors") or {}
    $hadRegion = false
    Foreach(array = $curSelectHouseRegionColor, key = "childRegionColorArr"){
        If(ArrayGet(array = $childRegionColorArr, index = 1) == $curInsideRegionKey){
            ArraySet(array = ArrayGet(array = $childRegionColorArr, index = 2), index = $curInsideRegionChildReionIndex, value = $colorBlock)
            $hadRegion = true
            Break()
        }
    }
    If(not $hadRegion){
        $defaultChildRegionBlock = Copy(value = GetRegionConfig(region = $curInsideRegion, key = "defaultChildRegionBlock"))
        ArraySet(array = $defaultChildRegionBlock, index = $curInsideRegionChildReionIndex, value = $colorBlock)
        $regionkey = GetRegionConfig(region = $curInsideRegion, key = "regionkey")
        ArrayAppend(array = $curSelectHouseRegionColor, value = [$regionkey,$defaultChildRegionBlock])
    }
    $index = DictionaryGet(dict = $curHousedict, key = "curHouseVarsIndex")
    DictionarySet(dict = $curHousedict, key = "curHouseVarsIndex", value = nil)
    ArraySet(array = $houseVars, index = $index, value = DictionaryToTable(dict = $curHousedict))
}
------------------------------------------------------------------------------------------------------------------------------ 分割线

ADD_MONEY
{  
    $today = GetYearDayStr(time = GetTime())

    If($today ~= $obj1.rewardDay)
    {
        $obj1.rewardCounts = 1
        $obj1.rewardDay = $today
    }

    $count = $obj1.rewardCounts
    $arraySize = ArraySize(array = @CDReward)
    If($count > $arraySize)
    {
        $count = $arraySize
    }
    $rewardArray = ArrayGet(array = @CDReward, index = $count)
    $cdTime = ArrayGet(array = $rewardArray, index = 1)
    $money = ArrayGet(array = $rewardArray, index = 2)

    If(ArrayFind(array = $obj1.privilege, value = "myplugin/vip_privilege"))
    {
        $money = $money * 2
    }

    If($obj1.isReadyReward)
    {
        AddCurrency(count = $money, coinName = "green_currency", entity = $obj1)
        ShowRewardRollTip(image = "set:jail_break.json image:jail_break_currency", text = Concat(p1 = "+", p2 = $money), entity = $obj1)
        $obj1.rewardCounts = $obj1.rewardCounts + 1
    }
    StartTimer(rep = false, time = $cdTime, object = $obj1, event = "ADD_MONEY")
    ShowRewardCD(player = $obj1, time = $cdTime)
    $obj1.isReadyReward = true
}

FUNC_STATE_GENERATE --params: stateBase, col, skill
{
    $state = $stateBase
    $subdivided = GetSkillVar(fullName = $skill, key = "subdivided")
    If(ArraySize(array = $subdivided or []) > 0)
    {
        $preState = GetObjectVar(obj = $obj1, key = Concat(p1 = "pre", p2 = $stateBase))
        If($preState)
        {
            RemoveSkill(entity = $obj1, name = Concat(p1 = @skillPath, p2 = $preState))
            CallTrigger(object = $obj1, event = "FUNC_STATE_RESET_DATA", state = $preState)
        }
        $state = ArrayGet(array = $subdivided, index = $col)
        SetObjectVar(obj = $obj1, value = $state, key = Concat(p1 = "pre", p2 = $stateBase))
        SetObjectVar(obj = $obj1, value = $col, key = Concat(p1 = $stateBase, p2 = "Idx"))
    }
    ShowBubbleMsg(
        entity = $obj1,
        contents = GetSkillVar(fullName = Concat(p1 = @skillPath, p2 = $state), key = "tipContents")
    )
    CallTrigger(object = $obj1, event = "FUNC_STATE_ADD", state = $state)
}

FUNC_STATE_ADD --params: state
{
    $stateBase = GetSkillVar(fullName = Concat(p1 = @skillPath, p2 = $state), key = "stateBase") or $state
    SetObjectVar(obj = $obj1, key = Concat(p1 = "had", p2 = $stateBase), value = true)
    SetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "InteractList"), value = nil)

    AddSkill(entity = $obj1, name = Concat(p1 = @skillPath, p2 = $state))
    CallTrigger(object = $obj1, event = "FUNC_STATE_RESET_DATA", state = $state)
    CallTrigger(object = $obj1, event = "FUNC_UPDATE_STATE_SKILL_JACK_AREA")
    CallTrigger(object = $obj1, event = "FUNC_STATE_ACTION", state = $state)
}

FUNC_STATE_ADD_EFFECT --params: state
{
    $effect = GetSkillVar(fullName = Concat(p1 = @skillPath, p2 = $state), key = "effect")
    If(not $effect)
    {
        Return()
    }
    SetObjectVar(
        obj = $obj1,
        key = Concat(p1 = $state, p2 = "Effect"),
        value = AddEntityBuff(
            entity = $obj1,
            cfg = $effect
        )
    )
}

FUNC_STATE_START_AFFECT --params: state
{
    $skill = Concat(p1 = @skillPath, p2 = $state)
    $timeSpan = GetSkillVar(fullName = $skill, key = "affectTimeSpanSec")
    If(not $timeSpan || $timeSpan <= 0)
    {
        Return()
    }

    SetObjectVar(
        obj = $obj1,
        key = $timerStr,
        value = StartTimer(
            rep = true,
            object = $obj1,
            state = $state,
            event = "FUNC_STATE_AFFECT_OTHER",
            time = ($timeSpan * 20) // 1
        )
    )
}

FUNC_STATE_AFFECT_OTHER --params: state
{
    $skill = Concat(p1 = @skillPath, p2 = $state)
    $stateBase = GetSkillVar(fullName = $skill, key = "stateBase") or $state

    $entities = GetNearbyEntities(
        entity = $obj1,
        distance = GetSkillVar(
            fullName = $skill,
            key = "affectDis"
        )
    )
    $name = GetEntityName(entity = $obj1)
    Foreach(array = $entities or [], key = "entity")
    {
        If(not IsPlayer(entity = $entity) || $entity == $obj1 || GetObjectVar(obj = $entity, key = Concat(p1 = "had", p2 = $stateBase)))
        {
            Continue()
        }
        ShowBubbleMsg(entity = $entity, textKey = "infect_sb", textP1 = $name)
        CallTrigger(
            object = $entity,
            event = "FUNC_STATE_ADD",
            state = $state
        )
    }
}

FUNC_STATE_RIDEON --params: ride[, isCancel = false]
{
    If(not $ride)
    {
        Return()
    }

    $state = GetConfigVar(obj = $ride, key = "releaseState")
    If(not $state)
    {
        Return()
    }
    If(not $isCancel)
    {
        CallTrigger(object = $obj1, event = "FUNC_STATE_RELEASE", state = $state)
    }
    Else
    {
        CallTrigger(object = $obj1, event = "FUNC_STATE_RELEASE_SUSPEND", state = $state)
    }
}

FUNC_STATE_CHECK_MAP --params: state
{
    $map = GetEntityMap(entity = $obj1)
    CallTrigger(
        object = $obj1,
        event = "FUNC_STATE_RELEASE",
        state = $map.cfg.releaseState
    )
}

FUNC_STATE_CHECK_REGION --params: state
{   
    -- 判断状态生成时是否就已经在区域里面了
    $reg = GetRegionByName(
        map = GetEntityMap(entity = $obj1),
        name = $state 
    )

    If($reg && IfEntityInArea(
                    entity = $obj1,
                    region = {
                        min = GetRegionMin(region = $reg),
                        max = GetRegionMax(region = $reg) + V3(1, 1, 1)
                    }
                )
    )
    {
        CallTrigger(
            object = $obj1,
            state = $state,
            event = "FUNC_REGION_STATE_OPERATION",
        )
    }
}

FUNC_REGION_STATE_OPERATION --parmas: opt, state
{
    $gpsDict = GetObjectVar(obj = $obj1, key = "gpsDict")
    If($gpsDict)
    {
        $dictDestination = DictionaryGet(dict = $gpsDict, key = "destination")
        If($state == $dictDestination)
        {
            ShowTip(entity = $obj1, tipType = 3, textKey = "you_came")
            CallTrigger(object = $obj1, event = "FUNC_CLOSE_GPS")
        }
    }

    CallTrigger(
        object = $obj1,
        state = $state,
        event = $opt ~= "LEAVE" and "FUNC_STATE_RELEASE" or "FUNC_STATE_RELEASE_SUSPEND"
    )
}

FUNC_STATE_SUSPEND_PROGRESS --params: state, stateBase, duration
{
    CallTrigger(
        object = $obj1,
        pgName = $state,
        event = "FUNC_CLOSE_PROGRESS_FOLLOW_OBJ"
    )

    CallTrigger(
        object = $obj1,
        event = "FUNC_STOP_TIMER",
        timerStr = Concat(p1 = $stateBase, p2 = "_TIMER")
    )

    $sTime = GetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "STime"))
    $usedTime = GetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "UsedTime"))
    If($sTime && $usedTime)
    {
        SetObjectVar(
            obj = $obj1,
            key = Concat(p1 = $stateBase, p2 = "UsedTime"),
            value = GetTime() - $sTime + $usedTime
        )
    }
}

FUNC_STATE_START_PROGRESS --params: state, stateBase, duration
{
    $usedTime = GetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "UsedTime")) or 0

    SetObjectVar(obj = $obj1, value = GetTime(), key = Concat(p1 = $stateBase, p2 = "STime"))
    SetObjectVar(obj = $obj1, value = $usedTime, key = Concat(p1 = $stateBase, p2 = "UsedTime"))

    ShowProgressFollowObj(
        isOpen = true,
        pgName = $state,
        entity = $obj1,
        totalTime = $duration * 20,
        usedTime = $usedTime * 20,
    )

    SetObjectVar(
        obj = $obj1,
        key = "progress",
        value = $state
    )

    SetObjectVar(
        obj = $obj1,
        key = Concat(p1 = $stateBase, p2 = "_TIMER"),
        value = StartTimer(
            rep = false,
            object =$obj1,
            state = $state,
            time = (($duration - $usedTime) * 20) // 1,
            event = "FUNC_STATE_REWARD"
        )
    )
}

FUNC_STATE_REWARD --state [, stateBase, skill]
{
    $skill = $skill or Concat(p1 = @skillPath, p2 = $state)
    $stateBase = $stateBase or GetSkillVar(fullName = $skill, key = "stateBase") or $state
    $hadState = Concat(p1 = "had", p2 = $stateBase)
    If(not GetObjectVar(obj = $obj1, key = $hadState))
    {
        Return()
    }

    RemoveSkill(entity = $obj1, name = $skill)
    SetObjectVar(obj = $obj1, key = Concat(p1 = "had", p2 = $stateBase), value = false)
    CallTrigger(object = $obj1, event = "FUNC_STATEBASE_RESET_DATA", stateBase = $stateBase)
    CallTrigger(object = $obj1, event = "FUNC_STATE_RESET_DATA", state = $state)

    $interactList = GetObjectVar(
        obj = $obj1,
        key = Concat(p1 = $stateBase, p2 = "InteractList")
    )

    If(not GetSkillVar(fullName = $skill, key = "hadReward"))
    {
        Return()
    }

    $rewardSelf = GetSkillVar(fullName = $skill, key = "rewardSelf")
    $rewardOther = GetSkillVar(fullName = $skill, key = "rewardOther")
    $rewardFamily = GetSkillVar(fullName = $skill, key = "rewardFamily")
    $rewardDis = GetSkillVar(fullName = $skill, key = "rewardDis")
    $rewardCount = GetSkillVar(fullName = $skill, key = "rewardCount")
    $rewardType = GetSkillVar(fullName = $skill, key = "rewardType")
    
    $teamId = GetEntityTeam(entity = $obj1)
    $rewardSum = 0
    $textKey = ""
    Foreach(array = $interactList or [], key = "objID")
    {
        $entity = GetObject(objID = $objID)
        If(not IsPlayer(entity = $entity))
        {
            Continue()
        }
        
        $countKey = Concat(p1 = $stateBase, p2 = "RewardCount")
        $count = GetObjectVar(obj = $entity, key = $countKey)
        $dis = GetObjectDistance(obj1 = $obj1, obj2 = $entity)
        $myTeamId =  GetEntityTeam(entity = $entity)
    
        If($count < $rewardCount && $dis < $rewardDis)
        {
            SetObjectVar(obj = $entity, key = $countKey, value = $count + 1)
            $isSameFamily = $teamId ~= 0 && $teamId == $myTeamId
            $rewardSum = $isSameFamily and $rewardFamily or $rewardOther
            $textKey = $isSameFamily and "reward_family" or "reward_takeCare"
            CallTrigger(
                object = $entity,
                event = "FUNC_STATE_REWARD_AND_TIP",
                rewardSum = $rewardSum,
                textKey = $textKey,
                rewardType = $rewardType
            )
        }
    }
    CallTrigger(
        object = $obj1,
        event = "FUNC_STATE_REWARD_AND_TIP",
        rewardSum = $rewardSelf,
        textKey = "reward_takeCare",
        rewardType = $rewardType
    )
}

FUNC_STATE_RELEASE --params: state[, duration]
{
    If(not $state)
    {
        Return()
    }

    $skill = Concat(p1 = @skillPath, p2 = $state)
    $stateBase = GetSkillVar(fullName = $skill, key = "stateBase") or $state
    $hadState = Concat(p1 = "had", p2 = $stateBase)
    $releasingState = Concat(p1 = "releasing", p2 = $stateBase)
    If(GetObjectVar(obj = $obj1, key = $hadState) && not GetObjectVar(obj = $obj1, key = $releasingState))
    {
        $subdivided = GetSkillVar(fullName = $skill, key = "subdivided")
        If(ArraySize(array = $subdivided or []) > 0)
        {
            $kind = ArrayGet(
                array = $subdivided,
                index = GetObjectVar(
                    obj = $obj1,
                    key = Concat(p1 = $stateBase, p2 = "Idx")
                )
            )
            If($kind ~= $state)
            {
                Return()
            }
        }

        SetObjectVar(obj = $obj1, key = $releasingState, value = true)
        $duration = $duration or GetSkillVar(fullName = $skill, key = "duration")
        If($duration && $duration > 0)
        {
            CallTrigger(object = $obj1, event = "FUNC_STATE_START_PROGRESS", state = $state, stateBase = $stateBase, duration = $duration)
        }
        Else
        {
            CallTrigger(object = $obj1, event = "FUNC_STATE_REWARD", state = $state, stateBase = $stateBase, skill = $skill)
        }
    }
}

FUNC_STATE_RELEASE_SUSPEND --params: state
{
    If(not $state)
    {
        Return()
    }

    $skill = Concat(p1 = @skillPath, p2 = $state)
    $stateBase = GetSkillVar(fullName = $skill, key = "stateBase") or $state
    $hadState = Concat(p1 = "had", p2 = $stateBase)
    $releasingState = Concat(p1 = "releasing", p2 = $stateBase)
    If(GetObjectVar(obj = $obj1, key = $hadState) && GetObjectVar(obj = $obj1, key = $releasingState))
    {
        SetObjectVar(obj = $obj1, key = $releasingState, value = false)
        $duration = GetSkillVar(fullName = $skill, key = "duration")
        If($duration && $duration > 0)
        {
            CallTrigger(object = $obj1, event = "FUNC_STATE_SUSPEND_PROGRESS", state = $state, stateBase = $stateBase, duration = $duration)
        }   
    }
}

FUNC_STATE_REWARD_AND_TIP --params: rewardSum, textKey, rewardType
{
    AddCurrency(coinName = $rewardType or "green_currency", entity = $obj1, count = $rewardSum)
    
    ShowRewardRollTip(
        entity = $obj1,
        image = "set:jail_break.json image:jail_break_currency",
        text = Concat(p1 = "+", p2 = $rewardSum),
    )

    ShowTip(
        entity = $obj1,
        tipType = 3,
        textKey = $textKey
    )
}

FUNC_STATE_SHOW_DETAILS_UI -- params: skill(fullName)
{
    $state = GetSkillVar(fullName = $skill, key = "state")
    $stateBase = GetSkillVar(fullName = $skill, key = "stateBase") or $state
    $duration = GetSkillVar(fullName = $skill, key = "duration")

    $usedTime = GetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "UsedTime")) or 0
    $percent = (not $duration or $duration <= 0) and 0 or NumberFloor(p1 = 100 * $usedTime / $duration)
    
    ShowDetails(
        entity = $obj1,
        fullName = Concat(p1 = "myplugin/", p2 = $state, p3 = "Detail"),
        isOpen = true,
        contents = {
            isNeedUpdate = $state == GetObjectVar(obj = $obj1, key = "progress"),
            subtitleVal = Concat(p1 = $percent, p2 = "%"),
            commentsVal = GetSkillVar(fullName = $skill, key = "rewardSelf"),
            commentsCurrencyIcon = GetSkillVar(fullName = $skill, key = "rewardType")
        }
    )
    
    SetObjectVar(obj = $obj1, key = "detailsUI", value = $state)
}

FUNC_STATE_OPEN_GPS --params: rtVal
{
    If(not $rtVal)
    {
        Return()
    }

    CallTrigger(
        object = $obj1,
        event = "FUNC_CLOSE_GPS"
    )

    $reg = Concat(p1 = "myplugin/region_state_", p2 = $rtVal)
    $gpsDict = NewDictionary(GetConfigValue(type = "region", fullName = $reg, key = "gpsDict")) 

    CallTrigger(
        object = $obj1,
        gpsDict = $gpsDict,
        event = "FUNC_GPS_SET_OR_SYNC"
    )
}

FUNC_GPS_SET_OR_SYNC --params: gpsDict
{
    If(not $gpsDict || DictionarySize(dict = $gpsDict) == 0)
    {
        Return()
    }
    
    $type = DictionaryGet(dict = $gpsDict, key = "type")
    If(not $type)
    {
        Return()
    }
    
    $belongMap = DictionaryGet(dict = $gpsDict, key = "belongMap")
    $pos = DictionaryGet(dict = $gpsDict, key = "pos")
    
    $conA = (($type == "map" || $type == "selfHouse" || $type == "region") && not $belongMap)
    $conB = $type == "entity" && (not $belongMap || not $pos)
    If($conA || $conB)
    {
        Return()
    }
    
    $curMapEntity = GetEntityMap(entity = $obj1)
    $curMap = GetEntityName(entity = $curMapEntity)
    $gps = { x = 0, y = 0, z = 0 }
    
    $owner = GetMapVar(map = $curMapEntity, key = "owner")
    $house = GetObjectVar(obj = $obj1, key = "house")
    If($owner)
    {
        $door = GetObjectVar(obj = $owner, key = "insideDoor")
        Test("type = ", $type)
        If($type == "selfHouse" && $curMapEntity == GetConfigVar(obj = $house, key = "houseInsideMap"))
        {
            ShowTip(entity = $obj1, tipType = 3, textKey = "you_came")
            CallTrigger(object = $obj1, event = "FUNC_CLOSE_GPS")
            Return()
        }
        $gps = GetEntityPosition(entity = $door)
    }
    ElseIf($curMap == $belongMap)
    {
        If($type == "map")
        {
            ShowTip(entity = $obj1, tipType = 3, textKey = "you_came")
            CallTrigger(object = $obj1, event = "FUNC_CLOSE_GPS")
            Return()
        }
        ElseIf($type == "region")
        {
            $region = GetRegionByName(map = $map, name = DictionaryGet(dict = $gpsDict, key = "regionName"))
            If($region)
            {
                $min = GetRegionMin(region = $region)
                $max = GetRegionMax(region = $region)
                $tmpReg = {
                    min = $min,
                    max = $max + V3(1, 1, 1)
                }
                If(IfEntityInArea(entity = $obj1, region = $tmpReg))
                {
                    ShowTip(entity = $obj1, tipType = 3, textKey = "you_came")
                    CallTrigger(object = $obj1, event = "FUNC_CLOSE_GPS")
                    Return()
                }
                $center = GetRegionCenter(region = $region)
                $gps = V3(GetPosX(pos = $center), GetPosY(pos = $min), GetPosZ(pos = $center))
            }
        }
        ElseIf($type == "entity")
        {
            $gps = DictionaryGet(dict = $gpsDict, key = "pos")
        }
        ElseIf($type == "selfHouse")
        {
            $housePos = GetEntityPosition(entity = $house)
            $gps = ComputeV3(p1 = $housePos, p2 = GetConfigVar(obj = $house, key = "doorPos") or {x = 0,y = 2, z = 0}, op = "add")
        }
    }
    Else
    {
        $road = []
        $maps = []
        $roadIdx = 1

        $ngt = DictionaryGet(dict = $gpsDict, key = "navigateDict")
        If(not $ngt)
        {
            CallTrigger(
                object = $obj1,
                event = "FUNC_GPS_RECALCULATE",
                mapStart = $curMap,
                mapEnd = $belongMap,
                gpsDict = $gpsDict,
            )
            Return()
        }
        Else
        {
            $roadIdx = DictionaryGet(dict = $ngt, key = "roadIdx") + 1
            $maps = DictionaryGet(dict = $ngt, key = "maps")
            $road = DictionaryGet(dict = $ngt, key = "road")
        
            $correctMap = ArrayGet(
                array = @allMaps,
                index = ArrayGet(
                    array = $maps,
                    index = $roadIdx
                )
            )
            If($correctMap ~= $mapName)
            {
                CallTrigger(
                    object = $obj1,
                    event = "FUNC_GPS_RECALCULATE",
                    mapStart = $curMap,
                    mapEnd = $belongMap,
                    gpsDict = $gpsDict,
                )
                Return()
            }
        
            $gps = GetConfigValue(
                type = "entity",
                key = "pos",
                fullName = ArrayGet(array = $road, index = $roadIdx)
            )
            
            $navigateDict = NewDictionary({
                road = $road,
                roadIdx = $roadIdx,
                maps = $maps
            })

            DictionarySet(
                dict = $gpsDict,
                key = "navigateDict",
                value = $navigateDict
            )
        }
    }

    CallTrigger(
        object = $obj1,
        event = "FUNC_OPEN_GPS",
        gps = $gps,
        gpsDict = $gpsDict
    )
}

FUNC_GPS_RECALCULATE --params: mapStart, mapEnd, gpsDict
{
    $road = []
    $maps = []
    $roadIdx = 1

    $mapAToBKey = Concat(p1 = $mapStart, p2 = "To", p3 = $mapEnd)
    $dwKey = Concat(p1 = $mapAToBKey, p2 = "DWs") --doorwayKey
    $maps = GetGlobalVar(key = $mapAToBKey) --maps
    $road = GetGlobalVar(key = $dwKey) --doorways
    If(not $road || not $maps)
    {
        ShowTip(entity = $obj1, tipType = 3, textKey = "unable_to_navigate", textP1 = $mapEnd)
        Return()
    }
    $mapSize = ArraySize(array = $maps)
    $matrix = GetGlobalVar(key = "mapMatrixNeedDijkstra")
    ForLoop(from = 1, to = $mapSize, key = "index")
    {
        $row = ArrayGet(array = $matrix, index = ArrayGet(array = $maps, index = $index))
        ForLoop(from = $index + 1, to = $mapSize, key = "idx")
        {
            If(ArrayGet(array = $row, index = $idx)) --从地图A到地图B存在多个门，那么即存在最短路的问题
            {   
                $type = DictionaryGet(dict = $gpsDict, key = "type")
                $belongMap = DictionaryGet(dict = $gpsDict, key = "belongMap")
                $targetPos = nil

                If($type == "selfHouse")
                {
                    $house = GetObjectVar(obj = $obj1, key = "house")
                    $housePos = GetEntityPosition(entity = $house)
                    $targetPos = ComputeV3(p1 = $housePos, p2 = GetConfigVar(obj = $house, key = "doorPos") or {x = 0,y = 2, z = 0}, op = "add")
                }
                ElseIf($type == "entity")
                {
                    $targetPos = DictionaryGet(dict = $gpsDict, key = "pos")
                }

                $targetDict = NewDictionary({
                    pos = $targetPos,
                    belongMap = $belongMap
                })

                --构建玩家在mapStart到mapEnd途径所有门的邻接矩阵
                $mapS2mapEDWsMatrix = "mapS2mapEDWsMatrix"
                CallTrigger(
                    object = $obj1,
                    allDWs = $road,
                    targetDict = $targetDict,
                    saveName = $mapS2mapEDWsMatrix,
                    event = "FUNC_NAVIGATION_SET_DOORWAY_ADJACENCY_MATRIX"
                )

                --利用上面的邻接矩阵进行最短路的查找操作
                $mapS2mapEDWs1Tother = Concat(p1 = $mapS2mapEDWsMatrix, p2 = "1Tother")
                CallTrigger(
                    object = $obj1,
                    startIdx = 1,
                    matrix = GetObjectVar(obj = $obj1, key = $mapS2mapEDWsMatrix),
                    saveName = $mapS2mapEDWs1Tother,
                    event = "FUNC_NAVIGATION_DIJKSTRA"
                )

                --利用上面的得到数据，再去找倒数第二张地图到最后一张地图需要经过的门，在这些门里找到dis最小的索引
                $index = 2
                If(not $targetPos)
                {
                    $secLastMap = ArrayGet(
                        array = @allMaps,
                        index = ArrayGet(array = $maps, index = $mapSize - 1)
                    )
                    $lastMap = ArrayGet(
                        array = @allMaps,
                        index = ArrayGet(array = $maps, index = $mapSize)
                    )
                    $mapEntry = NewDictionary(GetMapConfig(name = $secLastMap, key = "mapEntry"))
                    $entries = DictionaryGet(dict = $mapEntry, key = $lastMap)
                    $min = @mathHuge
                    $disArr = GetObjectVar(obj = $obj1, key = Concat(p1 = $mapS2mapEDWs1Tother, p2 = "Dis"))
                    ForLoop(from = 1, to = ArraySize(array = $entries or []), key = "idx")
                    {
                        $entryIdx = ArrayFind(
                            value = ArrayGet(array = $entries, index = $idx),
                            array = GetObjectVar(obj = $obj1, key = Concat(p1 = $mapS2mapEDWsMatrix, p2 = "ColVec"))
                        )
                        $curDis = ArrayGet(array = $disArr, index = $entryIdx)
                        If($curDis < $min)
                        {
                            $min = $curDis
                            $index = $entryIdx
                        }
                    }
                }

                --经过上面的操作后得到了到达最终地图的最短路径的最后一个索引
                $mapS2mapEDWsFinalPath = "mapS2mapEDWsFinalPath"
                CallTrigger(
                    object = $obj1,
                    endIdx = $index,
                    saveName = $mapS2mapEDWsFinalPath,
                    event = "FUNC_NAVIGATION_GET_PATH",
                    allPath = GetObjectVar(obj = $obj1, key = $mapS2mapEDWs1Tother)
                )
                $road = GetObjectVar(obj = $obj1, key = $mapS2mapEDWsFinalPath)
                $roadIdx = $roadIdx + 1 ---因为新的road第一个数据是obj1，如果是仅有唯一路径，则第一个数据就是第一个应该去的地方。

                SetObjectVar(obj = $obj1, value = nil, key = $mapS2mapEDWsFinalPath)
                SetObjectVar(obj = $obj1, value = nil, key = Concat(p1 = $mapS2mapEDWsMatrix, p2 = "ColVec"))
                SetObjectVar(obj = $obj1, value = nil, key = Concat(p1 = $mapS2mapEDWs1Tother, p2 = "Dis"))
                SetObjectVar(obj = $obj1, value = nil, key = $mapS2mapEDWs1Tother)
                SetObjectVar(obj = $obj1, value = nil, key = $mapS2mapEDWsMatrix)
                Break()
            }
        }
    }

    $navigateDict = NewDictionary({
        road = $road,
        roadIdx = $roadIdx,
        maps = $maps
    })

    DictionarySet(
        dict = $gpsDict,
        key = "navigateDict",
        value = $navigateDict
    )

    CallTrigger(
        object = $obj1,
        event = "FUNC_OPEN_GPS",
        gps = GetConfigValue(
            type = "entity",
            key = "pos",
            fullName = ArrayGet(array = $road, index = $roadIdx)
        ),
        gpsDict = $gpsDict
    )
}

FUNC_UPDATE_STATE_SKILL_JACK_AREA --params: up
{
    If($up == nil)
    {
        $map = GetEntityMap(entity = $obj1)
        $houseInsideMap = GetObjectVar(
            obj = GetObjectVar(
                obj = $obj1,
                key = "house"
            ),
            key = "houseInsideMap"
        )
        $up = $map ~= $houseInsideMap and true or false
    }
    UpdateSkillJackArea(
        entity = $obj1,
        info = {
            pos = "top",
            yOff = $up and @skillJackUp or @skillJackDown
        }
    )
}

FUNC_STATE_RESET_DATA --params: state
{
    CallTrigger(object = $obj1, event = "FUNC_CLOSE_GPS", destination = $state)
    CallTrigger(object = $obj1, event = "FUNC_CLOSE_DETAILS_UI", uiName = $state)
    CallTrigger(object = $obj1, event = "FUNC_CLOSE_PROGRESS_FOLLOW_OBJ", pgName = $state)
    CallTrigger(object = $obj1, event = "FUNC_REMOVE_BUFF", buffStr = Concat(p1 = $state, p2 = "Effect"))
    CallTrigger(object = $obj1, event = "FUNC_STOP_TIMER", timerStr = Concat(p1 = $state, p2 = "AFFECT_TIMER"))
}

FUNC_STATEBASE_RESET_DATA -- params: stateBase
{
    SetObjectVar(obj = $obj1, key = Concat(p1 = "pre", p2 = $stateBase), value = nil)
    SetObjectVar(obj = $obj1, key = Concat(p1 = "releasing", p2 = $stateBase), value = nil)
    SetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "STime"), value = nil)
    SetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "UsedTime"), value = nil)
    SetObjectVar(obj = $obj1, key = Concat(p1 = $stateBase, p2 = "InteractList"), value = nil)
}

FUNC_STATE_ACTION --params: state
{
    CallTrigger(object = $obj1, event = "FUNC_STATE_ADD_EFFECT", state = $state)
    CallTrigger(object = $obj1, event = "FUNC_STATE_START_AFFECT", state = $state)
    CallTrigger(object = $obj1, event = "FUNC_STATE_CHECK_REGION", state = $state)
    CallTrigger(object = $obj1, event = "FUNC_STATE_CHECK_MAP", state = $state)
    CallTrigger(object = $obj1, event = "FUNC_STATE_RIDEON", ride = GetRideOn(entity = $obj1)) --检查是否已经在骑乘并且该骑乘是否会触发解除状态
}

SHOW_BUY_PRIVILEGE_TIP
{
    $btnInfo = {
        leftCoinId = nil,
        leftContent = "ui_sure",
        rightCoinId = nil,
        rightContent = "ui_cancel"
    }

    ShowDialogTip(entity = $obj1, tipType = 5, event = "OPEN_PRIVILEGE_SHOP", p1 = "ui_tip", p2= "action_not_have_privilege", p3 = $btnInfo)
}

OPEN_PRIVILEGE_SHOP
{
    If($result && $button == "left")
    {
        OperationWindows(player = $obj1, isOpen = true, win = "priShop")
    }
}

INTERACTION_BACK
{
    UpdateObjectInteractionUI(cfgKey = "interaction_choices", player = $obj2, target = $obj1, show = true)
}

INTERACTION_ACT
{
    If($obj2.role == @child)
    {
        UpdateObjectInteractionUI(cfgKey = "interaction_child", player = $obj2, target = $obj1, show = true)
    }
    Else
    {
        UpdateObjectInteractionUI(cfgKey = "interaction_adult", player = $obj2, target = $obj1, show = true)
    }
}

INTERACTION_CLAP
{ --拍手 小孩和小孩

    If($obj2.role == @child && $obj1.role == @child)
    {
        CallTrigger(obj1 = $obj2, to = $obj1, from = $obj2, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_clap")
    }
    Else
    {
        ShowDialogTip(tipType = 2, entity = $obj2, p1 = "action_role_not_matching_child")
    }
}

INTERACTION_HUG
{ --拥抱 小孩和小孩 or 大人和大人

    If($obj2.role == @child && $obj1.role == @child)
    {
        CallTrigger(obj1 = $obj2, to = $obj1, from = $obj2, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_hug")
    }
    ElseIf($obj2.role == @adult && $obj1.role == @adult)
    {
        CallTrigger(obj1 = $obj2, to = $obj1, from = $obj2, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_hug")
    }
    Else
    {
        ShowDialogTip(tipType = 2, entity = $obj2, p1 = "action_role_not_matching_child_or_adult")
    }
}

INTERACTION_HANDSHAKE
{ --握手 大人和大人
    If($obj2.role == @adult && $obj1.role == @adult)
    {
        CallTrigger(obj1 = $obj2, to = $obj1, from = $obj2, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_handshake")
    }
    Else
    {
        ShowDialogTip(tipType = 2, entity = $obj2, p1 = "action_role_not_matching_adult")
    }
}

INTERACTION_RIDE
{ --骑乘 小孩和大人
    If($obj2.role == @child && $obj1.role == @adult)
    {
        CallTrigger(obj1 = $obj2, to = $obj1, from = $obj2, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_ride")
    }
    Else
    {
        ShowDialogTip(tipType = 2, entity = $obj2, p1 = "action_role_not_matching_child_and_adult")
    }
}

INTERACTION_HAND_IN_HAND
{ --手拉手 小孩和小孩
    If($obj2.role == @child && $obj1.role == @child)
    {
        CallTrigger(obj1 = $obj2, to = $obj2, from = $obj1, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_handinhand")
    }
    Else
    {
        ShowDialogTip(tipType = 2, entity = $obj2, p1 = "action_role_not_matching_child")
    }
}

INTERACTION_FOLLOW
{ --跟随
    CallTrigger(obj1 = $obj2, to = $obj1, from = $obj2, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_follow")
}

INTERACTION_PICK
{ --大人向小孩发起
    If($obj2.role == @adult && $obj1.role == @child)
    {
        CallTrigger(obj1 = $obj2, to = $obj2, from = $obj1, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_pickup")
    }
    Else
    {
        ShowDialogTip(tipType = 2, entity = $obj2, p1 = "action_role_not_matching_adult_child_and")
    }
}

INTERACTION_BABY_TROLLEY {

}

INTERACTION_RAISE_HEIGHT
{
    If($obj2.role == @adult && $obj1.role == @child)
    {
        CallTrigger(obj1 = $obj2, to = $obj2, from = $obj1, event = "USER_INTERACTION_ACTION", skillName = "myplugin/action_raise_height")
    }
    Else
    {
        ShowDialogTip(tipType = 2, entity = $obj2, p1 = "action_role_not_matching_adult_child_and")
    }
}

USER_INTERACTION_ACTION
{
    $toTeamId = GetEntityTeam(entity = $to)
    $fromTeamId = GetEntityTeam(entity = $from)
    --双人交互检查是不是相同家庭
    If(not $fromTeamId || not $toTeamId || $fromTeamId ==  0 || $toTeamId == 0 || $fromTeamId ~= $toTeamId)
    {
        If(GetSkillVar(fullName = $skillName, key = "needPrivilege"))
        {
            $privilegeName = GetSkillVar(fullName = $skillName, key = "privilegeName")
            If(not ArrayFind(array = $obj1.privilege, value = $privilegeName))
            {
                $shopIndex = GetSkillVar(fullName = $skillName, key = "shopIndex")
                CallTrigger(obj1 = $obj1, event = "POP_UP_BUY_PRIVILEGE", shopIndex = $shopIndex)
                Return()
            }
        }
    }

    CastSkill(skill = $skillName, from = $from, packet = {targetID = GetObjectID(object = $to)})
}

ENTITY_JUMP {
    CallTrigger(obj1 = $obj1, event = "ENTITY_REMOVE_INTERACTION")
    If (GetRideOn(entity = $obj1) && not IsCameraMode(player = $obj1)){
        Dismount(entity = $obj1)
    }
}

ENTITY_DIE {
	CallTrigger(obj1 = $obj1, event = "ENTITY_REMOVE_INTERACTION")
}

ENTITY_REMOVE_INTERACTION {
    If($obj1.interactionObjId){
        $target = GetObject(objID = $obj1.interactionObjId)
        If($target && $target.interactionBuff) {
            RemoveEntityBuff(buff = $target.interactionBuff, entity = $target)
        }
    }

    If($obj1.interactionBuff){
        RemoveEntityBuff(buff = $obj1.interactionBuff, entity = $obj1)
    }
}

CREATE_FAMILY
{
    If(IsPlayer(entity = $obj1))
    {
        $familyId = GetEntityTeam(entity = $obj1)
        If($familyId and $familyId ~= 0)
        {
            Return()
        }
        SetEntityTeam(entity = $obj1, teamId = @familyIndex)
        @familyIndex = @familyIndex + 1
        ShowTip(entity = $obj1, tipType = 2,textKey = "gui_create_family_success", time = 40)
    }
}

QUIT_FAMILY
{
    If(IsPlayer(entity = $obj1))
    {
        $teamId = GetEntityTeam(entity = $obj1)
        SetEntityTeam(entity = $obj1, teamId = 0)
        ShowTip(entity = $obj1, tipType = 2,textKey = "gui_quit_family_success", time = 40)
        CallTrigger(event = "INIT_TEAM_UI", obj1 = $obj1, show = false)
        CallTrigger(event = "UPDATE_TEAM_MEMBER_LIST", teamId = $teamId, object = $obj1)
    }
}

UPDATE_TEAM_MEMBER_LIST
{
    $entityList = GetTeamEntityList(teamId = $teamId)
    ForeachDictionary(keyName = "objID", valueName = "entity", dict = NewDictionary($entityList))
    {
        If(IsPlayer(entity = $entity))
        {
            UpdateUIData(player = $entity, ui = "win_team")
        }
    }
}

INTERACTION_JOIN_FAMILY
{
    -- obj2是点击UI的那个人
    -- obj1是被点击的那个人

    $selfFamilyID = GetEntityTeam(entity = $obj2)
    
    If($selfFamilyID and $selfFamilyID ~= 0)
    {
        $playerCount = GetTeamInfo(teamId = $selfFamilyID, key = "playerCount")
        If($playerCount >= 4)
        {
            ShowTip(entity = $obj2, tipType = 2,textKey = "ui_family_full_member", time = 40)
            Return()
        }
    }

    $obj1.requestFamilyPlayer = $obj2
    $btnInfo = {
        leftCoinId = nil,
        leftContent = "ui_cancel",
        rightCoinId = nil,
        rightContent = "ui_sure"
    }
    
    $content = {
        msg = "ui_family_request",
        args = NewArray(GetEntityName(entity = $obj2))
    }

    ShowBubbleMsg(entity = $obj2, textKey = "ui_family_ask_for_family", textP1 = GetEntityName(entity = $obj1))
    ShowDialogTip(entity = $obj1, tipType = 5, event = "REQUEST_JOIN_FAMILY", p1 = "ui_tip", p2 = $content, p3 = $btnInfo)
}

REQUEST_JOIN_FAMILY
{
    $selfFamilyID = GetEntityTeam(entity = $obj1)
    $targetPlayer = $obj1.requestFamilyPlayer
    If($button == "left")
    {
        If(IsPlayer(entity = $targetPlayer))
        {
            ShowTip(entity = $targetPlayer, tipType = 2,textKey = "ui_family_reject", time = 40)
        }
        Return()
    }
    ElseIf($button == "right")
    {
        If(not IsPlayer(entity = $targetPlayer))
        {
            ShowTip(entity = $obj1, tipType = 2,textKey = "ui_family_offline", time = 40)
            Return()
        }

        $targetFamilyID = GetEntityTeam(entity = $targetPlayer)

        
        If($targetFamilyID and $targetFamilyID ~= 0)
        {
            -- 如果 target 有家庭
            $playerCount = GetTeamInfo(teamId = $targetFamilyID, key = "playerCount")
            If(not $playerCount or $playerCount >= 4)
            {
                ShowTip(entity = $obj1, tipType = 2,textKey = "ui_family_full_member", time = 40)
                Return()
            }

            SetEntityTeam(entity = $obj1, teamId = $targetFamilyID)
            CallTrigger(event = "UPDATE_TEAM_MEMBER_LIST", teamId = $targetFamilyID)
            ShowTip(entity = $obj1, tipType = 2,textKey = "ui_family_join_in", time = 40)
            $obj1.requestFamilyPlayer = nil
        }
        Else
        {
            -- 如果 target 没有家庭
            $teamId = @familyIndex
            @familyIndex = @familyIndex + 1
            SetEntityTeam(entity = $targetPlayer, teamId = $teamId)
            SetEntityTeam(entity = $obj1, teamId = $teamId)
            CallTrigger(event = "UPDATE_TEAM_MEMBER_LIST", teamId = $teamId)
            ShowTip(entity = $obj1, tipType = 2,textKey = "ui_family_join_in", time = 40)
            $obj1.requestFamilyPlayer = nil
        }
    }
}


SHOW_TEAM_UI
{
    $teamId = GetEntityTeam(entity = $obj1)
    If($teamId and $teamId ~= 0)
    {
        CallTrigger(event = "INIT_TEAM_UI", object = $obj1) 
    }
    Else
    {
        CallTrigger(event = "SHOW_TEAM_CREATE_UI", object = $obj1) 
    }
}

SHOW_TEAM_CREATE_UI
{

    $btnInfo = {
        leftCoinId = nil,
        leftContent = "ui_family_create",
        rightCoinId = nil,
        rightContent = "ui_family_album"
    }

    ShowDialogTip(entity = $obj1, tipType = 5, event = "TEAM_CREATE_RESULT", p1 = "ui_tip", p2= "ui_not_in_family", p3 = $btnInfo)
}

TEAM_CREATE_RESULT
{
    If($result)
    {
        If($button == "left")
        {
            CallTrigger(event = "CREATE_FAMILY", object = $obj1) 
        }
        ElseIf($button == "right")
        {
            ShowFamilyAlbum(entity = $obj1)
        }
    }
}


INIT_TEAM_UI
{
    $title = {
        name = "gui_my_family"
    }
    
    $buttons = [
        {
            event = "SHOW_FAMILY_ALBUM",
            normalImage = "set:team.json image:blue_btn",
            pushedImage = "set:team.json image:blue_btn",
             name = "ui_family_album"
        },
        {
            event = "QUIT_FAMILY",
            normalImage = "set:team.json image:green_btn",
            pushedImage = "set:team.json image:green_btn",
            name = "ui_family_quit"
        }
    ]

    $closeBtn = {
        disableClose = false
    }

    $info = {
        title = $title,
        buttons = $buttons,
        closeBtn = $closeBtn
    }

    ShowTeam(entity = $obj1, show = $show, info = $info)
}

SHOW_FAMILY_ALBUM
{
    ShowFamilyAlbum(entity = $obj1)
}

STORE_ITEM_STATUS_CHANGE
{
    $dictItem = NewDictionary($item)
    $storeId = DictionaryGet(dict = $dictItem, key = "storeId")
    $itemIndex = DictionaryGet(dict = $dictItem, key = "index")
    $danceStoreId = GetWorldVar(key = "danceStoreId")
    If($storeId == $danceStoreId && $targetIndex > 0)
    {
        $newDance = {}
        $danceIndex = 1
        $oldUseIndex = 0
        $oldItemIndex = 0
        Foreach(array = $obj1.danceUseItem, key = "value")
        {
            If($value == $itemIndex)
            {
                $oldUseIndex = $danceIndex
            }

            If ($danceIndex == $targetIndex)
            {
                $oldItemIndex = $value
                ArraySet(array = $newDance, index = $danceIndex , value = $itemIndex)
            }
            Else
            {
                ArraySet(array = $newDance, index = $danceIndex , value = $value)
            }

            $danceIndex = $danceIndex + 1
        }

        If ($oldUseIndex > 0)
        {
            ArraySet(array = $newDance, index = $oldUseIndex , value = $oldItemIndex)
        }

        $newData = {}
        $dataIndex = 1
        $hasOldItemIndex = false
        Foreach(array = $data, key = "value")
        {
            $dictValue = NewDictionary($value)
            $dictStoreId = DictionaryGet(dict = $dictValue, key = "storeId")
            $dictStoreItemIndex = DictionaryGet(dict = $dictValue, key = "itemIndex")
            If ($dictStoreId == $danceStoreId)
            {
                DictionarySet(dict = $dictValue, key = "status", value = 1)
            }

            If($dictStoreItemIndex == $oldItemIndex)
            {
                $hasOldItemIndex = true
            }

            ArraySet(array = $newData, index = $dataIndex , value = $dictValue)
            $dataIndex = $dataIndex + 1
        }

        If($hasOldItemIndex == false)
        {
            $dictValue = NewDictionary({})
            DictionarySet(dict = $dictValue, key = "status", value = 1)
            DictionarySet(dict = $dictValue, key = "itemIndex", value = $oldItemIndex)
            DictionarySet(dict = $dictValue, key = "storeId", value = $danceStoreId)
            ArrayAppend(array = $newData, value = $dictValue)
        }
        
        Foreach(array = $newDance, key = "value")
        {
            $isChange = false
            Foreach(array = $newData, key = "dictValue")
            {
                $dictStoreId = DictionaryGet(dict = $dictValue, key = "storeId")
                $dictItemIndex = DictionaryGet(dict = $dictValue, key = "itemIndex")
                If($dictItemIndex == $value && $dictStoreId == $danceStoreId)
                {
                    $isChange = true
                    DictionarySet(dict = $dictValue, key = "status", value = 2)
                    Break()
                }
            }
            If ($isChange == false)
            {
                $dictValue = NewDictionary({})
                DictionarySet(dict = $dictValue, key = "status", value = 2)
                DictionarySet(dict = $dictValue, key = "itemIndex", value = $value)
                DictionarySet(dict = $dictValue, key = "storeId", value = $danceStoreId)
                ArrayAppend(array = $newData, value = $dictValue)
            }
        }

        $dataIndex = 1
        $dataRestore = {}
        Foreach(array = $newData, key = "dictValue")
        {
            ArraySet(array = $dataRestore, index = $dataIndex , value = DictionaryToTable(dict = $dictValue))
            $dataIndex = $dataIndex + 1
        }

        CallTrigger(event = "CHANGE_DANCE_SKILL", obj1 = $obj1, oldSkill = $obj1.danceUseItem , newSkill = $newDance)
        $obj1.danceUseItem = $newDance
        $data = $dataRestore
        $def = false
    }
}

ADD_DANCE_EXPIRY_DATE
{
    --TODO money obj1 index time
    $danceStoreId = GetWorldVar(key = "danceStoreId")
    $status = GetStoreItemStatus(player = $obj1, storeId = $danceStoreId, index = $index)
    If($status == 1 || $status == 2 )
    {
        AddCurrency(entity = $obj1, coinName = "green_currency", count = $money)
        Return()
    }

    $isNeedChange = true
    Foreach(array = $obj1.danceExpiryDate, key = "value")
    {
        $dictValue = NewDictionary($value)
        $dictIndex = DictionaryGet(dict = $dictValue, key = "index")
        $dictTime = DictionaryGet(dict = $dictValue, key = "time")
        If($index == $dictIndex)
        {
            $dictTime = $dictTime + $time
            DictionarySet(dict = $dictValue, key = "time", value = $dictTime)
            $value = DictionaryToTable(dict = $dictValue)
            $isNeedChange = false
            Break()
        }
    }

    If($isNeedChange)
    {
        $expiryDate = GetTime() + $time
        $dictValue = NewDictionary({})
        DictionarySet(dict = $dictValue, key = "index", value = $index)
        DictionarySet(dict = $dictValue, key = "time", value = $expiryDate)
        ArrayAppend(array = $obj1.danceExpiryDate, value = DictionaryToTable(dict = $dictValue))
    }

    CallTrigger(event = "CHANGE_DANCE_EXPIRY_DATE", obj1 = $obj1)
}

CHANGE_DANCE_EXPIRY_DATE
{
    $newDances = {}
    $minRemainTime = -1
    $dances = $obj1.danceExpiryDate
    $danceStoreId = GetWorldVar(key = "danceStoreId")
    Foreach(array = $dances, key = "value")
    {
        $dictValue = NewDictionary($value)
        $dictIndex = DictionaryGet(dict = $dictValue, key = "index")
        $dictTime = DictionaryGet(dict = $dictValue, key = "time")
        $remainTime = GetTime() - $dictTime

        If($remainTime <= 0)
        {
            $remainTime = 0
        }
        Else
        {
            ArrayAppend(array = $newDances, value = DictionaryToTable(dict = $dictValue))
        }

        If($minRemainTime == -1 || $remainTime < $minRemainTime )
        {
            $minRemainTime = $remainTime
        }

        ChangeStoreItemRemainTime(player = $obj1, storeId = $danceStoreId, index = $dictIndex, remainTime = $remainTime)
    }

    $obj1.danceExpiryDate = $newDances

    If($minRemainTime > 0)
    {
        StartTimer(rep = false, time = $minRemainTime * 20, object = $obj1, event = "CHANGE_DANCE_EXPIRY_DATE")
    }
}

CHANGE_DANCE_SKILL
{
    $danceStoreId = GetWorldVar(key = "danceStoreId")
    If($oldSkill)
    {
        Foreach(array = $oldSkill, key = "value")
        {
            $skillName = GetStoreItemName(storeId= $danceStoreId , index = $value)
            ForgetSkill(entity = $obj1, name = $skillName)
        }
    }

    If($newSkill)
    {
        UpdateUIData(player = $obj1, ui = "win_dance", data = $newSkill)

        Foreach(array = $newSkill, key = "value")
        {
            $skillName = GetStoreItemName(storeId= $danceStoreId , index = $value)
            LearnSkill(entity = $obj1, name = $skillName)
            EquipSkill(entity = $obj1, name = $skillName)
        }
    }
}

SET_DRESS_ARCHIVE
{
    $dressArchive = GetObjectVar(obj = $obj1, key = "dressArchive")
    If(not $dressArchive)
    {
        Return()
    }

    $bmDressData = {
        vars = {
            custom_face = GetSkin(entity = $obj1, skinName = "custom_face"),
            custom_hair = GetSkin(entity = $obj1, skinName = "custom_hair"),
            clothes_tops = GetSkin(entity = $obj1, skinName = "clothes_tops"),
            clothes_pants = GetSkin(entity = $obj1, skinName = "clothes_pants"),
            custom_shoes = GetSkin(entity = $obj1, skinName = "custom_shoes"),
            custom_glasses = GetSkin(entity = $obj1, skinName = "custom_glasses"),
            custom_scarf = GetSkin(entity = $obj1, skinName = "custom_scarf"),
            custom_wing = GetSkin(entity = $obj1, skinName = "custom_wing"),
            custom_hat = GetSkin(entity = $obj1, skinName = "custom_hat"),
            custom_tail = GetSkin(entity = $obj1, skinName = "custom_tail")
        }
    }

    $bmDressInfo = {
        vars = {
            name = "ui_dress_archive_app_dress",
            data = $bmDressData
        }
    }

    ArrayAppend(array = $dressArchive, value = $bmDressInfo)

    UpdateUIData(player = $obj1, ui = "win_dressArchive", data = $dressArchive)

}


SHOW_OPERATE_DRESS_ARCHIVE_INPUT_DIALOG
{
    $title = {
        name = "ui_dress_archive_change_name"
    }

    $inputTitle = {
        name = "ui_dress_archive_input_name"
    }

    $buttons = [
        {
            event = "cancel",
            normalImage = "set:tip_dialog.json image:btn_big_blue",
            pushedImage = "set:tip_dialog.json image:btn_big_blue",
            name = "ui_cancel"
        },
        {
            event = "SHOW_DELETE_DRESS_ARCHIVE_DIALOG",
            normalImage = "set:tip_dialog.json image:btn_big_yellow",
            pushedImage = "set:tip_dialog.json image:btn_big_yellow",
            name = "ui_delete",
            clickAndShow = true
        },
        {
            event = "RENAME_DRESS_ARCHIVE",
            normalImage = "set:tip_dialog.json image:btn_big_green",
            pushedImage = "set:tip_dialog.json image:btn_big_green",
            name = "ui_sure"
        }
    ]

    $options = {
        index = $index
    }

    $contents = {
        title = $title,
        inputTitle = $inputTitle,
        buttons = $buttons,
        text = $name
    }

    ShowInputDialog(entity = $obj1, contents = $contents, options = $options)
}

ADD_DRESS_ARCHIVE
{
    $dressArchive = GetObjectVar(obj = $obj1, key = "dressArchive")
    If(not $dressArchive)
    {
        Return()
    }

    If((ArraySize(array = $dressArchive) - 1) < GetWorldVar(key = "dressArchiveNum"))
    {
        $addDressData = {
            vars = {
                custom_face = GetSkin(entity = $obj1, skinName = "custom_face"),
                custom_hair = GetSkin(entity = $obj1, skinName = "custom_hair"),
                clothes_tops = GetSkin(entity = $obj1, skinName = "clothes_tops"),
                clothes_pants = GetSkin(entity = $obj1, skinName = "clothes_pants"),
                custom_shoes = GetSkin(entity = $obj1, skinName = "custom_shoes"),
                custom_glasses = GetSkin(entity = $obj1, skinName = "custom_glasses"),
                custom_scarf = GetSkin(entity = $obj1, skinName = "custom_scarf"),
                custom_wing = GetSkin(entity = $obj1, skinName = "custom_wing"),
                custom_hat = GetSkin(entity = $obj1, skinName = "custom_hat"),
                custom_tail = GetSkin(entity = $obj1, skinName = "custom_tail")
            }
        }

        $addDressInfo = {
            vars = {
                name = $name,
                data = $addDressData,
                isSelect = false
            }
        }

        ArrayInsert(array = $dressArchive, index = 1, value = $addDressInfo)

        UpdateUIData(player = $obj1, ui = "win_dressArchive", data = $dressArchive)
    }
}

SHOW_DELETE_DRESS_ARCHIVE_DIALOG
{
    ShowDialogTip(entity = $obj1, 
        p1 = "ui_dress_archive_delete_dialog_context",
        event = "DELETE_DRESS_ARCHIVE",
        tipType = 2,
        context = { index = $index })
}

DELETE_DRESS_ARCHIVE
{
    If(not $result)
    {
        Return()
    }
    
    ShowInputDialog(entity = $obj1, contents = { isClose = true })
    $dressArchive = GetObjectVar(obj = $obj1, key = "dressArchive")

    If(not $dressArchive)
    {
        Return()
    }

    If(not $index)
    {
        Return()
    }

    If($index <= ArraySize(array = $dressArchive) and $index > 0)
    {
        ArrayRemove(array = $dressArchive, index = $index)
    }
    
    UpdateUIData(player = $obj1, ui = "win_dressArchive", data = $dressArchive)
}

RENAME_DRESS_ARCHIVE
{
    $dressArchive = GetObjectVar(obj = $obj1, key = "dressArchive")
    If(not $dressArchive)
    {
        Return()
    }

    If(not $index)
    {
        Return()
    }

    If($index <= ArraySize(array = $dressArchive) and $index > 0)
    {
        $archive = ArrayGet(array = $dressArchive, index = $index)
        If($archive)
        {
            SetObjectVar(obj = $archive, key = "name", value = $name)
        }
    }
    
    UpdateUIData(player = $obj1, ui = "win_dressArchive", data = $dressArchive) 
}

REMOVE_BM_DRESS
{
    $dressArchive = GetObjectVar(obj = $obj1, key = "dressArchive")
    If(not $dressArchive)
    {
        Return()
    }

    ArrayRemove(array = $dressArchive, index = ArraySize(array = $dressArchive))
}

INIT_PLAYER_DRESS
{
    $gameDress = GetObjectVar(obj = $obj1, key = "gameDress")
    If(not $gameDress)
    {
        Return()
    }

    If($gameDress ~= "")
    {
        $dressDict = NewDictionary($gameDress)
        ForeachDictionary(dict = $dressDict, keyName = "skinName", valueName = "skinValue")
        {
            SetSkin(entity = $obj1, skinName = $skinName, skinValue = $skinValue)
        }
    }
}

SAVE_PLAYER_DRESS
{
    $gameDress = GetObjectVar(obj = $obj1, key = "gameDress")
    If(not $gameDress)
    {
        Return()
    }

    $gameDress = {
        custom_face = GetSkin(entity = $obj1, skinName = "custom_face"),
        custom_hair = GetSkin(entity = $obj1, skinName = "custom_hair"),
        clothes_tops = GetSkin(entity = $obj1, skinName = "clothes_tops"),
        clothes_pants = GetSkin(entity = $obj1, skinName = "clothes_pants"),
        custom_shoes = GetSkin(entity = $obj1, skinName = "custom_shoes"),
        custom_glasses = GetSkin(entity = $obj1, skinName = "custom_glasses"),
        custom_scarf = GetSkin(entity = $obj1, skinName = "custom_scarf"),
        custom_wing = GetSkin(entity = $obj1, skinName = "custom_wing"),
        custom_hat = GetSkin(entity = $obj1, skinName = "custom_hat"),
        custom_tail = GetSkin(entity = $obj1, skinName = "custom_tail")
    }
}

SHOW_CHANGE_NAME_INPUT_DIALOG
{
    $title = {
        name = "ui_dress_change_name"
    }

    $inputTitle = {
        name = "ui_dress_input_name"
    }

    $buttons = [
        {
            event = "cancel",
            normalImage = "set:tip_dialog.json image:btn_big_blue",
            pushedImage = "set:tip_dialog.json image:btn_big_blue",
            name = "ui_cancel"
        },
        {
            event = "CHANGE_PLAYER_NAME",
            normalImage = "set:tip_dialog.json image:btn_big_green",
            pushedImage = "set:tip_dialog.json image:btn_big_green",
            name = "ui_sure"
        }
    ]

    $contents = {
        title = $title,
        inputTitle = $inputTitle,
        buttons = $buttons,
        text = $obj1.nickName
    }

    ShowInputDialog(entity = $obj1, contents = $contents)
}

CHANGE_PLAYER_NAME
{
    SetEntityName(entity = $obj1, name = $name)
    $obj1.nickName = $name
    UpdateUIData(player = $obj1, ui = "win_dressArchiveName", data = $name)
}

SHOW_OPERATE_DRESS_STORE_ITEM
{   
    If(not $storeId or not $itemIndex or not $targetIndex)
    {
        Return()
    }

    $item = GetStoreItem(storeId = $storeId, index = $itemIndex)
    If(not $item)
    {
        Return()
    }


    $itemDic = NewDictionary($item)

    $coinId = DictionaryGet(dict = $itemDic, key = "coinId")
    $price = DictionaryGet(dict = $itemDic, key = "price")

    If (not $coinId or not $price)
    {
        Return()
    }

    $btnInfo = {
        leftCoinId = nil,
        leftContent = "ui_cancel",
        rightCoinId = $coinId,
        rightContent = $price
    }
    $context = {
        storeId = $storeId,
        itemIndex = $itemIndex,
        targetIndex = $targetIndex
    }
    ShowDialogTip(entity = $obj1,tipType = 5, event = "DRESS_STORE_ITEM_BUY", p1 = "ui_tip", p2 = "ui_sure_to_pay", p3 = $btnInfo, context = $context)
}

DRESS_STORE_ITEM_BUY
{
    If($result and $button == "right")
    {
        StoreOperation(entity = $obj1, storeId = $storeId, itemIndex = $itemIndex, targetIndex = $targetIndex)
    }
}

SHOW_SAVE_WORKS_TIP
{
    $price = GetConfigVar(obj = $obj1, key = "publishWorksPrice" )
    $btnInfo = { leftCoinId = 3, leftContent = $price, rightCoinId = nil, rightContent = "works_save" }
    $context = { url = $url}
    ShowDialogTip(entity = $obj1, tipType = 5, event = "SAVE_WORKS_TIP_CLICK_EVENT", p1 = "ui_tip", p2= "publish_works_tip", p3 = $btnInfo, context = $context)
}

SAVE_WORKS_TIP_CLICK_EVENT
{
    If(not $result) {
        Return()
    }

    $isPublish = false
    If($button == "left")
    {
        $price = GetConfigVar(obj = $obj1, key = "publishWorksPrice" )
        If(CheckBalance(entity = $obj1, coinName = "green_currency", price = $price))
        {
            $isPublish = true
        }
        Else
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_not_enough_money", time = 20)
            Return()
        }
    }

    $content = { url = $url, isPublish = $isPublish }
    CallTrigger(event = "CHECK_WORKS_ARCHIVE", obj1 = $obj1, url = $url, isPublish = $isPublish)
}


CHECK_WORKS_ARCHIVE
{
    GetWorksCount(player = $obj1, event = "CHECK_WORKS_ARCHIVE_RESPONSE", url = $url, isPublish = $isPublish)
}

CHECK_WORKS_ARCHIVE_RESPONSE
{
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")

    If($code == 1)
    {
        $freeCount = GetConfigVar(obj = $obj1, key = "freeWorksArchive" )
        $count = DictionaryGet(dict = $dictValue, key = "data")
        If($count < $freeCount)
        {
            CallTrigger(event = "CREATE_WORKS", obj1 = $obj1, url = $url, isPublish = $isPublish)
        }
        Else
        {
            CallTrigger(event = "SHOW_BYU_WORKS_ARCHIVE_UI", obj1 = $obj1, url = $url, isPublish = $isPublish)
        }
    }
    Else
    {
        If($isPublish)
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_failure", time = 20)
        }
        Else
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "works_save_failure", time = 20)
        }
    }
}

SHOW_BYU_WORKS_ARCHIVE_UI
{
    $price = GetConfigVar(obj = $obj1, key = "worksArchivePrice" )
    $btnInfo = { leftCoinId = 0, leftContent = $price, rightCoinId = nil, rightContent = "works_save_coverage_new" }
    $context = { url = $url, isPublish = $isPublish }
    ShowDialogTip(entity = $obj1, tipType = 5, event = "BUY_WORKS_ARCHIVE", p1 = "publish_works_buy_archive_title", p2= "publish_works_buy_archive", p3 = $btnInfo, context = $context)
}

BUY_WORKS_ARCHIVE
{
    If($button == "close" || not $result)
    {
        Return()
    }

    If($button == "left")
    {
        $price = GetConfigVar(obj = $obj1, key = "worksArchivePrice" )
        If(not CheckBalance(entity = $obj1, coinName = "gDiamonds", price = $price))
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_buy_archive_not_enough_money", time = 20)
            Return()
        }
        BuyGoods(player = $obj1, uniqueId = 1000001,  coinId = 0, price = $price, url = $url, isPublish = $isPublish, event = "BUY_WORKS_ARCHIVE_RESPONSE" )
    }

    If($button == "right")
    {
        CallTrigger(event = "GET_NEW_UN_PUBLISH_WORKS", obj1 = $obj1, url = $url, isPublish = $isPublish)
    }
}

BUY_WORKS_ARCHIVE_RESPONSE
{
    $dictValue = NewDictionary($response)

    $url = DictionaryGet(dict = $dictValue, key = "url")
    $obj1 = DictionaryGet(dict = $dictValue, key = "player")
    $isPublish = DictionaryGet(dict = $dictValue, key = "isPublish")
    If($isSucceed)
    {
        CallTrigger(event = "CREATE_WORKS", obj1 = $obj1, url = $url, isPublish = $isPublish)
    }
    Else
    {
        ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_buy_archive_failure", time = 20)
    }
}

GET_NEW_UN_PUBLISH_WORKS
{
    GetNewUnPublishWorks(event = "GET_NEW_UN_PUBLISH_RESPONSE", player = $obj1, url = $url, isPublish = $isPublish)
}

GET_NEW_UN_PUBLISH_RESPONSE
{
     $dictValue = NewDictionary($response)
     $code = DictionaryGet(dict = $dictValue, key = "code")
     If($code == 1)
     {
        $worksId = DictionaryGet(dict = $dictValue, key = "data")
        CallTrigger(event = "UPDATE_WORKS", obj1 = $obj1, url = $url, isPublish = $isPublish, worksId = $worksId)
     }
     Else
     {
        ShowTip(entity = $obj1, tipType = 6, textKey = "works_save_coverage_new_works_failure", time = 20)
     }
}

UPDATE_WORKS
{
    UpdateWorks(player = $obj1, url = $url, isPublish = $isPublish, worksId = $worksId, event = "UPDATE_WORKS_RESPONSE")
}

UPDATE_WORKS_RESPONSE
{
     $dictValue = NewDictionary($response)
     $code = DictionaryGet(dict = $dictValue, key = "code")
     If($code == 1)
     {
        If($isPublish)
        {
            $price = GetConfigVar(obj = $obj1, key = "publishWorksPrice" )
            PayCurrency(entity = $obj1, coinName = "green_currency", count = $price)
            ShowTip(entity = $obj1, tipType = 6, textKey = "works_save_coverage_and_publish_new_works_succeed", time = 20)
            UpdateUIData(player =$obj1, ui = "works_person", data = true)
        }
        Else
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "works_save_coverage_new_works_succeed", time = 20)
        }
     }
     Else
     {
        ShowTip(entity = $obj1, tipType = 6, textKey = "works_save_coverage_new_works_failure", time = 20)
     }
}

CREATE_WORKS
{
    CreateWorks(player = $obj1, picUrl = $url, isPublish = $isPublish, event = "CREATE_WORKS_RESPONSE")
}

CREATE_WORKS_RESPONSE
{
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")

    If($code == 1)
    {
        If($isPublish)
        {
            $price = GetConfigVar(obj = $obj1, key = "publishWorksPrice" )
            PayCurrency(entity = $obj1, coinName = "green_currency", count = $price)
            ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_succeed", time = 20)
            UpdateUIData(player =$obj1, ui = "works_person", data = true)
        }
        Else
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "works_save_succeed", time = 20)
        }
    }
    Else
    {
        If($isPublish)
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_failure", time = 20)
        }
        Else
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "works_save_failure", time = 20)
        }
    }
}

SHOW_PUBLISH_WORKS_UI
{
    $price = GetConfigVar(obj = $obj1, key = "publishWorksPrice" )
    $cubePrice = GetConfigVar(obj = $obj1, key = "publishWorksCubePrice" )
    $btnInfo = { leftCoinId = 3, leftContent = $price, rightCoinId = 0, rightContent = $cubePrice }
    $context = { worksId = $worksId, isEdit = false }
    ShowDialogTip(entity = $obj1, tipType = 5, event = "PUBLISH_WORKS", p1 = "ui_tip", p2= "publish_works_tip", p3 = $btnInfo, context = $context)
}

PUBLISH_WORKS
{
    If($button == "left")
    {
        $price = GetConfigVar(obj = $obj1, key = "publishWorksPrice" )
        If(not CheckBalance(entity = $obj1, coinName = "green_currency", price = $price))
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_not_enough_money", time = 20)
            Return()
        }
        PublishWorks(player = $obj1, id = $worksId, event = "PUBLISH_WORKS_RESPONSE")
    }

    If($button == "right")
    {
        $price = GetConfigVar(obj = $obj1, key = "publishWorksCubePrice")
        If(not CheckBalance(entity = $obj1, coinName = "gDiamonds", price = $price))
        {
            ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_not_enough_money", time = 20)
            Return()
        }
        BuyGoods(player = $obj1, uniqueId = 1000002,  coinId = 0, price = $price, worksId = $worksId, event = "PAY_PUBLISH_WORKS_BY_CUBE_RESPONSE" )
    }
}

PUBLISH_WORKS_RESPONSE
{
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")
    If($code == 1)
    {
        $price = GetConfigVar(obj = $obj1, key = "publishWorksPrice" )
        PayCurrency(entity = $obj1, coinName = "green_currency", count = $price)
        ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_succeed", time = 20)
        UpdateUIData(player =$obj1, ui = "works_person", data = true)
    }
    Else
    {
        ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_failure", time = 20)
    }
}

PAY_PUBLISH_WORKS_BY_CUBE_RESPONSE
{
    $dictValue = NewDictionary($response)

    $obj1 = DictionaryGet(dict = $dictValue, key = "player")
    $worksId = DictionaryGet(dict = $dictValue, key = "worksId")

    If($isSucceed)
    {
        PublishWorks(player = $obj1, id = $worksId, event = "PUBLISH_WORKS_BY_CUBE_RESPONSE")
    }
    Else
    {
        ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_failure", time = 20)
    }
}

PUBLISH_WORKS_BY_CUBE_RESPONSE
{
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")
    If($code == 1)
    {
        ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_succeed", time = 20)
    }
    Else
    {
        ShowTip(entity = $obj1, tipType = 6, textKey = "publish_works_failure", time = 20)
    }
}

COMMENT_WORKS
{
    CommentWorks(player = $obj1, id = $id, msg = $msg, event = "HTTP_TEST")
}

HTTP_TEST
{

}

HTTP_PURCHASED_DRESS_CALL_BACK
{
    $responseDict = NewDictionary($response)
    $dressStoreIds = GetWorldVar(key = "dressStoreIds")
    Foreach(array = $dressStoreIds, key = "storeId")
    {
        $storeItems = GetStoreById(storeId = $storeId)
        Foreach(array = $storeItems, key = "item")
        {
            $itemDict = NewDictionary($item)
            $itemType = DictionaryGet(dict = $itemDict, key = "itemType")
            $itemName = DictionaryGet(dict = $itemDict, key = "itemName")
            $itemId = GetConfigValue(type = "dress", fullName = $itemName, key = "itemId")
            $itemIndex = DictionaryGet(dict = $itemDict, key = "index")
            ForeachDictionary(dict = $responseDict, keyName = "type", valueName = "responseItems")
            {
                If($itemType == $type)
                {
                    Foreach(array = $responseItems, key = "responseItem")
                    {
                        If($itemId == $responseItem)
                        {
                            ChangeStoreItemStatus(storeId = $storeId, index = $itemIndex, status = 1, player = $obj1)
                        }
                    }
                }
            }
        }
    }
    SyncStore(player = $obj1)
}

--打工
GUIDE_POSITION_CHANGE {
    If($key == "taskWork"){
        $obj1.taskShowGps = $show
        If($show){
           CallTrigger(event = "TASK_UPDATE_DATA", object = $obj1)
        }
    }ElseIf($show == false){
        $obj1.taskShowGps = false
    }
}
GET_TASK_UI_DATA(){ --更新打工UI数据。
    $count = $obj1.worktaskCount % 10
    If($count == 0 && $obj1.worktaskCount >0){
        $count = 10 
    }
    $textCount = $obj1.worktaskCount
    $isTasking = false
    If($obj1.taskKey){
        $count = $count - 1
        $isTasking = true
    }
    $str = "/"
    If($obj1.taskKey){
        If($textCount == 1){
            $str = "st/"
        }ElseIf($textCount == 2){
            $str = "nd/"
        }ElseIf($textCount == 3){
            $str = "rd/"
        }Else{
            $str = "th/"
        }
    }
    $text = Concat(p1 = $textCount, p2 = $str, p3 = "40")
    $rateArray = [0.02, 0.11, 0.35, 0.485, 0.5, 0.52, 0.61, 0.9485, 1]
    $rate = 1 - (ArrayGet(array = $rateArray, index = $count) or 0)
    $imgcount = $obj1.worktaskCount
    If($obj1.taskKey){
        $imgcount = $imgcount - 1
    }
    If($imgcount >=0 and $imgcount <10){
        $btnImg = "set:indentImg.json image:btn1.png"
    }ElseIf($imgcount >=10 &&  $imgcount <20){
        $btnImg = "set:indentImg.json image:BtnGreen.png"
    }ElseIf($imgcount >=20 && $imgcount <30){
        $btnImg = "set:indentImg.json image:btnBule.png"
    }ElseIf($imgcount >=30 && $imgcount <40){
        $btnImg = "set:indentImg.json image:BtnRed.png"
    }
    $taskData = Table(
        Progress =  $rate,
        BtnText = $text,
        gpsPos = $obj1.gpsPos,
        isTasking = $isTasking,
        animation = $animation,
        btnImg = $btnImg
    )
    If($obj1.taskShowGps){
       SetGuidePostion(entity = $obj1, pos = $obj1.gpsPos)
    }
    UpdateUIData(player = $obj1, ui = "circleProgress", data = $taskData)
}
GET_TASK_GPS  --得到打工的GPS位置
{
    $map = GetPlayerMap(player = $obj1)
    $owner = GetMapVar(map = $map, key = "owner")
    If($owner){
        $door = GetObjectVar(obj = $owner , key = "insideDoor")
        If($door){
            $obj1.gpsPos = GetEntityPosition(entity = $door)
        }
    }Else{
        $obj1.gpsPos = $obj1.taskPos or  @INDENT_POS
    }
}
TASK_NPC_ADD_BUFF  --给任务npc还动作加buff
{
    $map = GetPlayerMap(player = $obj1)
    $mapNpc = GetMapNpc(map = $map)
    Foreach(array = $mapNpc, key = "npc"){
        $name = GetConfigVar(obj = $npc, key = "fullName")
        If($name == $obj1.lastNpc){
            PlayAction(entity = $npc, target = $obj1, actionName = "idle", actionTime = -1)
            --UnMarkEntityForPlayer(player = $obj1, entity = $npc, buffName = "myplugin/hand_point_effect")
        }
        If($name == $obj1.taskNpc){
            PlayAction(entity = $npc, target = $obj1, actionName = "idle_1", actionTime = -1)
            --MarkEntityForPlayer(player = $obj1, entity = $npc, buffName = "myplugin/hand_point_effect")
        }
    }
}
TASK_UPDATE_DATA(){ --更新数据
    CallTrigger(event = "TASK_NPC_ADD_BUFF", object = $obj1)
	CallTrigger(event = "GET_TASK_GPS", object = $obj1)
    CallTrigger(event = "GET_TASK_UI_DATA", object = $obj1)
}
PUNISH_END(){--惩罚结束
    If($obj1 and $obj2){
        $obj1.punishmentCD = false
        --ShowEntityHeadCountDown(show = false, entity = $obj2, player = $obj1)
        CallTrigger(object = $obj2, event = "CHANG_ACTOR", actorName = "actorName", obj2 = $obj1)
    }
}
DELETE_ITEM(){
    --出售
    --$price = GetItemCfg(fullName = $fullName, key = "recyclePrice")
    --If($price and $price>0){
    --   AddCurrency(entity = $obj1, coinName = "green_currency", count = $price)
    --    ShowRewardRollTip(image = "set:jail_break.json image:jail_break_currency", text = Concat(p1 = "+", p2 = $price), entity = $obj1)
    --}
    --放弃任务
    If($fullName == "myplugin/taskItem" and $obj1.taskKey){
        $obj1.punishmentCD = true
        If(@INDENT_MODEL){
            --ShowEntityHeadCountDown(show = true, entity = @INDENT_MODEL, player = $obj1, time = @punishmentTime)
            CallTrigger(object = @INDENT_MODEL, event = "CHANG_ACTOR", actorName = "punishmentActor", obj2 = $obj1)
            StartTimer(object = $obj1, event = "PUNISH_END", obj2 = @INDENT_MODEL, time = @punishmentTime * 20)
            --倒计时
            --换模型
        }
        CancelTask(player = $obj1, name = $obj1.taskKey)
        $obj1.lastNpc = $obj1.taskNpc
        $obj1.taskNpc = nil
        $obj1.taskKey = nil
        $obj1.taskPos = nil
        $obj1.npcMap = nil
        $obj1.taskIndex = nil
        $obj1.worktaskCount = $obj1.worktaskCount - 1
        If($obj1.taskShowGps){
            SetGuidePostion(entity = $obj1)
        }
        $obj1.taskShowGps = false
        CallTrigger(event = "GET_TASK_GPS", object = $obj1, showGps = false)
        StartTimer(time = 1, event = "TASK_NPC_ADD_BUFF", object = $obj1)
        CallTrigger(event = "GET_TASK_UI_DATA", object = $obj1, closeGps = true)
    }
}
MAC_TASK_COUNT_RESET(){--第二天，刷新最大累计
    If($obj1 and IsValidObject(object = $obj1)){
        $obj1.maxtaskCount = 0
        If(@INDENT_MODEL and IsValidObject(object = @INDENT_MODEL)){
            -- 换模型
            CallTrigger(object = @INDENT_MODEL, event = "CHANG_ACTOR", actorName = "actorName", obj2 = $obj1)
        }
    }
}
INIT_WORK_TASK(){ --初始打工
    $today = GetYearDayStr(time = GetTime())
    $dayEnd = GetDayEndTime(time = GetTime()) - GetTime()
    StartTimer(event = "MAC_TASK_COUNT_RESET", object = $obj1, rep = false, time = $dayEnd * 20)
    $obj1.workday = $today
    $obj1.maxtaskCount = 0
    $index =  $obj1.taskIndex
    If($index && $index > 0){
        $taskDict = ArrayGet(array = @TASK_TABLE, index = $index)
        $obj1.taskKey = DictionaryGet(dict = $taskDict, key = "taskKey") -- 随机到的任务
        $obj1.taskPos = DictionaryGet(dict = $taskDict, key = "pos") -- 随机到的任务NPC ,GPS位置
        $obj1.taskNpc = DictionaryGet(dict = $taskDict, key = "npcName") --npc的 fullName
        $obj1.npcMap = DictionaryGet(dict = $taskDict, key = "npcMap")
        $obj1.succeedMsg = DictionaryGet(dict = $taskDict, key = "succeedMsg")
        $obj1.failedMsg = DictionaryGet(dict = $taskDict, key = "failedMsg")
    }
    StartTimer(rep = false, time = 20, object = $obj1, event = "TASK_UPDATE_DATA")
}
DO_SUBMIT_ORDER(){ --对话
    $obj1.nowSubmitTask = true
    $obj1.taskobjID = GetObjectID(object = $obj2)
    OpenConversation(
        entity = $obj1,
        optionList = [
            {
                triggerName = "SUBMIT_ORDER"
            }
        ],
        optionNpc = "myplugin/player1",
        talkList = [
            {
                msg =  $obj1.succeedMsg or "tr.37965",
                npc = $obj1.taskNpc
            }
        ]
    )
}
SUBMIT_ORDER(){ --对话提交
    If(not $obj1.nowSubmitTask or not $obj1.taskobjID){
        Return()
    }
    $obj2 = GetObject(objID = $obj1.taskobjID)
    $obj1.nowSubmitTask = false
    AddTarget(entity = $obj1, target = $obj2, type = "SubmitForm")
    DoFinishTask(player = $obj1, name = $obj1.taskKey)
    $obj1.taskobjID = nil
}
FINISH_TASK(debug = false){ --完成打工的对话
    If($obj1.taskKey ~= $name){
       Return()
    }
    If($obj1.taskShowGps){
        SetGuidePostion(entity = $obj1)
    }
    $obj1.taskShowGps = false
    $obj1.finishworkTime =  StartTimer(object = $obj1, event = "FINISH_AWARD", time = 20)
}
FINISH_AWARD(){ --完成打工的奖励
    $obj1.maxtaskCount = ($obj1.maxtaskCount or 0 ) + 1
    If($obj1.maxtaskCount >= @workmaxCount && not $obj1.maxActor and @INDENT_MODEL){ --最大累计数，改变模型
        $obj1.maxActor = true
        CallTrigger(object = @INDENT_MODEL, event = "CHANG_ACTOR", actorName = "maxCountActor", obj2 = $obj1)
    }
    If($obj1.finishworkTime){
        StopTimer(timer = $obj1.finishworkTime)
    }
    $rewDict = DictionaryGet(dict = @worlkMoeny, key =  $obj1.worktaskCount)
    If($rewDict){
        $money = DictionaryGet(dict = $rewDict, key = "money")
        $recfg = DictionaryGet(dict = $rewDict, key = "item")
    }
    If(ArrayFind(array = $obj1.privilege, value = "myplugin/svip_privilege")){ --svip
        $money = DictionaryGet(dict = $rewDict, key = "mvpmoney")
    }
    If($recfg){
        AddItem(entity = $obj1, cfg = $recfg, count = 1)
        ShowRewardItemEffect(type = "Item", count = 1, fullName = $recfg, player = $obj1) -- todo 改变弹窗
    }
    If($money){
        AddCurrency(entity = $obj1, coinName = "green_currency", count = $money)
        ShowRewardRollTip(image = "set:jail_break.json image:jail_break_currency", text = Concat(p1 = "+", p2 = $money), entity = $obj1)
    }
    $taskItem = SearchBagItem(entity = $obj1, key = "newTaskitem", val = true)
    If($taskItem){
        ConsumeItem(item = $taskItem, num = 1)
    }
    $proessLeve = NumberFloor(p1 = $obj1.worktaskCount / 10)
    $proessCount = $obj1.worktaskCount % 10
    $obj1.lastNpc = $obj1.taskNpc
    $obj1.taskNpc = nil
    $obj1.taskKey = nil
    $obj1.taskPos = nil
    $obj1.npcMap = nil
    $obj1.taskIndex = nil
    $animation = true
    CallTrigger(event = "GET_TASK_GPS", object = $obj1, showGps = false)
    StartTimer(time = 1, event = "TASK_NPC_ADD_BUFF", object = $obj1)
    CallTrigger(event = "GET_TASK_UI_DATA", object = $obj1, closeGps = true, animation = $animation)
}
--场景商店的牌子
SHOW_SHOP_TITLE(){
    $data1 = GetWorldVar(key = "titilePos1")
    $data2 = GetWorldVar(key = "titilePos2")
    Show3DUI(player = $obj1, show = $show, data = $data1)
    Show3DUI(player = $obj1, show = $show, data = $data2)
}
--公共区域购买，场景商店的购买逻辑
SURE_BUY {
   If($button == "left"){
      Return()
   }    
   $obj2 = GetObject(objID = $objID)
   If($checkId ~= $obj1.buySessionSS or not $obj2 or not IsValidObject(object = $obj2))
   {
      Return()
   }
   $itemfullName = GetConfigVar(obj = $obj2, key = "sellItem")
   $price = GetConfigVar(obj = $obj2, key = "sellPrice")
   $coinId = GetConfigVar(obj = $obj2, key = "coinId")
   $coinName = "green_currency"
   If($coinId == 0){
       $coinName = "gDiamonds"
   }
   $itemName = GetItemCfg(fullName = $itemfullName, key = "itemname")
   If(not $price){
       $price = 0
   }
   $enoughMoney = CheckBalance(entity = $obj1, coinName = $coinName, price = $price)
   If($enoughMoney){ --够钱
         $checkBag = AddItem(entity = $obj1, cfg = $itemfullName, count = 1)
         If($checkBag ~= false){
            If($coinId == 0){
                BuyGoods(player = $obj1, uniqueId = 20201999,  coinId = $coinId, price = $price, event = "SHOP_BUY_COIN", objID = GetObjectID(object = $obj1))
            }Else{
                ShowTip(textKey = "ui_buy_success", tipType = 1, entity = $obj1, keepTime = 20)
                PayCurrency(entity = $obj1, coinName = $coinName, count = $price)
            }
         }Else{ --背包满了
            ShowTip(textKey = "gui_str_app_shop_inventory_is_full", tipType = 2, entity = $obj1, keepTime = 20)
         }
   }Else{ --不够钱
      ShowTip(textKey = "app_shop_lack_of_money", tipType = 2, entity = $obj1, keepTime = 20)
   }
}
SHOP_BUY_COIN {
    $dict = NewDictionary($response)
    $objID = DictionaryGet(dict = $dict, key = "objID")
    $obj1 = GetObject(objID = $objID)
    If($isSucceed and $obj1){
        ShowTip(textKey = "ui_buy_success", tipType = 2, entity = $obj1, keepTime = 20)
    }ElseIf($obj1){
        ShowTip(textKey = "app_shop_lack_of_money", tipType = 1, entity = $obj1, keepTime = 20)
    }
}
--区域购买逻辑： 领主家里买厨具，食物，
SURE_FURNITURE_BUY {
   If($button == "left"){
      Return()
   }
   $coinName = "green_currency"
   $itemName = GetItemCfg(fullName = $sellItem, key = "itemname")
   $enoughMoney = CheckBalance(entity = $obj1, coinName = $coinName, price = $price)
   If($enoughMoney){ --够钱
         $checkBag = AddItem(entity = $obj1, cfg = $sellItem, count = 1)
         If($checkBag ~= false){
            PayCurrency(entity = $obj1, coinName = $coinName, count = $price)
            If(not $isTeam and $owner){ --不是一个家庭给领主加钱
               ShowTip(textKey = "ui_buy_success", tipType = 1, entity = $obj1, keepTime = 20)
               $name = GetEntityName(entity = $obj1)
               ShowTip(textKey = "gui_buys_your_stuff",textP1 = $name, textP2 = $itemName, tipType = 1, entity = $owner, keepTime = 20)
               AddCurrency(entity = $owner, coinName = $coinName, count = $price)
               ShowRewardRollTip(image = "set:jail_break.json image:jail_break_currency", text = Concat(p1 = "+", p2 = $price), entity = $owner)
            }Else{
               ShowTip(textKey = "gui_gain_item_success",textP1 = $itemName, tipType = 1, entity = $obj1, keepTime = 20)
            }
         }Else{ --背包满了
            ShowTip(textKey = "gui_str_app_shop_inventory_is_full", tipType = 2, entity = $obj1, keepTime = 20)
         }
   }Else{ --不够钱
      ShowTip(textKey = "app_shop_lack_of_money", tipType = 2, entity = $obj1, keepTime = 20)
   }
}
--特权购买成功
ENTITY_BUY_APPSTOP {
    $dict = NewDictionary($shop)
    $shopType = DictionaryGet(dict = $dict, key = "type")
    $itemIndex = DictionaryGet(dict = $dict, key = "index")
    $index = ArrayFind(array = $obj1.privilege, value = $itemName)
    If($shopType == "1" && not $index){
        $addItem = false
        $goodsDict = NewDictionary($obj1.goodsInfo)
        $limit = $limit - DictionaryGet(dict = $goodsDict, key = $itemIndex)
        
        ArrayAppend(array = $obj1.privilege, value = $itemName)
        CallTrigger(obj1 = $obj1, event = "BUY_PRIVILEGE_SUCCEED", shopIndex = $itemIndex, privilegeName = $itemName)
    }
}

--弹窗购买特权
POP_UP_BUY_PRIVILEGE 
{
    If(not $shopIndex || not GetShopItem(shopIndex = $shopIndex, key = "price"))
    {
        Return()
    }
    $obj1.checkprid = ($obj1.checkprid or 0 ) + 1
    $price = GetShopItem(shopIndex = $shopIndex, key = "price")
    $itemdesc = GetShopItem(shopIndex = $shopIndex, key = "desc")
    $Context = {
        seccionid = $obj1.checkprid,
        shopIndex = $shopIndex,
        buyResultEvent = $buyResultEvent
    }
    $btnInfo = {
        leftCoinId = nil,
        leftContent = "ui_cancel",
        rightCoinId = 0,
        rightContent = $price
    }
    $content = {
        msg = "gui_you_want_to_buy_this",
        args = [$itemdesc]
    }

    ShowDialogTip(entity = $obj1, tipType = 5, event = "SURE_BUY_PRI", p1 = "ui_tip", p2 = $content, p3 = $btnInfo, context = $Context)
}
SURE_BUY_PRI 
{
    If($button == "left"){
        CallTrigger(object = $obj1, event = $buyResultEvent, succeed = false, shopIndex = $shopIndex)
        Return()
    }
 
    If($seccionid ~= $obj1.checkprid or not $shopIndex)
    {
        Return()
    }

    $obj1.checkbuyData = [
        $shopIndex,
        $buyResultEvent
    ]
    RequestCanBuy(shopIndex = $shopIndex, player = $obj1, count = 1)
}

ENTITY_BUY_RESULT()
{
    If($obj1.checkbuyData){
        $nowBuyIndex = ArrayGet(array = $obj1.checkbuyData, index = 1)
        $event = ArrayGet(array = $obj1.checkbuyData, index = 2)
        If($nowBuyIndex == $itemIndex){
            $obj1.checkbuyData = nil
            CallTrigger(object = $obj1, event = $event, succeed = $succeed, shopIndex = $itemIndex)
            ShowTip(textKey = $msg, tipType = 2, entity = $obj1, keepTime = 20)
        }
    }
}
--场景商店门面
SHOW_SHOP_TITLE {
    Return()
    If($obj1.showShopTitleTimer){
        StopTimer(timer = $obj1.showShopTitleTimer)
    }
    If(GetEntityMapName(entity = $obj1) == "map001" ){
        $data1 = GetWorldVar(key = "titilePos1")
        $data2 = GetWorldVar(key = "titilePos2")
        Show3DUI(player = $obj1, show = true, data = $data1)
        Show3DUI(player = $obj1, show = true, data = $data2)
    }
}

EQUIP_CAR {
    $mapName = GetEntityName(entity = GetEntityMap(entity = $obj1))
    If($mapName ~= "map001")
    {
        Return()
    }

    If(not GetEntityOnGround(entity = $obj1))
    {
        ShowTip(entity = $obj1, tipType = 2, textKey = "create_car_in_the_air", time = 40)
        Return()
    }
    If(not $obj1.createdCars)
    {
        $obj1.createdCars = NewDictionary()
    }

    $createdCarOfCarName = DictionaryGet(dict = $obj1.createdCars, key = $carName)
    $ownerInCar = false
    If($createdCarOfCarName)
    {
        $passengers = GetPassengers(entity = $createdCarOfCarName)
        Foreach(array = $passengers, key = "passenger")
        {
            If($passenger ~= GetObjectID(object = $obj1))
            {
                ShowTip(entity = GetObject(objID = $passenger), tipType = 2, textKey = "car_owner_already_remove_car", time = 40)
            }
            Else
            {
                $ownerInCar = true
            }
        }

        KillEntity(entity = $createdCarOfCarName)
        DictionaryRemove(dict = $obj1.createdCars, key = $carName)
        If($ownerInCar)
        {
            Return()
        }
    }

    $car = CreateNpc(cfgName = $carName, pos = GetEntityPosition(entity = $obj1), 
        map = GetEntityMap(entity = $obj1), owner = $obj1)
    RideOn(ctrl = true, entity = $obj1, target = $car)

    DictionarySet(dict = $obj1.createdCars, key = $carName, value = $car)

}

DEBARK {
	Dismount(entity = $obj1)
}

TAKEOFF_EQUIP_CAR
{
    If(not $obj1.createdCars)
    {
        Return()
    }

    $createdCarOfCarName = DictionaryGet(dict = $obj1.createdCars, key = $carName)
    If($createdCarOfCarName)
    {
        $passengers = GetPassengers(entity = $createdCarOfCarName)
        Foreach(array = $passengers, key = "passenger")
        {
            If($passenger ~= GetObjectID(object = $obj1))
            {
                ShowTip(entity = GetObject(objID = $passenger), tipType = 2, textKey = "car_owner_already_remove_car", time = 40)
            }
        }

        KillEntity(entity = $createdCarOfCarName)
        DictionaryRemove(dict = $obj1.createdCars, key = $carName)
    }
}

START_CAMERA_MODE
{
    If(not IsCameraMode(player = $obj1))
    {
        SetCameraMode(player = $obj1, isOpen = true)
        $obj1.cameraModeCloseBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/camera_mode_close")
        $obj1.cameraModeTakePhotosBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/camera_mode_take_photos")
        OperationWindows(player = $obj1, isOpen = false, win = "main_tray_g2020")
    }
}

ClOSE_CAMERA_MODE
{
    If(IsCameraMode(player = $obj1))
    {
        SetCameraMode(player = $obj1, isOpen = false)
        RemoveEntityBuff(entity = $obj1, buff = $obj1.cameraModeBuff)
        $obj1.cameraModeBuff = nil
    }
}

STORE_ITEM_OPERATION_PRIVILEGE
{
    CallTrigger(obj1 = $obj1, event = "POP_UP_BUY_PRIVILEGE", shopIndex = $shopIndex)
}

BUY_PRIVILEGE_SUCCEED
{
    If($privilegeName == "myplugin/dance_privilege")
    {
        $danceStoreId = GetWorldVar(key = "danceStoreId")
        $storeItems = GetStoreById(storeId = $danceStoreId)
        Foreach(array = $storeItems, key = "value")
        {
            $dictValue = NewDictionary($value)
            $dictStoreId = DictionaryGet(dict = $dictValue, key = "storeId")
            $dictItemIndex = DictionaryGet(dict = $dictValue, key = "index")
            $dictShopIndex = DictionaryGet(dict = $dictValue, key = "shopIndex")

            If($dictShopIndex == $shopIndex)
            {
                SyncStoreItemStatus(player = $obj1, storeId = $dictStoreId, index = $dictItemIndex, status = 1)
            }
        }
    }
}


SHOW_CASH_BOX_INPUT_DIALOG
{
    $title = {
        name = "ui_cash_box_title_name"
    }

    $inputTitle = {
        name = "ui_cash_box_input_name"
    }

    $buttons = [
        {
            event = "cancel",
            normalImage = "set:tip_dialog.json image:btn_big_blue",
            pushedImage = "set:tip_dialog.json image:btn_big_blue",
            name = "ui_cancel"
        },
        {
            event = "CASH_BOX_BUTTON_SURE",
            normalImage = "set:tip_dialog.json image:btn_big_green",
            pushedImage = "set:tip_dialog.json image:btn_big_green",
            name = "ui_sure"
        }
    ]

    $options = {
        ownerId = $ownerId
    }

    $contents = {
        title = $title,
        inputTitle = $inputTitle,
        buttons = $buttons,
        text = $name,
        inputMode = 2
    }

    ShowInputDialog(entity = $obj1, contents = $contents, options = $options)
}

CASH_BOX_BUTTON_SURE
{
    $money = ToNumber($name)
    If(not IsNumber($money))
    {
        ShowTip(entity = $obj1, tipType = 2, textKey = "gui_cashbox_only_number", time = 40)
        Return()
    }

    If($money // 1 ~= $money)
    {
        ShowTip(entity = $obj1, tipType = 2, textKey = "gui_cashbox_only_number", time = 40)
        Return()
    }
    Else
    {

        If($money <= 0 or $money > 100)
        {
            ShowTip(entity = $obj1, tipType = 2, textKey = "gui_cashbox_only_number", time = 40)
            Return()
        }

        $owner = GetObject(objID = $ownerId)
        If(not $owner or not IsPlayer(entity = $owner))
        {
            Return()
        }

        If(not PayCurrency(entity = $obj1, coinName = "green_currency", count = $money))
        {
            ShowTip(entity = $obj1, tipType = 2, textKey = "gui_cashbox_not_enough_number", time = 40)
            Return()
        }

        AddCurrency(entity = $owner, coinName = "green_currency", count = $money)
        ShowRewardRollTip(image = "set:jail_break.json image:jail_break_currency", text = Concat(p1 = "+", p2 = $money), entity = $owner)
        ShowTip(entity = $owner, tipType = 2, textKey = "gui_cashbox_get_money", textP1 = GetEntityName(entity = $obj1), textP2 = $money, time = 40)
    }
}

SHOW_EDIT_INPUT_DIALOG
{
     $title = {
        name = "ui_edit_title_name"
    }

    $inputTitle = {
        name = "ui_edit_input_name"
    }

    $buttons = [
        {
            event = "cancel",
            normalImage = "set:tip_dialog.json image:btn_big_blue",
            pushedImage = "set:tip_dialog.json image:btn_big_blue",
            name = "ui_cancel"
        },
        {
            event = "EDIT_BUTTON_SURE",
            normalImage = "set:tip_dialog.json image:btn_big_green",
            pushedImage = "set:tip_dialog.json image:btn_big_green",
            name = "ui_sure"
        }
    ]

    $options = {
        index = $index
    }

    $contents = {
        title = $title,
        inputTitle = $inputTitle,
        buttons = $buttons,
        text = $name,
        textLength = 100,
    }

    ShowInputDialog(entity = $obj1, contents = $contents, options = $options)
}

EDIT_BUTTON_SURE
{
    $name = FilterWord(msg = $name)
    If(not $name)
    {
        Return()
    }

    If($index ~= 0)
    {
        $U3Dcfg = GetWorldVar(key = "sceneUI")
        Foreach(array = $U3Dcfg, key = "ucfg")
        {
            $dict = NewDictionary($ucfg)
            $w_index = DictionaryGet(dict = $dict, key = "index")
            If($index == $w_index)
            {
                DictionarySet(dict = @graffitiWallText, key = $w_index, value = $name)

                $data = {
                    name = DictionaryGet(dict = $dict, key = "name"),
                    width = DictionaryGet(dict = $dict, key = "width"),
                    height = DictionaryGet(dict = $dict, key = "height"),
                    rotate = DictionaryGet(dict = $dict, key = "rotate"),
                    position = DictionaryGet(dict = $dict, key = "position"),
                    args = $name,
                }

                $map = GetEntityMap(entity = $obj1)
                $players = GetMapPlayer(map = $map)
                Foreach(array = $players, key = "player")
                {
                    Show3DUI(player = $player, show = true, data = $data)
                }
            }
        }
    }
}

PICK_HAND_ITEM {
    $canHandMap = GetConfigVar(obj = GetEntityHandItem(entity = $obj1), key = "canHandMap")
    $playerCurMap = GetEntityMapName(entity = $obj1)
    If ($canHandMap and $playerCurMap ~= $canHandMap)
    {
        SetHandItem(player = $obj1)
    }
}

ENTER_EQUIPMEN {
    $disableHand = GetConfigVar(obj = $item, key = "disableHand")
    If($disableHand)
    {
        Return()
    }
    $canHandMap = GetConfigVar(obj = $item, key = "canHandMap")
    $playerCurMap = GetEntityMapName(entity = $obj1)
    If ($canHandMap and $playerCurMap ~= $canHandMap)
    {
        Return()
    }
    SetHandItem(player = $obj1, item = $item)
}

ENTER_MAP
{
    CallTrigger(object = $obj1, event = "PICK_HAND_ITEM", obj1 = $obj1)
    If(GetEntityMapName(entity = $obj1) == "map001") 
    {
        CallTrigger(event = "SHOW_SHOP_TITLE", object = $obj1, show = true)
        $U3Dcfg = GetWorldVar(key = "sceneUI")      -- u3d ui
        Foreach(array = $U3Dcfg, key = "ucfg")
        {

            $dict = NewDictionary($ucfg)
            $index = DictionaryGet(dict = $dict, key = "index")

             $data = {
                name = DictionaryGet(dict = $dict, key = "name"),
                width = DictionaryGet(dict = $dict, key = "width"),
                height = DictionaryGet(dict = $dict, key = "height"),
                rotate = DictionaryGet(dict = $dict, key = "rotate"),
                position = DictionaryGet(dict = $dict, key = "position"),
                args = DictionaryGet(dict = @graffitiWallText, key = $index),
            }

            Show3DUI(player = $obj1, show = true, data = $data)
        }

        $worksWallConfig = GetWorldVar(key = "worksWall")
        Show3DUI(player = $obj1, show = true, data = $worksWallConfig)
    }

    CallTrigger( object = $obj1, event = "FUNC_STATE_RELEASE", state = $map.cfg.releaseState)
}

LEAVE_MAP
{
    If(GetEntityMapName(entity = $obj1) == "map001") 
    {
        CallTrigger(event = "SHOW_SHOP_TITLE", object = $obj1, show = false)
        $U3Dcfg = GetWorldVar(key = "sceneUI")      -- u3d ui

        Foreach(array = $U3Dcfg, key = "ucfg")
        {
            Show3DUI(player = $obj1, show = false, data = $ucfg)
        }

        $worksWallConfig = GetWorldVar(key = "worksWall")
        Show3DUI(player = $obj1, show = false, data = $worksWallConfig)
    }
    CallTrigger(object = $obj1, event = "FUNC_STATE_RELEASE_SUSPEND", state = $map.cfg.releaseState)
}
--↓↓↓↓↓↓ party
CHECK_LEAVE_PARTY_FROM_DOOR {
    If($obj1.inSelfParty == 1){
        ShowDialogTip(entity = $obj1, p1 = "gui.party.ask.close", event = "CHECK_CLOSE_PARTY", tipType = 2)
    }
    Else{
        ShowDialogTip(entity = $obj1, p1 = "gui.party.ask.exit", event = "ASK_SHOW_PARTY_LIST", tipType = 2)
    }
}
PLAYER_BE_INVITE {
    --party invite will call PLAYER_BE_INVITE_OTHER_SERVER
    $userId = GetPlayerUserID(player = $obj1)
    $inPartyOwnerId = $obj1.inPartyOwnerId
    If($inPartyOwnerId ~= 0){
        Return()--in party
    }
    $obj1.visitTarget = $obj2
    $contentDict = NewDictionary($content)
    --todo: can close by showTime
    --showTime = DictionaryGet(dict = $contentDict, key = "showTime")
    ShowNotice(
		buttonType = 2,
		content = $guiText or "gui_friend_invite_notice",
        eventYes = "PLAYER_ON_VISIT",
        eventNo = "PLAYER_BE_INVITE_REFUSE",
		entity = $obj1,
		textP1 = DictionaryGet(dict = $contentDict, key = "nickName")
	)
}
PLAYER_BE_INVITE_REFUSE(){

}
PLAYER_BE_INVITE_OTHER_SERVER_REFUSE(){
    $obj1.partyInfo = nil
}
PLAYER_BE_INVITE_OTHER_SERVER {
    $userId = GetPlayerUserID(player = $obj1)
    $inPartyOwnerId = $obj1.inPartyOwnerId
    If($inPartyOwnerId ~= 0){
        Return()--in party
    }
    $contentDict = NewDictionary($content)
    $partyInfo = DictionaryGet(dict = $contentDict, key = "partyInfo")
    If($partyInfo){
        $guiText = "gui_party_invite_notice"
        $obj1.partyInfo = $partyInfo--todo:need reset
    }
    $obj1.visitTargetUserId = DictionaryGet(dict = $contentDict, key = "userId")
    ShowNotice(
        buttonType = 2,
        content = $guiText or "gui_friend_invite_notice",
        eventYes = "PLAYER_ON_VISIT_OTHER_SERVER",
        eventNo = "PLAYER_BE_INVITE_OTHER_SERVER_REFUSE",
        entity = $obj1,
        textP1 = DictionaryGet(dict = $contentDict, key = "nickName")
    )
}
PLAYER_ON_VISIT_OTHER_SERVER {
    $targetUserId = $targetUserId or $obj1.visitTargetUserId
    $contentDict = NewDictionary($content)
    $partyInfo = DictionaryGet(dict = $contentDict, key = "partyInfo")
    If($partyInfo){--can be set by PLAYER_BE_INVITE_OTHER_SERVER
        $obj1.partyInfo = $partyInfo
    }
    If($obj1.partyInfo){
        $partyDict = NewDictionary($obj1.partyInfo)
        $partyId = DictionaryGet(dict = $partyDict, key = "partyId")
        TryJoinParty(targetUserId = $targetUserId, partyId = $partyId)
    }
    Else{
        GotoOtherServer(targetUserId = $targetUserId, player = $obj1)
    }
}
PLAYER_ON_VISIT {--same server
    If(not $target){
        $target = $obj2 or $obj1.visitTarget
    }
    If(not $target){
        Return()
    }
    CallTrigger(object = $target, event = "GET_VISIT_TELE_POS", from = $obj1, fromInParty = $fromInParty)
    If($obj1.telePos and $obj1.teleMap){
        SetEntityPosition(map = $obj1.teleMap, entity = $obj1, pos = $obj1.telePos)
        $obj1.teleMap = nil
        $obj1.telePos = nil
    }
}
GET_VISIT_TELE_POS {
    $house = $obj1.house
    $isLock =$house.isLock
    $inHouse = ($house == GetMapVar(map = GetEntityMap(entity = $obj1), key = "owner"))
    If($fromInParty or not $inHouse or not $isLock){
        $toInside = true
    }
    Else{
        $id1 = GetEntityTeam(entity = $obj1)
        $id2 = GetEntityTeam(entity = $from)
        If($id1 ~= 0 and $id1 == $id2){
            $toInside = true
        }
    }
    If($toInside){
        --transfer to the targets pos
        $from.telePos = GetEntityPosition(entity = $obj1)
        $from.teleMap = GetEntityMap(entity = $obj1)
    }
    Else{
        ShowTip(entity = $from, tipType = 1, textKey = "house_lock")
        $door = $house.door
        $from.telePos = ComputeV3(p1 = GetEntityPosition(entity = $door), p2 = V3RotationYaw(vector = Table(x = 0, y = 0.5, z = 3), yaw = GetEntityYaw(entity = $door)), op = "add")
        $from.teleMap = GetEntityMap(entity = $door)
    }
}
TRY_JOIN_PARTY {
    If($obj1.excludedPartyId == $partyId){
        $canJoin = false
    }
}
TRY_CREATE_PARTY {
    If(ArrayFind(array = $obj1.privilege, value = "myplugin/create_party_privilege")){
        $canCreate = true
    }Else{
        $canCreate = false
        CallTrigger(obj1 = $obj1, event = "POP_UP_BUY_PRIVILEGE", shopIndex = 13, buyResultEvent = "BUY_CREATE_PARTY_PRIVILEGE_RESULT")
    }
}
BUY_CREATE_PARTY_PRIVILEGE_RESULT {
    If($succeed){
        TryCreateParty(player = $obj1)
    }
}
CHECK_LEAVE_PARTY {
    If($obj1.inSelfParty == 1){
        CloseParty(player = $obj1)
        Return()
    }
    If($obj1.inSelfParty == 0){
        LeaveParty(player = $obj1, targetUserId = $obj1.inPartyOwnerId)
    }
}
ASK_SHOW_PARTY_LIST {
    If($result == false) {
        LeaveParty(player = $obj1, targetUserId = $obj1.inPartyOwnerId)
        Return()
    }
    ShowDialogTip(entity = $obj1, p1 = "gui.party.ask.show.party", event = "CHECK_EXIT_PARTY", tipType = 2)
}
CHECK_CLOSE_PARTY {
    If($result == false) {Return()}
    CloseParty(player = $obj1)
}
CHECK_EXIT_PARTY {
    If($result == false) {Return()}
    LeaveParty(player = $obj1, targetUserId = $obj1.inPartyOwnerId)
}
LEAVE_PARTY_SUCCESS {
    $obj1.inPartyOwnerId = 0
    GotoOtherServer(player = $obj1)
}
CHECK_IN_PARTY {
    $userId = GetPlayerUserID(player = $obj1)
    $inPartyOwnerId = $obj1.inPartyOwnerId
    If($inPartyOwnerId ~= 0){
        $partyOwner = GetPlayerByUserId(userId = $inPartyOwnerId)
        If(not $partyOwner){
            ShowTip(entity = $obj1, tipType = 2, textKey = "gui.party.no.owner", time =  20)
            LeaveParty(player = $obj1, targetUserId = $obj1.inPartyOwnerId)
            $obj1.inPartyOwnerId = 0--just in case
            GotoOtherServer(player = $obj1)
            Return()
        }
        UpdatePartyInnerSettingUI(player = $obj1, inPartyOwnerId = $inPartyOwnerId, isShow = true)
        If($userId == $inPartyOwnerId){
            $obj1.inSelfParty = 1
            --My Party!!!!
            --broadcast!!!!!
        }
        Else{
            --In Party
            $obj1.inSelfParty = 0
            CallTrigger(fromInParty = true, target = $partyOwner, event = "PLAYER_ON_VISIT", object = $obj1)
        }
    }
}
INTERACTION_BANISH {
    ShowTip(entity = $obj1, tipType = 2, textKey = "gui.party.be.banished", time =  20)
    $owner = GetPlayerByUserId(userId = $obj1.inPartyOwnerId)
    $obj1.excludedPartyId = $owner.inPartyId
    LeaveParty(player = $obj1, targetUserId = $obj1.inPartyOwnerId)
}
CREATE_PARTY_SUCCESS {
    $obj1.inPartyOwnerId = GetPlayerUserID(player = $obj1)
    $obj1.inPartyId = $partyId
    GotoOtherServer(player = $obj1)
}
JOIN_PARTY_SUCCESS {
    $obj1.inPartyOwnerId = $targetUserId
    GotoOtherServer(player = $obj1, targetUserId = $targetUserId)
}
UPDATE_CREATE_PARTY_VIEW_INFO {
    $curHouseCfg = $obj1.curHouseCfg
    $maxPlayerNum = GetEntityCfg(fullName = $curHouseCfg, key = "partyMaxPlayerNumber")
    $partyImage = GetEntityCfg(fullName = $curHouseCfg, key = "partyImage")
    $reachPlayerNum = GetEntityCfg(fullName = $curHouseCfg, key = "reachGoodPartyPlayerNumber")
    $lowerRate = GetEntityCfg(fullName = $curHouseCfg, key = "goodPartyPlayerNumberLowerRate")
}
CLOSE_PARTY_SUCCESS {
    --get all player, send tip, resetgame
    $name = GetEntityName(entity = $obj1)
    $obj1.inPartyId = 0
    $players = NewDictionary(GetAllPlayers())
    ForeachDictionary(keyName = "_", valueName = "player", dict = $players){
        ShowTip(entity = $player, tipType = 2, textKey = "gui.party.be.closed", time =  20, textP1 = $name)
        $player.inPartyOwnerId = 0
        GotoOtherServer(player = $player)
    }
}
PARTY_BE_LIKED {}
LIKE_PARTY {}
--↑↑↑↑↑↑ party

START_PLAY_BGM  {
    $cfg = ArrayGet(array = @BGMConfigs, index = $index)
    If(not $cfg) {
        Return()
    }
    $cfg = NewDictionary($cfg)
    If(DictionaryGet(dict = $cfg, key = "needPrivilege") == "TRUE"
        and not ArrayFind(array = $obj1.privilege, value = "music_privilege"))
    {
        -- 没有特权，调用弹窗购买
        CallTrigger(obj1 = $obj1, event = "POP_UP_BUY_PRIVILEGE", shopIndex = 3)
        Return()
    }
    If($obj1.house ~= GetEntityMap(entity = $obj1).owner) {
        ShowTip(entity = $obj1, tipType = 2, textKey = "party_tip_not_your_sound")
        Return()
    }

    Foreach(array = GetMapNpc(map = GetEntityMap(entity = $obj1)), key = "entity") {
        If(GetConfigVar(obj = $entity, key = "fullName") == "myplugin/chess_body_12") {
            If($entity.bgmBuff) {
                RemoveEntityBuff(entity = $entity, buff = $entity.bgmBuff)
            }
            $entity.bgmBuff = AddEntityBuff(entity = $entity, cfg = DictionaryGet(dict = $cfg, key = "buff"), buffTime = nil)
        }
    }
    HideBgmList(player = $obj1, playingIndex = $index)
    Foreach(array = GetMapPlayer(map = GetEntityMap(entity = $obj1)), key = "player") {
        SendUpdatePlayingBgmIndex(player = $player, playingIndex = $index)
    }
}

CALLBACK_PHOTOGRAPH
{
    UploadPhoto(player = $obj1, info = $info, event = "SAVE_PHOTO")
}

SAVE_PHOTO
{
    $familyAlbum = GetObjectVar(obj = $obj1, key = "familyAlbum")
    If(not $familyAlbum)
    {
        Return()
    }
    
    $photo = {
        image = $data,
        name = "unNamed"
    }

    ArrayAppend(array = $familyAlbum, value = $photo)

    UpdateUIData(player = $obj1, ui = "win_familyAlbum", data = $familyAlbum)
}

DELETE_FAMILY_ALBUM
{
    $familyAlbum = GetObjectVar(obj = $obj1, key = "familyAlbum")
    If(not $familyAlbum)
    {
        Return()
    }
    If(not $index)
    {
        Return()
    }

    If($index <= ArraySize(array = $familyAlbum) and $index > 0)
    {
        ArrayRemove(array = $familyAlbum, index = $index)
        UpdateUIData(player = $obj1, ui = "win_familyAlbum", data = $familyAlbum)
    }
}

RENAME_FAMILY_ALBUM
{
    $familyAlbum = GetObjectVar(obj = $obj1, key = "familyAlbum")
    If(not $familyAlbum)
    {
        Return()
    }

    If(not $index)
    {
        Return()
    }

    If($index <= ArraySize(array = $familyAlbum) and $index > 0)
    {
        $photo = ArrayGet(array = $familyAlbum, index = $index)
        If($photo)
        {
            SetObjectVar(obj = $photo, key = "name", value = $name)
        }
    }

    $data = {
        index = $index,
        name = $name
    } 
    UpdateUIData(player = $obj1, ui = "win_familyAlbumRename", data = $data) 
}
