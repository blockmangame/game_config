ENTITY_ENTER
{
    CallTrigger(object = $obj1, event = "CREATR_SUBSIDIARY")
}

CREATR_HOUSE_OUTSIDE_MAIL{
    $mail = CreateNpc(map = GetEntityMap(entity = $obj1), cfgName = GetConfigVar(obj = $obj1, key = "mail"), owner = GetObjectVar(obj = $obj1, key = "owner"))
    SetObjectVar(obj = $mail, key = "house", value = $obj1)
    SetObjectVar(obj = $obj1, key = "mail", value = $mail)

    StartTimer(rep = false, time = 1, object = $obj1, mail = $mail, event = "CHANGE_MAIL_YAW")
}

CREATR_SUBSIDIARY{
    ------------------------------------------ 创建邮箱
    ------------------------------------------ 房子 <house-------mail> 邮箱
    StartTimer(time = 1, object = $obj1, event = "CREATR_HOUSE_OUTSIDE_MAIL") -- 延迟一帧是因为需要用到上一帧创建完才设置的owner
    ------------------------------------------ 创建房门，房门播放开门的
    ------------------------------------------ 房子 <house-------door> 房门
    $door = CreateNpc(map = GetEntityMap(entity = $obj1), cfgName = GetConfigVar(obj = $obj1, key = "door"))
    SetObjectVar(obj = $door, key = "house", value = $obj1)
    SetObjectVar(obj = $obj1, key = "door", value = $door)

    StartTimer(rep = false, time = 1, object = $obj1, door = $door, event = "CHANGE_DOOR_YAW")
    ------------------------------------------ 创建内部房子（创建动态地图）
    ------------------------------------------ 房子 <owner-------houseInsideMap> 内部房子
    $map = CreateMap(name = GetConfigVar(obj = $obj1, key = "insideMap"))
    SetObjectVar(obj = $obj1, key = "houseInsideMap", value = $map)
    SetMapVar(map = $map, key = "owner", value = $obj1)
    StartTimer(time = 1, object = $obj1, event = "CREATR_HOUSE_INSIDE_SUBSIDIARY") -- 延迟一帧是因为需要用到上一帧创建完才设置的owner
}

CHANGE_MAIL_YAW{
    $yaw = GetEntityYaw(entity = $obj1)
    SetEntityYaw(entity = $mail, rotationYaw = $yaw)
    SetBodyYaw(entity = $mail, yaw = $yaw)
    SetEntityPosition(entity = $mail, map = GetEntityMap(entity = $obj1), 
        pos = ComputeV3(p1 = GetEntityPosition(entity = $obj1), p2 = V3RotationYaw(vector = GetConfigVar(obj = $obj1, key = "mailPos") or {x = 0,y = 0, z = 0}, yaw = $yaw), op = "add"))
}

CHANGE_DOOR_YAW{
    $yaw = GetEntityYaw(entity = $obj1)
    SetEntityYaw(entity = $door, rotationYaw = $yaw)
    SetBodyYaw(entity = $door, yaw = $yaw)
    SetEntityPosition(entity = $door, map = GetEntityMap(entity = $obj1), 
        pos = ComputeV3(p1 = GetEntityPosition(entity = $obj1), p2 = V3RotationYaw(vector = GetConfigVar(obj = $obj1, key = "doorPos") or {x = 0,y = 0, z = 0}, yaw = $yaw), op = "add"))
    StartTimer(time = 1, object = $door, event = "CREATE_DOOR_REGION")
    StartTimer(time = 1, object = $door, event = "CREATE_DOOR_BLOCK_RIDE_FILED")
}

CREATR_HOUSE_INSIDE_SUBSIDIARY{
    $houseVars = GetObjectVar(obj = GetObjectVar(obj = $obj1, key = "owner"), key = "houseVars")
    ForLoop(from = 1, to = ArraySize(array = $houseVars), key = "index"){
        $dict = NewDictionary(ArrayGet(array = $houseVars, index = $index))
        If(DictionaryGet(dict = $dict, key = "isCurEquip") == "enable"){
            $curHousedict = $dict
            Break()
        }
    }
    ------------------------------------------ 内部各区域的颜色，没有则默认颜色，vars里面取，此处的颜色是替换方块 // 已在自定义客户端实现
    $map = GetObjectVar(obj = $obj1, key = "houseInsideMap")
    ------------------------------------------ 内部各个家具
    ------------------------------------------ 房子 <house------- 家具
    $furnitures = DictionaryGet(dict = $curHousedict, key = "houseInsideFurnishing") or {}
    $lastInterFruId = DictionaryGet(dict = $curHousedict, key = "lastInterFruId")
    ForLoop(from = 1, to = ArraySize(array = $furnitures), key = "findex"){
        $fDict = NewDictionary(ArrayGet(array = $furnitures, index = $findex))
        $temp = DictionaryGet(dict = $fDict, key = "pos")
        $pos = {
            x = GetPosX(pos = $temp),
            y = GetPosY(pos = $temp),
            z = GetPosZ(pos = $temp)
        } -- 这样拼接的原因是直接把pos传入会导致pos内部值多了个map，而这个是不需要的，故此处拼接一个临时的pos

        $npc = CreateNpc(map = $map, cfgName = DictionaryGet(dict = $fDict, key = "cfg"), 
            pos = $pos, ry = DictionaryGet(dict = $fDict, key = "yaw") or 0, rp = DictionaryGet(dict = $fDict, key = "pitch") or 0, owner = $obj1.owner)
        If($npc){
            SetEntityRoll(entity = $npc, rotationRoll = DictionaryGet(dict = $fDict, key = "roll") or 0)
            $owner = GetObjectVar(obj = $obj1, key = "owner")
            If($lastInterFruId and $lastInterFruId == DictionaryGet(dict = $fDict, key = "interFruId")){
                StartTimer(object =$npc, obj1 =$npc, owner = $owner, time = 3, event = "CLAC_PUSHOUT_BLOCK")
            }
            $npc.lastSideNormal = DictionaryGet(dict = $fDict, key = "lastSideNormal") or Table(x = 0,y = 1,z = 0)
            -- UpdateEntityDateToClient(entity = $npc, key = "lastSideNormal", value = $npc.lastSideNormal)
            Foreach(array = DictionaryGet(dict = $fDict, key = "saveSkinList") or {}, key = "skinTb"){
                $skinDict = NewDictionary($skinTb)
                If(DictionaryGet(dict = $skinDict, key = "skin")){
                    SetSkin(entity = $npc, skinName = DictionaryGet(dict = $skinDict, key = "skinName"), skinValue = DictionaryGet(dict = $skinDict, key = "skin"))
                }
            }
            $interFruId = Concat(p1 = DictionaryGet(dict = $fDict, key = "cfg"), p2 = GetGameTime(), p3 = $findex)
            DictionarySet(dict = $fDict, key = "interFruId", value = $interFruId)
            $npc.interFruId = $interFruId
            DictionarySet(dict = $fDict, key = "furnishingId", value = GetObjectID(object = $npc))
            SetObjectVar(obj = $npc, key = "house", value = $obj1)
            ArraySet(array = $furnitures, index = $findex, value = DictionaryToTable(dict = $fDict))
            SetObjectVar(obj = $npc, key = "blackboardStr", value = DictionaryGet(dict = $fDict, key = "blackboardStr"))
            CallTrigger(object = $owner, obj1 = $owner, npc = $npc, add = true, event = "UPDATE_PLAYER_3DUI_ENTITY")

            CallTrigger(object = $owner, obj1 = $owner, npc = $npc, add = true, event = "CREATE_PLAYER_SINGLE_NUMBER_BOARD_ENTITY")

            CallTrigger(object = $owner, obj1 = $owner,  npc = $npc, add = true, event = "CREATE_PLAYER_SINGLE_BLACK_BOARD_ENTITY")     
            If(GetConfigVar(obj = $npc, key = "isBgmSpeakers")) {
                $counts = GetMapVar(map = $map, key = "SpeakersCount")
                If(not $counts){
                    $counts = 0
                }
                $counts = $counts + 1
                SetMapVar(map = $map, key = "SpeakersCount", value = $counts)
            }
        }
    }
    ------------------------------------------ 内部门
    ------------------------------------------ 房子 <house-------insideDoor> 内部门
    CallTrigger(object = $obj1, event = "CREATE_INSIDE_DOOR", map = $map, cfgName = DictionaryGet(dict = $curHousedict, key = "houseInsideDoor") or GetConfigVar(obj = $obj1, key = "insideDoor"))
}

CHANGE_HOUSE_REGION_COLOR{ -- 该替换为简单的方块替换 无排序无顺序
    $center = GetRegionCenter(region = $region)
    $max = GetRegionMax(region = $region)
    $pos = {x = GetPosX(pos = $center), y = GetPosY(pos = $max), z = GetPosZ(pos = $center)}
    $block = GetBlockConfig(map = $map, pos = V3ToBlockPos(v3 = $pos), key = "fullName")
    If($destBlock and "" ~= $destBlock){
        $destBlockTypes = GetBlockConfigByName(block = $destBlock, key = "destBlockTypes")
        If($destBlockTypes){
            ReplaceBlockInRegion(map = $map, region = $region, replaceTb = [{key = "fullName", value = $block, destBlock = $destBlock}])
        }
        Else{
            ReplaceBlockInRegion(map = $map, region = $region, replaceTb = [{key = "fullName", value = $block, destBlock = $destBlock}])
        }
    }
}

CREATE_INSIDE_DOOR{
    $insideDoor = CreateNpc(map = $map, cfgName = $cfgName, rp = 0, ry = 0, owner = $obj1.owner)
    SetEntityPosition(map = $map, entity = $insideDoor, pos = GetConfigVar(obj = $obj1, key = "insideDoorPos") or {x = 0,y = 0, z = 0}, ry = 0)
    StartTimer(time = 1, object = $insideDoor, event = "CREATE_DOOR_REGION")
    StartTimer(time = 1, object = $obj1, insideDoor = $insideDoor, event = "CHANGE_INSIDE_DOOR_YAW")
    SetObjectVar(obj = $insideDoor, key = "house", value = $obj1)
    SetObjectVar(obj = $obj1, key = "insideDoor", value = $insideDoor)
}

CHANGE_INSIDE_DOOR_YAW{
    SetBodyYaw(entity = $insideDoor, yaw = 0)
}

ENTITY_CLICK{
    If(not IsPlayer(entity = $obj2)){
        Return()
    }
    If(GetObjectVar(obj = $obj1, key = "isLock")){
        ShowTip(entity = $obj2, tipType = 2, textKey = "house_lock")
        Return()
    }
}

OPEN_THE_DOOR{
    If(not IsPlayer(entity = $player)){
        Return()
    }
    If(GetObjectVar(obj = $obj1, key = "isLock")){
        Return()
    }
    -- 此处门entity播放动作即可？房子entity不播放？
    PlayAction(entity = $obj1, target = $player, actionName = "openDoor", actionTime = -1)
}

CLOSE_THE_DOOR{
    If(not IsPlayer(entity = $player)){
        Return()
    }
    If(GetObjectVar(obj = $obj1, key = "isLock")){
        Return()
    }
    -- 此处门entity播放动作即可？房子entity不播放？
    PlayAction(entity = $obj1, target = $player, actionName = "closeDoor", actionTime = -1)
}

ENTITY_DIE{
    $outsideDoor = GetObjectVar(obj = $obj1, key = "door")
    $outsideDoorPos = GetEntityPosition(entity = $outsideDoor)
    $outsideDoorMap = GetEntityMap(entity = $outsideDoor)
    KillEntity(entity = GetObjectVar(obj = $obj1, key = "mail"))
    KillEntity(entity = $outsideDoor)
    $insideDoor = GetObjectVar(obj = $obj1, key = "insideDoor")
    KillEntity(entity = $insideDoor)

    ------------------------------------------ 销毁内部房子
    $map = GetObjectVar(obj = $obj1, key = "houseInsideMap")
    $allPlayer = GetMapPlayer(map = $map)
    Foreach(array = $allPlayer, key = "player"){
        ShowTip(entity = $player, tipType = 2,textKey = "house_change_and_kick_you", keepTime = 40)
    }
    -- SetEntityPosition(entity = $door, map = GetEntityMap(entity = $obj1), 
    --     pos = ComputeV3(p1 = GetEntityPosition(entity = $obj1), p2 = V3RotationYaw(vector = GetConfigVar(obj = $obj1, key = "doorPos") or {x = 0,y = 0, z = 0}, yaw = $yaw), op = "add"))
    MoveMapPlayerTo(map = $map, newmap = $outsideDoorMap, pos = ComputeV3(p1 = GetEntityPosition(entity = $obj1), 
        p2 = V3RotationYaw(vector = Table(x = 0, y = 0.1, z = 9), yaw = GetEntityYaw(entity = $outsideDoor)), op = "add"))
    CloseMap(map = $map)
}