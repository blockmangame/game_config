ENTITY_ENTER
{
    CallTrigger(object = $obj1, event = "CREATR_SUBSIDIARY")
}

CREATR_SUBSIDIARY{
    Test("CREATR_SUBSIDIARY")
    ------------------------------------------ 创建邮箱
    ------------------------------------------ 房子 <house-------mail> 邮箱
    $mail = CreateNpc(map = GetEntityMap(entity = $obj1), cfgName = GetConfigVar(obj = $obj1, key = "mail"))
    SetEntityPosition(entity = $mail, pos = 
        ComputeV3(p1 = GetEntityPosition(entity = $obj1), p2 = GetConfigVar(obj = $obj1, key = "mailPos") or {x = 0,y = 0, z = 0}, op = "add"))
    SetObjectVar(obj = $mail, key = "house", value = $obj1)
    SetObjectVar(obj = $obj1, key = "mail", value = $mail)

    ------------------------------------------ 创建房门，房门播放开门的
    ------------------------------------------ 房子 <house-------door> 房门
    $door = CreateNpc(map = GetEntityMap(entity = $obj1), cfgName = GetConfigVar(obj = $obj1, key = "door"))
    SetEntityPosition(entity = $door, pos = 
        ComputeV3(p1 = GetEntityPosition(entity = $obj1), p2 = GetConfigVar(obj = $obj1, key = "doorPos") or {x = 0,y = 0, z = 0}, op = "add"))
    SetObjectVar(obj = $door, key = "house", value = $obj1)
    SetObjectVar(obj = $obj1, key = "door", value = $door)

    ------------------------------------------ 创建内部房子（创建动态地图）
    ------------------------------------------ 房子 -------houseInsideMap> 内部房子
    $map = CreateMap(name = GetConfigVar(obj = $obj1, key = "insideMap"))
    SetObjectVar(obj = $obj1, key = "houseInsideMap", value = $map)
    StartTimer(time = 1, object = $obj1, event = "CREATR_HOUSE_INSIDE_SUBSIDIARY") -- 延迟一帧是因为需要用到上一帧创建完才设置的owner
}

CREATR_HOUSE_INSIDE_SUBSIDIARY{
    Test("CREATR_HOUSE_INSIDE_SUBSIDIARY")
    $curSelectHouse = {}
    Foreach(array = GetObjectVar(obj = GetObjectVar(obj = $obj1, key = "owner"), key = "houseVars"), key = "house"){
        If(ArrayGet(array = $house, index = 5) == "enable"){
            $curSelectHouse = $house
            Break()
        }
    }
    ------------------------------------------ 内部各区域的颜色，没有则默认颜色，vars里面取，此处的颜色是替换方块
    $map = GetObjectVar(obj = $obj1, key = "houseInsideMap")
    $allRegion = GetAllRegion(map = $map)
    -- //获取当前房子的内部regionColor 即存进数据库的
    $regionColor = ArrayGet(array = $curSelectHouse, index = 4) or {}
    Foreach(array = $allRegion, key = "region"){
        $hadRegion = false
        $regionkey = GetRegionConfig(region = $region, key = "regionkey")
        $childRegionKeyArr = GetRegionConfig(region = $region, key = "childRegionKey") or {}
        $defaultChildRegionBlock = GetRegionConfig(region = $region, key = "defaultChildRegionBlock") or {}
        Foreach(array = $regionColor, key = "regionColorTb"){
            If(ArrayGet(array = $regionColorTb, index = 1) == $regionkey){
                $hadRegion = true
                $childRegionColorArr = ArrayGet(array = $regionColorTb, index = 2) or {}
                ForLoop(from = 1, to = ArraySize(array = $childRegionKeyArr), key = "index"){
                    $childRegionArr = GetRegionConfig(region = $region, key = ArrayGet(array = $childRegionKeyArr, index = $index)) or {}
                    Foreach(array = $childRegionArr, key = "cRegion"){
                        FillBlocksInRegion(map = $map, region = $cRegion, 
                            block = ArrayGet(array = $childRegionColorArr, index = $index) or 
                            ArrayGet(array = $defaultChildRegionBlock, index = $index) or 
                            GetRegionConfig(region = $region, key = "defaultBlock")
                        )
                    }
                }
                Break()
            }
        }
        If(not $hadRegion){
            Foreach(array = $childRegionKeyArr, key = "key"){
                Foreach(array = GetRegionConfig(region = $region, key = $key) or {}, key = "cRegion"){
                    FillBlocksInRegion(map = $map, region = $cRegion, 
                        block = GetRegionConfig(region = $region, key = "defaultBlock"))
                }
            }
        }
    }
    ------------------------------------------ 内部各个家具
    ------------------------------------------ 房子 <house------- 家具
    $furnitures = ArrayGet(array = $curSelectHouse, index = 3) or {}
    Foreach(array = $furnitures, key = "furniture"){
        $temp = ArrayGet(array = $furniture, index = 1)
        $pos = {
            x = GetPosX(pos = $temp),
            y = GetPosY(pos = $temp),
            z = GetPosZ(pos = $temp)
        } -- 这样拼接的原因是直接把pos传入会导致pos内部值多了个map，而这个是不需要的，故此处拼接一个临时的pos
        $npc = CreateNpc(map = $map, cfgName = ArrayGet(array = $furniture, index = 3), 
            pos = $pos, ry = ArrayGet(array = $furniture, index = 2))
        $yaw = ArrayGet(array = $furniture, index = 2)
        If($yaw and 0 ~= $yaw){
            $ry = 90
            If(90 == $yaw or -270 == $yaw){
                $ry = 90
            }ElseIf(180 == $yaw or -180 == $yaw){
                $ry = 180
            }ElseIf(270 == $yaw or -90 == $yaw){
                $ry = 270
            }
            AddEntityBuff(entity = $npc, cfg = Concat(p1= "myplugin/rotate_bounding_box_buff_", p2 = $ry))
        }
        ArraySet(array = $furniture, index = 4, value = GetObjectID(object = $npc))
        SetObjectVar(obj = $npc, key = "house", value = $obj1)
    }
    ------------------------------------------ 内部门
    ------------------------------------------ 房子 <house-------insideDoor> 内部门
    $insideDoor = CreateNpc(map = $map, cfgName = GetConfigVar(obj = $obj1, key = "insideDoor"))
    SetEntityPosition(map = $map, entity = $insideDoor, pos = GetConfigVar(obj = $obj1, key = "insideDoorPos") or {x = 0,y = 0, z = 0})
    SetObjectVar(obj = $insideDoor, key = "house", value = $obj1)
    SetObjectVar(obj = $obj1, key = "insideDoor", value = $insideDoor)
}

ENTITY_CLICK{
    If(not IsPlayer(entity = $obj2)){
        Return()
    }
    If(GetObjectVar(obj = $obj1, key = "isLock")){
        Test(" don't touch me ! ")
        ShowTip(entity = $obj2, tipType = 1, textKey = "house_lock")
        Return()
    }
    Test(" come in ! ")
}

OPEN_THE_DOOR{
    If(not IsPlayer(entity = $player)){
        Return()
    }
    If(GetObjectVar(obj = $obj1, key = "isLock")){
        Return()
    }
    -- 此处门entity播放动作即可？房子entity不播放？
    PlayAction(entity = GetObjectVar(obj = $obj1, key = "door"), target = $player, actionName = "attack", actionTime = -1)
    Test(" yes sir ! ")
}

CLOSE_THE_DOOR{
    If(not IsPlayer(entity = $player)){
        Return()
    }
    If(GetObjectVar(obj = $obj1, key = "isLock")){
        Return()
    }
    -- 此处门entity播放动作即可？房子entity不播放？
    PlayAction(entity = GetObjectVar(obj = $obj1, key = "door"), target = $player, actionName = "attack", actionTime = -1)
    Test(" yes madan ! ")
}

ENTITY_DIE{
    $outsideDoor = GetObjectVar(obj = $obj1, key = "door")
    $outsideDoorPos = GetEntityPosition(entity = $outsideDoor)
    $outsideDoorMap = GetEntityMap(entity = $outsideDoor)
    KillEntity(entity = GetObjectVar(obj = $obj1, key = "mail"))
    KillEntity(entity = $outsideDoor)
    $insideDoor = GetObjectVar(obj = $obj1, key = "insideDoor")
    KillEntity(entity = $insideDoor)

    ------------------------------------------ 销毁内部房子
    $map = GetObjectVar(obj = $obj1, key = "houseInsideMap")
    MoveMapPlayerTo(map = $map, newmap = $outsideDoorMap, pos = ComputeV3(p1 = $outsideDoorPos, p2 = Table(x = 0, y = 0.1, z = 5), op = "add"))
    CloseMap(map = $map)
}