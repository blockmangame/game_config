GAME_INIT 
{
    @BGMConfigs = ReadGameCsv(path = "bgm.csv")
    @CDReward = ReadGameCsv(path = "cd_reward.csv")
    @Generate_State_List = ["hunger", "thirsty", "dirty", "fatigue", "ill", "region"]
    @skillPath = "myplugin/skill_state_"
    CallTrigger(event = "FUNC_STATE_STARTTIMER")
    @mathHuge = 10000000

    @DECORATION_STORE = ReadGameCsv(path = "decoration_store.csv")
    SortArrayByKey(array = @DECORATION_STORE, key = "itemsort", isToNumberKey = true)

    @CURRENT_ID_IMAGE = [
        "set:diamond.json image:Diamond-icon2.png",
        "set:diamond.json image:Diamond-icon2.png",
        "set:diamond.json image:Diamond-icon2.png",
        "plugin/myplugin/image/coin.png"
    ]

    @interactionWithMovementEventMap = {
        WindowTouchDown = {
            startHorn = "START_HORN",
            forward = "START_FORWARD",
            back = "START_BACK"
        },
        WindowTouchUp = {
            debark = "DEBARK",
            stopHorn = "STOP_HORN",
            forward = "STOP_FORWARD",
            back = "STOP_BACK"
        },
        MotionRelease = {
            forward = "STOP_FORWARD",
            back = "STOP_BACK"
        }
    }

    CallTrigger(event = "WORKS_WALL_TIMER_START")
    CallTrigger(event = "INIT_WORK_TASK_DATA")
    CallTrigger(event = "FUNC_NAVIGATION_GET_MAPS_ALL_PATH")
    CallTrigger(event = "FUNC_LOAD_WORKS_WALL_TOP")
    @Map001 = GetStaticMap(name = "map001", create = true)--load map001
    CallTrigger(event = "CREATE_EMAIL_NPC", npc1 = true, npc2 = true)
    CallTrigger(event = "SHOW_SHOP_TITLE")
    @FamilyCreateDuration = NewDictionary({})   -- 保存家庭创建的时间，用来数据统计
    @FamilyName = NewDictionary({})             -- 保存队伍的名字

    --
    @HOME_SUM = 0
}
--创建邮箱npc
CREATE_EMAIL_NPC
{   
    If($npc2)
    {
        $pos = GetConfigValue(type = "entity", key = "pos", fullName = "myplugin/email_entity2")
        $ry = GetConfigValue(type = "entity", key = "ry", fullName = "myplugin/email_entity2")
        $rp = GetConfigValue(type = "entity", key = "rp", fullName = "myplugin/email_entity2")
        @ENTITY_ENTER2 = CreateNpc(pos = $pos, map = @Map001, cfgName = "myplugin/email_entity2", ry = $ry, rp = $rp)
    }
    If($npc1)
    {
        $pos = GetConfigValue(type = "entity", key = "pos", fullName = "myplugin/email_entity1")
        $ry = GetConfigValue(type = "entity", key = "ry", fullName = "myplugin/email_entity1")
        $rp = GetConfigValue(type = "entity", key = "rp", fullName = "myplugin/email_entity1")
        @ENTITY_ENTER1 = CreateNpc(pos = $pos, map = @Map001, cfgName = "myplugin/email_entity1", ry = $ry, rp = $rp)
    }
}
--场景商店的牌子
SHOW_SHOP_TITLE(){
    $mapName = "map001"
    $cfg = GetWorldVar(key = "ShopTitle")
    $Dict = NewDictionary($cfg or {})
    $array = DictionaryGet(dict = $Dict, key = $mapName)
    Foreach(array = $array or {}, key = "data"){
        AddSceneUI(map = @Map001, data = $data)
    }
}
INIT_WORK_TASK_DATA(){    --打工配置表的读取
    $mapcfgarray = GetMapConfig(name = "map001", key = "entity")
    @worlkMoeny = NewDictionary({}) --奖励表
    @failedMsg = NewDictionary({})  --对话表
    @taskMap = NewDictionary({}) --
    @TASK_TABLE = NewArray() --打工的任务表
    @TASK_WEIGHT = NewArray() --打工随机比重表
    @INDENT_POS = GetConfigValue(type = "task", fullName = "myplugin/work", key = "indentPos")--订单领取位置
    @CircleNum = GetConfigValue(type = "task", fullName = "myplugin/work", key = "circleNum")
    @workmaxCount = @CircleNum --GetConfigValue(type = "task", fullName = "myplugin/work", key = "maxCount") or 60--最大限度
    @NumCycle = @CircleNum

    @punishmentTime = GetConfigValue(type = "task", fullName = "myplugin/work", key = "punishmentTime") --惩罚时间
    $taskData = GetConfigValue(type = "task", fullName = "myplugin/work", key = "tasks")
    Foreach(array = $taskData, key = "Dict"){
        $taskDict = NewDictionary($Dict)
        $arg = DictionaryGet( dict = $taskDict, key = "targets")
        $targets = NewDictionary(ArrayGet(array = $arg, index = 1)) 
        $value = NewDictionary()
        $npcName = DictionaryGet(dict = $targets, key = "cfgName")
        $msg = DictionaryGet(dict = $taskDict, key = "succeedMsg")
        $pos = DictionaryGet(dict = $taskDict, key = "npcPos")
        DictionarySet(dict = $value,  key = "taskKey" , value = DictionaryGet(dict = $taskDict, key = "fullName"))
        DictionarySet(dict = $value,  key = "npcName" , value = $npcName)
        DictionarySet(dict = $value,  key = "npcMap" , value = "map001")
        DictionarySet(dict = $value,  key = "failedMsg" , value = DictionaryGet(dict = $taskDict, key = "failedMsg"))
        DictionarySet(dict = $value,  key = "succeedMsg" , value = $msg)
        $weight =  DictionaryGet(dict = $taskDict, key = "weight")
        DictionarySet(dict = $value,  key = "weight" , value = $weight)
        ArrayAppend(array = @TASK_WEIGHT, value = $weight)
        DictionarySet(dict = @failedMsg, key = $npcName, value = $msg)
       
        Foreach(array = $mapcfgarray, key = "mapcfg"){
            $mapdict = NewDictionary($mapcfg)
            $mapNpcName = DictionaryGet(dict = $mapdict, key = "cfg")
            $mapNpcPos = DictionaryGet(dict = $mapdict, key = "pos")
            If($mapNpcName == $npcName){
                $copyPos = Copy(value = $mapNpcPos)
                DictionarySet(dict = $value,  key = "pos" , value = $copyPos)
            }
        }

        ArrayAppend(array = @TASK_TABLE, value = $value)
    }
    ForLoop(from=1, to = @CircleNum, key = "index"){
        $redict = GetConfigValue(type = "reward", fullName = "myplugin/task_money", key = $index)
        If(not $redict){
            Break()
        }
        $mdict = NewDictionary($redict)
        DictionarySet(dict = @worlkMoeny, key = DictionaryGet(dict = $mdict, key = "index"), value = $mdict)
    }
}

FUNC_STATE_STARTTIMER
{
    ForLoop(from = 1, to = ArraySize(array = @Generate_State_List), key = "idx")
    {
        $stateBase = ArrayGet(array = @Generate_State_List, index = $idx)
        $keyStr = Concat(p1 = "myplugin/skill_state_", p2 = $stateBase)
        $timeInfo = GetSkillVar(fullName = $keyStr, key = "generateTime")

        $cdTime = ArrayGet(array = $timeInfo, index = 1)
        StartTimer(column = 1, rep = false, timeArr = $timeInfo, stateBase = $stateBase, event = "FUNC_BROADCAST_STATE", time = ($cdTime * 1200) // 1)
    }
}

FUNC_BROADCAST_STATE
{
    Foreach(array = GetAllPlayers(), key = "player"){
        If($player.inPartyOwnerId ~= 0){
            Continue()
        }
        SetObjectVar(obj = $player, value = 0, key = Concat(p1 = $stateBase, p2 = "RewardCount"))

        $skill = Concat(p1 = @skillPath, p2 = $stateBase)
        $generateType = GetSkillVar(fullName = $skill, key = "generateType")
        If($generateType == "A" && GetObjectVar(obj = $player, key = Concat(p1 = "had", p2 = $stateBase)))
        {
            Continue()
        }
        
        If($generateType == "B" && GetObjectVar(obj = $player, key = Concat(p1 = $stateBase, p2 = "UsedTime")))
        {
            Continue()
        }

        CallTrigger(object = $player, event = "FUNC_STATE_GENERATE", stateBase = $stateBase, col = $column, skill = $skill)
	}

    $curTime = ArrayGet(array = $timeArr, index = $column)
    $column = $column + 1
    If($column > ArraySize(array = $timeArr))
    {
        $column = 1
    }
    $endTime = ArrayGet(array = $timeArr, index = $column)
    $cdTime = $endTime - $curTime
    If($cdTime <= 0)
    {
        $cdTime = 60 - $curTime + $endTime
    }

    StartTimer(rep = false, event = $event, column = $column, timeArr = $timeArr, stateBase = $stateBase, time = ($cdTime * 1200) // 1)
}

SHOW_SELECT_ROLE
{
    $options = [
        {
            image = "set:select_role.json image:child",
            pushImage = "set:select_role.json image:child",
            event = "CHANGE_PLAYER_SCALE_CHILD",
            detail = "ui_pick_child"
        },
        {
            image = "set:select_role.json image:adult",
            pushImage = "set:select_role.json image:adult",
            event = "CHANGE_PLAYER_SCALE_ADULT",
            detail = "ui_pick_adult"
        }
    ]

    ShowCardOptionsView(
        entity = $obj1,
        uiCfg = "myplugin/select_role",
        title = "ui_title_select_role",
        options = $options
    )
}

CHANGE_PLAYER_SCALE_ADULT
{
    If(IsPlayer(entity = $obj1) and not $obj1.checkSignIn){
        CheckSignInShow(player = $obj1)
        $obj1.checkSignIn = true
    }
    If($obj1.scaleBuff)
    {
        RemoveEntityBuff(entity = $obj1, buff = $obj1.scaleBuff)
        $obj1.scaleBuff = nil
    }

    If($obj1.boundingBuff)
    {
        RemoveEntityBuff(entity = $obj1, buff = $obj1.boundingBuff)
        $obj1.boundingBuff = nil
    }

    If($obj1.interactionBuff ~= nil && $obj1.role ~= @adult)
    {
        CallTrigger(obj1 = $obj1, event = "ENTITY_REMOVE_INTERACTION")
    }

    $obj1.scaleBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/scale_adult")
    $obj1.boundingBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/bounding_adult")
    $obj1.role = @adult

    If(IsPlayer(entity = $obj1))
    {
        UpdateUIData(player = $obj1, ui = "win_dressArchiveScale", data = 1)
        CallTrigger(object = $obj1, event = "CHANGE_BABY_TROLLEY_SCALE")
    }
    CallTrigger(obj1 = $obj1, event = "ADD_SVIP_BUFF", object = $obj1)
}

CHANGE_PLAYER_SCALE_CHILD
{
    If(IsPlayer(entity = $obj1) and not $obj1.checkSignIn){
        CheckSignInShow(player = $obj1)
        $obj1.checkSignIn = true
    }
    If($obj1.scaleBuff)
    {
        RemoveEntityBuff(entity = $obj1, buff = $obj1.scaleBuff)
        $obj1.scaleBuff = nil
    }

    If($obj1.boundingBuff)
    {
         RemoveEntityBuff(entity = $obj1, buff = $obj1.boundingBuff)
         $obj1.boundingBuff = nil
    }

    If($obj1.interactionBuff ~= nil && $obj1.role ~= @child)
    {
        CallTrigger(obj1 = $obj1, event = "ENTITY_REMOVE_INTERACTION")
    }

    $obj1.scaleBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/scale_child")
    $obj1.boundingBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/bounding_child")
    $obj1.role = @child
    
    If(IsPlayer(entity = $obj1))
    {
        UpdateUIData(player = $obj1, ui = "win_dressArchiveScale", data = 0.8)
        CallTrigger(object = $obj1, event = "CHANGE_BABY_TROLLEY_SCALE")
    }
    CallTrigger(obj1 = $obj1, event = "ADD_SVIP_BUFF", object = $obj1)
}

FUNC_CLOSE_GPS --params: destination[, hideTip]
{
    $gpsDict = GetObjectVar(obj = $obj1, key = "gpsDict")
    If($destination and $gpsDict)
    {
        $dictDestination = DictionaryGet(dict = $gpsDict, key = "destination")
    }
    If(not $destination || $destination == $dictDestination)
    {
        If(not $hideTip and $destination)
        {
            ShowTip(entity = $obj1, tipType = 3, textKey = "you_came")
        }
        SetGuideTarget(entity = $obj1)
        SetObjectVar(obj = $obj1, key = "gpsDict", value = nil)
    }
}

FUNC_OPEN_GPS --params: gpsDict
{
    $obj1.taskShowGps = false
    SetGuideTarget(pos = $gps, entity = $obj1, guideSpeed = 0.05)
    SetObjectVar(obj = $obj1, key = "gpsDict", value = $gpsDict)
    ShowTip(entity = $obj1, tipType = 3, textKey = "openning_gps")
}

FUNC_CLOSE_DETAILS_UI --params: uiName
{
    If(not $uiName || $uiName == GetObjectVar(obj = $obj1, key = "detailsUI"))
    {
        ShowDetails(entity = $obj1, isOpen = false)
        SetObjectVar(obj = $obj1, key = "detailsUI", value = nil)
    }
}

FUNC_STOP_TIMER --params: timerStr
{
    $timer = GetObjectVar(obj = $obj1, key = $timerStr)
    If(not $timer)
    {
        Return()
    }
    StopTimer(timer = $timer)
    SetObjectVar(obj = $obj1, key = $timerStr, value = nil)
}

FUNC_REMOVE_BUFF --params: buffStr
{
    $hadBuff = GetObjectVar(obj = $obj1, key = $buffStr)
    If(not $hadBuff)
    {
        Return()
    }
    RemoveEntityBuff(entity = $obj1, buff = $hadBuff)
    SetObjectVar(obj = $obj1, key = $buffStr, value = nil)
}

FUNC_STATE_ADD_INTERACTLIST --params: entities
{
    Foreach(array = @Generate_State_List, key = "stateBase")
    {
        $listName = Concat(p1 = $stateBase, p2 = "InteractList")
        Foreach(array = $entities, key = "entity")
        {
            If(not IsPlayer(entity = $entity) || not GetObjectVar(obj = $entity, key = Concat(p1 = "had", p2 = $stateBase)))
            {
                Continue()
            }
            $interactList = GetObjectVar(obj = $entity, key = $listName) or []
            Foreach(array = $entities, key = "cp")
            {
                $cpID = GetObjectID(object = $cp)
                If($cp == $entity || not IsPlayer(entity = $cp) || ArrayFind(array = $interactList, value = $cpID))
                {
                    Continue()
                }
                ArrayAppend(array = $interactList, value = $cpID)
            }
            SetObjectVar(obj = $entity, key = $listName, value = $interactList)
        }
    }
}

WORKS_WALL_TIMER_START
{
    @userPublishWorks = NewDictionary({})
    $worksWallConfig = GetWorldVar(key = "worksWalls")
    If($worksWallConfig == nil)
    {
        Return()
    }

    $time = GetWorldVar(key = "worksWallUpdateTime")
    Foreach(array = $worksWallConfig, key = "data")
    {
        $dict = NewDictionary($data)
        $name = DictionaryGet(dict = $dict, key = "name")
        StartTimer(rep = true, time = $time, event = "WORKS_WALL_UPDATE", wallName = $name)
    }
}

SYNC_CUR_WALL_WORKS
{
    If(@worksWallIndex ~= nil and @worksWalls ~= nil)
    {
        $dictValue = NewDictionary(@worksWallIndex)
        ForeachDictionary(dict = $dictValue, keyName = "wallName", valueName = "wallIndex")
        {
           $dictWall = NewDictionary(@worksWalls)
           $worksList = DictionaryGet(dict = $dictWall, key = $wallName)
           If($worksList)
           {
               $works = ArrayGet(array = $worksList, index = $wallIndex)
               If($works)
               {
                   UpdateUIData(player = $player, ui = $wallName, data = $works)
               }
           }
        }
    }
}

PLAYER_PUBLISH_WORKS
{
    --$worksId
    $wallName = "curServerPublishWorks"

    If(not @worksWalls)
    {
       @worksWalls = {}
    }

    $dictValue = NewDictionary(@worksWalls)
    $worksList = DictionaryGet(dict = $dictValue, key = $wallName)
    If(not $worksList)
    {
       $worksList = {}
    }

    If(not @userPublishWorks)
    {
        @userPublishWorks = {}
    }

    ArrayAppend(array= $worksList, value = {graffitiId = $worksId})
    DictionarySet(dict = $dictValue, key = $wallName, value = $worksList)
    @worksWalls = DictionaryToTable(dict = $dictValue)

    If(ArraySize(array= $worksList) == 1)
    {
        Foreach(array = GetAllPlayers(), key = "user")
        {
            UpdateUIData(player = $user, ui = $wallName, data = {graffitiId = $worksId})
        }
    }
    Else
    {
        UpdateUIData(player = $player, ui = $wallName, data = {graffitiId = $worksId})
    }

    DictionarySet(dict = @userPublishWorks, key = GetPlayerUserID(player = $player), value = GetTime())
    GameAnalyticsCustomEvent(player = $player, parts = ["player:works:publish"])
}

SET_WORKS_WALL_INDEX
{
    If(not @worksWallIndex)
    {
        @worksWallIndex = {}
    }
    $dictValue = NewDictionary(@worksWallIndex)
    DictionarySet(dict = $dictValue, key = $wallName, value = $wallIndex)
    @worksWallIndex = DictionaryToTable(dict = $dictValue)
}

UPDATE_WORKS_WALL_DATA
{
    If(not @worksWalls)
    {
        @worksWalls = {}
    }
    $dictValue = NewDictionary(@worksWalls)
    DictionarySet(dict = $dictValue, key = $wallName, value = $works)
    @worksWalls = DictionaryToTable(dict = $dictValue)
}

WORKS_WALL_UPDATE
{
    $worksList = {}
    $worksIndex = 0
    If(@worksWallIndex)
    {
        $dictValue = NewDictionary(@worksWallIndex)
        $worksIndex = DictionaryGet(dict = $dictValue, key = $wallName)
        If (not $worksIndex)
        {
            $worksIndex = 0
        }
    }

    If(@worksWalls)
    {
       $dictValue = NewDictionary(@worksWalls)
       $worksList = DictionaryGet(dict = $dictValue, key = $wallName)
       If(not $worksList)
       {
           $worksList = {}
       }
    }

    $index = 1
    $curTime = GetTime()
    $size = ArraySize(array = $worksList)
    $worksWallUpdateTime = GetWorldVar(key = "worksWallUpdateTime")
    Foreach(array = $worksList, key = "works")
    {
        If($worksIndex < $index || ($worksIndex >= $size && $index == 1))
        {
            $worksIndex = $index
            CallTrigger(event = "SET_WORKS_WALL_INDEX", wallName = $wallName , wallIndex = $worksIndex)

            Foreach(array = GetAllPlayers(), key = "player")
            {
                If(GetEntityMapName(entity = $player) == "map001")
                {
                    If($wallName ==  "curServerPublishWorks")
                    {
                        $userId = GetPlayerUserID(player = $player)
                        $userUpdateTime = DictionaryGet(dict = @userPublishWorks, key = $userId)
                        If(not $userUpdateTime || ($curTime - $userUpdateTime) * 20 > $worksWallUpdateTime)
                        {
                            UpdateUIData(player = $player, ui = $wallName, data = $works)
                        }
                        Else
                        {
                            DictionarySet(dict = @userPublishWorks, key = $userId)
                        }
                    }
                    Else
                    {
                        UpdateUIData(player = $player, ui = $wallName, data = $works)
                    }
                }
            }
            Break()
        }
        $index = $index + 1
    }
}

FUNC_LOAD_NEW_WORKS { --获取最新作品
    $currentCount = GetWorldVar(key = "curServerWorksCount")
    $othersCount = GetWorldVar(key = "otherServerWorksCount")
    $userIds = {}
    Foreach(array = GetAllPlayers(), key = "player")
    {
        $userId = GetPlayerUserID(player = $player)
        ArrayAppend(array = $userIds, value = $userId)
    }
    GetNewWorks(currentCount = $currentCount , othersCount = $othersCount, userIds = $userIds,  event = "LOAD_NEW_WORKS_RESPONSE" )
}

LOAD_NEW_WORKS_RESPONSE {
    $info = {}
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")

    If($code == 1)
    {
        $info = DictionaryGet(dict = $dictValue, key = "data")
    }

    Foreach(array = GetAllPlayers(), key = "player")
    {
        UpdateUIData(player =$player, ui = "works_appreciate", data = $info)
    }

    CallTrigger(event = "UPDATE_WORKS_WALL_DATA", wallName = "newWorksWall", works = $info)
    @newWorks = $info
}

FUNC_LOAD_EXCELLENT_WORKS {--获取优秀作品
    $count = GetWorldVar(key = "excellentWorksCount")
    $praiseNum = GetWorldVar(key = "excellentWorksPraiseNum")
    GetExcellentWorks(count = $count, limit = 0, event = "FUNC_LOAD_EXCELLENT_RESPONSE" )
}

FUNC_LOAD_EXCELLENT_RESPONSE {
    $info = {}
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")

    If($code == 1)
    {
        $info = DictionaryGet(dict = $dictValue, key = "data")
    }

    Foreach(array = GetAllPlayers(), key = "player")
    {
        UpdateUIData(player =$player, ui = "works_excellent", data = $info)
    }

    CallTrigger(event = "UPDATE_WORKS_WALL_DATA", wallName = "excellentWorksWall", works = $info)
}


FUNC_LOAD_WORKS_WALL_TOP {--获取上周前10名
    $count = GetWorldVar(key = "weekTopWorksCount")
    GetTopWorks(count = $count, event = "FUNC_LOAD_WORKS_WALL_TOP_RESPONSE" )
}

FUNC_LOAD_WORKS_WALL_TOP_RESPONSE {
    $info = {}
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")
    If($code == 1)
    {
        $info = DictionaryGet(dict = $dictValue, key = "data")
    }

    Foreach(array = GetAllPlayers(), key = "player")
    {
        UpdateUIData(player =$player, ui = "topWorksWall", data = $info)
    }

    CallTrigger(event = "UPDATE_WORKS_WALL_DATA", wallName = "topWorksWall", works = $info)
}

ENTITY_RIDE_OFF {
    If(not IsPlayer(entity = $obj2)){ Return() }
    PlayAction(
        entity = $obj1,
        includeSelf = true,
        actionName = "jump",
        actionTime = -1
    )
    $targetAttachPointIndexBuff = GetConfigVar(obj = $obj1, key = $obj2.role == @adult and "targetAttachPointIndexBuffByAdult" or "targetAttachPointIndexBuffByChild")
    If($targetAttachPointIndexBuff and GetEntityTypeBuff(entity = $obj2, key = "fullName", value = $targetAttachPointIndexBuff)){
        RemoveEntityBuff(entity = $obj2, buff = GetEntityTypeBuff(entity = $obj2, key = "fullName", value = $targetAttachPointIndexBuff))
    }
    CallTrigger(object = $obj2, event = "FUNC_STATE_RIDE_OPERATION", ride = $obj1, opt = "RIDE_OFF")
    CallTrigger(object = $obj2, event = "FUNC_CHECK_SHOWN_DANCES_SKILL", shown = true)

    StartTimer(rep = false, object = $obj2, target = $obj1, time = 1, event = "RIDEOFF_IN_DEC")

    If(not IsPlayer(entity = $obj1)){
        If(not GetEntityTypeBuff(entity = $obj1, key = "fullName", value = "myplugin/dec_jump2_2_idle_buff") and not GetRideOn(entity = $obj1))
        {
            AddEntityBuff(entity = $obj1, cfg = "myplugin/dec_jump2_2_idle_buff")
        }
        Return() 
    }
}
ENTITY_RIDE_ON {
    If(not IsPlayer(entity = $obj2)){ Return() }
    CallTrigger(object = $obj2, event = "FUNC_STATE_RIDE_OPERATION", ride = $obj1, opt = "RIDE_ON")

    If(not IsPlayer(entity = $obj1))
    { 
        CallTrigger(object = $obj2, event = "FUNC_CHECK_SHOWN_DANCES_SKILL", shown = false)
        Return() 
    }
    CallTrigger(event = "FUNC_STATE_ADD_INTERACTLIST", entities = [$obj1, $obj2])
}
REMOVE_ILL_BUFF
{
    CallTrigger(object = $obj1, event = "FUNC_STATE_RELEASE", state = "ill", duration = 0)
}

FUNC_NAVIGATION_GET_MAPS_ALL_PATH
{
    CallTrigger(event = "FUNC_NAVIGATION_SET_MAP_ADJACENCY_MATRIX")

    ForLoop(from = 1, to = ArraySize(array = @allMaps or []), key = "index")
    {
        $mapA = ArrayGet(array = @allMaps, index = $index)
        $curMapToOther = Concat(p1 = $mapA, p2 = "ToOther")

        CallTrigger(
            event = "FUNC_NAVIGATION_DIJKSTRA",
            startIdx = $index,
            type = "global",
            matrix = GetGlobalVar(key = "mapAdjacencyMatrix"),
            saveName = $curMapToOther
        )

        ForLoop(from = 1, to = ArraySize(array = @allMaps), key = "idx")
        {
            If($idx == $index)
            {
                Continue()
            }

            $mapB = ArrayGet(array = @allMaps, index = $idx)
            $mapATomapB = Concat(p1 = $mapA, p2 = "To", p3 = $mapB)
            CallTrigger(
                event = "FUNC_NAVIGATION_GET_PATH",
                allPath = GetGlobalVar(key = $curMapToOther),
                endIdx = $idx,
                type = "global",
                saveName = $mapATomapB
            )

            $mapATomapBDWs = Concat(p1 = $mapATomapB, p2 = "DWs")
            CallTrigger(
                event = "FUNC_NAVIGATION_GET_DOORWAY_LIST",
                mapS = $mapA,
                mapE = $mapB,
                type = "global",
                saveName = $mapATomapBDWs
            )
        }
    }
}

FUNC_NAVIGATION_SET_MAP_ADJACENCY_MATRIX
{
    $needDJ = []
    $mapModel = []
    ForLoop(from = 1, to = ArraySize(array = @allMaps or []), key = "index")
    {
        $colMap = ArrayGet(array = @allMaps, index = $index)
        $mapEntry = NewDictionary(GetMapConfig(name = $colMap, key = "mapEntry"))
        $curMapModel = []
        $curNeedDJ = []

        ForLoop(from = 1, to = ArraySize(array = @allMaps), key = "idx")
        {
            $map = ArrayGet(array = @allMaps, index = $idx)
            $rowMap = DictionaryGet(dict = $mapEntry, key = $map)
            $size = ArraySize(array = $rowMap or [])
            $value = $size == 0 and @mathHuge or 1
            If($idx == $index)
            {
                $value = 0
            }
            ArrayInsert(array = $curMapModel, index = $idx, value = $value)

            --用来在跨地图寻路时判断是否还需要用Dijkstra，如果地图A到地图B只有一扇门，也就不存在什么最短路了。
            ArrayInsert(array = $curNeedDJ, index = $idx, value = ($size > 1 and true or false))
        }

        ArrayInsert(array = $needDJ, index = $index, value = $curNeedDJ)
        ArrayInsert(array = $mapModel, index = $index, value = $curMapModel)
    }
    SetGlobalVar(key = "mapMatrixNeedDijkstra", value = $needDJ)
    SetGlobalVar(key = "mapAdjacencyMatrix", value = $mapModel)
}

FUNC_NAVIGATION_DIJKSTRA --params: startIdx(开始的索引), matrix[, mapS(开始的地图名), saveName, type = "global"/"entity"]
{
    ---如果是查找地图A开始的最短路，可以使用mapS或者startIdx
    ---注！但如果是查找途径门X开始的最短路，必须使用startIdx，固定传1就行，因为设置的doorways的邻接矩阵时是固定将player插入index=1的位置。

    $startIdx = $startIdx or ArrayFind(array = @allMaps or [], value = $mapS)
    If(not $startIdx)
    {
        Return()
    }
    $rowInfo = ArrayGet(array = $matrix, index = $startIdx)
    If(not $rowInfo)
    {
        Return()
    }
    $dis = []
    $vis = []
    $path = []
    $rowNum = ArraySize(array = $matrix)
    ForLoop(from = 1, to = $rowNum, key = "index")
    {
        ArraySet(array = $vis, value = false, index = $index)
        ArraySet(
            array = $dis,
            index = $index,
            value = ArrayGet(array = $rowInfo, index = $index)
        )
        $ttDis = ArrayGet(array = $rowInfo, index = $index)
        ArraySet(array = $path, index = $index, value = $ttDis < @mathHuge and $startIdx or -1)
    }
    ArraySet(array = $vis, index = $startIdx, value = true)
    ArraySet(array = $path, index = $startIdx, value = -1)

    $tmpIdx = 0
    ForLoop(from = 1, to = $rowNum, key = "index")
    {
        $min = @mathHuge
        ForLoop(from = 1, to = $rowNum, key = "idx")
        {
            $tmpVis = ArrayGet(array = $vis, index = $idx)
            $tmpDis = ArrayGet(array = $dis, index = $idx)
            If(not $tmpVis && $tmpDis < $min)
            {
                $min = $tmpDis
                $tmpIdx = $idx
            }
        }
        -- If($tmpIdx == 0)
        -- {
        --     Continue()
        -- }
        ArraySet(array = $vis, index = $tmpIdx, value = true)

        $tmpInfo = ArrayGet(array = $matrix, index = $tmpIdx)
        $midDis = ArrayGet(array = $dis, index = $tmpIdx)
        ForLoop(from = 1, to = $rowNum, key = "idx")
        {
            $tmpVis = ArrayGet(array = $vis, index = $idx)
            $oldDis = ArrayGet(array = $dis, index = $idx)
            $calMapDisToIdx = ArrayGet(array = $tmpInfo, index = $idx)
            If(not $tmpVis && ($oldDis > $midDis + $calMapDisToIdx))
            {
                ArraySet(
                    array = $dis,
                    index = $idx,
                    value = $midDis + $calMapDisToIdx
                )
                ArraySet(array = $path, index = $idx, value = $tmpIdx)
            }
        }
    }

    If($saveName && $saveName ~= "")
    {
        $disKey = Concat(p1 = $saveName, p2 = "Dis")
        If($type == "global")
        {
            SetGlobalVar(key = $saveName, value = $path)
            SetGlobalVar(key = $disKey, value = $dis)
        }
        ElseIf($obj1)
        {
            SetObjectVar(obj = $obj1, key = $saveName, value = $path)
            SetObjectVar(obj = $obj1, key = $disKey, value = $dis)
        }
    }
}

FUNC_NAVIGATION_GET_PATH --allPath, endIdx[, mapE(结束的地图名称), saveName, type = "global"/"entity"]
{
    ---需要分开是获取地图路径 还是 获取途径门的路径。
    ---如果是获取跨越的地图路径话，推荐使用mapE
    ---注意如果是获取途径门的路径，必须传入endIdx

    $idx = 1
    $path = []
    $allPathSize = ArraySize(array = $allPath or [])
    $endIdx = $endIdx or ArrayFind(array = @allMaps or [], value = $mapE)
    If(not $endIdx || $allPathSize == 0)
    {
        Return()
    }
    $val = ArrayGet(array = $allPath, index = $endIdx)
    ArrayInsert(array = $path, index = 1, value = $endIdx)
    ForLoop(from = 1, to = $allPathSize, key = "index")
    {
        If($val == -1)
        {
            Break()
        }
        ArrayInsert(array = $path, index = 1, value = $val)
        $idx = $idx + 1
        $val = ArrayGet(array = $allPath, index = $val)
    }

    If($saveName && $saveName ~= "")
    {
        If($type == "global")
        {
            SetGlobalVar(key = $saveName, value = $path)
        }
        ElseIf($obj1)
        {
            SetObjectVar(obj = $obj1, key = $saveName, value = $path)
        }
    }
}

FUNC_NAVIGATION_GET_DOORWAY_LIST --params: mapS(玩家当前所在地图名), mapE(目标所在地图名)[, saveName, type = "global"/"entity"]
{
    $mapPath = GetGlobalVar(key = Concat(p1 = $mapS, p2 = "To", p3 = $mapE))
    If(not $mapPath)
    {
        Return()
    }
    
    $i = 1
    $allDWs = [] -- all doorways
    $mapPathSize = ArraySize(array = $mapPath)
    
    ForLoop(from = 1, to = $mapPathSize, key = "index")
    {
        $idx = $index + 1
        If($idx > $mapPathSize)
        {
            Break()
        }
        $nextMap = ArrayGet(array = @allMaps, index = ArrayGet(array = $mapPath, index = $idx))
        $mapEntryDict = NewDictionary(GetMapConfig(name = $mapS, key = "mapEntry"))
        $mapEntryArr = DictionaryGet(dict = $mapEntryDict, key = $nextMap)
        $mapS = $nextMap

        Foreach(array = $mapEntryArr, key = "entry")
        {
            ArraySet(array = $allDWs, index = $i, value = $entry)
            $i = $i + 1
        }
    }

    If($saveName && $saveName ~= "")
    {
        If($type == "global")
        {
            SetGlobalVar(key = $saveName, value = $allDWs)
        }
        ElseIf($obj1)
        {
            SetObjectVar(obj = $obj1, key = $saveName, value = $allDWs)
        }
    }
}

SHOW_PUBLIC_BLACK_BOARD_EDIT_UI
{
    $objID = GetObjectID(object = $obj1)
    $title = {
        name = "ui_edit_title_name"
    }

    $inputTitle = {
        name = "ui_edit_input_name"
    }

    $buttons = [
        {
            event = "cancel",
            normalImage = "set:tip_dialog.json image:btn_big_blue",
            pushedImage = "set:tip_dialog.json image:btn_big_blue",
            name = "ui_cancel"
        },
        {
            event = "EDIT_PUBLIC_BLACKBOARD_BUTTON_SURE",
            normalImage = "set:tip_dialog.json image:btn_big_green",
            pushedImage = "set:tip_dialog.json image:btn_big_green",
            name = "ui_sure"
        }
    ]

    $options = {
        objID = $objID
    }

    $text = $obj1.blackboardStr or ""
    $textLength = GetConfigVar(obj = $obj1, key = "textLength") or 50
 
    $contents = {
        title = $title,
        inputTitle = $inputTitle,
        buttons = $buttons,
        text = $text,
        textLength = $textLength,
        isHintRemainingText = true,
    }

    ShowInputDialog(entity = $obj2, contents = $contents, options = $options)
}

EDIT_PUBLIC_BLACKBOARD_BUTTON_SURE
{
    $npc = GetObject(objID = $objID)
    If(not $npc)
    {
        Return()
    }

    $textLength = GetConfigVar(obj = $npc, key = "textLength") or 50
    
    $name = FilterWord(msg = $name, len = $textLength)
    If(not $name)
    {
        Return()
    }

    $npc.blackboardStr = $name

    CallTrigger(event = "UPDATE_SINGLE_BLACK_BOARD_ENTITY", object = $obj1, npc = $npc)
}

SET_RANGE_BOUNDARY
{
    $index = 1
    If(not $num || not $array)
    {
        @rangeBoundary = "0"
        Return()
    }

    @rangeBoundary = "0"
    SortNumberArray(array = $array)
    $size = ArraySize(array = $array)
    Foreach(array = $array or {}, key = "value")
    {
        If($index == $size)
        {
            @rangeBoundary =  ToString($value)
            Return()
        }

        If($num <= $value)
        {
            @rangeBoundary = ToString($value)
            Return()
        }

        $index = $index + 1
    }
}