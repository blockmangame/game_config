GAME_INIT
{
    @Generate_State_List = ["hunger", "thirsty", "dirty", "fatigue", "ill", "region"]
    @skillPath = "myplugin/skill_state_"
    CallTrigger(event = "FUNC_STATE_STARTTIMER")

    @TASK_TABLE = [ --打工的任务表
        {
            taskKey = "myplugin/work/key_1",
            pos =  { x = 187, y = 18, z = 343},
            npcName = "myplugin/xiaoming",
            npcMap = "map001"
        },
        {
            taskKey = "myplugin/work/key_2",
            pos =  { x = 207, y = 18, z = 343},
            npcName = "myplugin/xiaohong",
            npcMap = "map001"
        }
    ]
    CreateNpc(cfgName = "myplugin/indentModel", pos = {x = 170, y = 17, z = 343}  ,name = "订单")
    CreateNpc(cfgName = "myplugin/xiaoming", pos = {x = 187, y = 17, z = 343}  ,name = "小明")
    CreateNpc(cfgName = "myplugin/xiaohong", pos = {x = 207, y = 17, z = 343} ,name = "小红")
}


FUNC_STATE_STARTTIMER
{
    ForLoop(from = 1, to = ArraySize(array = @Generate_State_List), key = "idx")
    {
        $stateBase = ArrayGet(array = @Generate_State_List, index = $idx)
        $keyStr = Concat(p1 = "myplugin/skill_state_", p2 = $stateBase)
        $timeInfo = GetSkillVar(fullName = $keyStr, key = "generateTime")

        $cdTime = ArrayGet(array = $timeInfo, index = 1)
        StartTimer(
            column = 1,
            rep = false,
            timeArr = $timeInfo,
            stateBase = $stateBase,
            time = ($cdTime * 1200) // 1,
            event = "FUNC_BROADCAST_STATE",
        )
    }
}

FUNC_BROADCAST_STATE
{
    Foreach(array = GetAllPlayers(), key = "player"){
        --TODO 完善状态的生成
        --If(player in party)
        --{
        --    Continue()
        --}
        SetObjectVar(obj = $player, value = 0, key = Concat(p1 = $stateBase, p2 = "RewardCount"))

        $skill = Concat(p1 = @skillPath, p2 = $stateBase)
        $generateType = GetSkillVar(
            fullName = $skill,
            key = "generateType"
        )
        If($generateType == "A" && GetObjectVar(obj = $player, key = Concat(p1 = "had", p2 = $stateBase)))
        {
            Continue()
        }
        
        If($generateType == "B" && GetObjectVar(obj = $player, key = Concat(p1 = $stateBase, p2 = "UsedTime")))
        {
            Continue()
        }

        CallTrigger(object = $player, event = "FUNC_STATE_GENERATE", stateBase = $stateBase, col = $column, skill = $skill)
	}

    $curTime = ArrayGet(array = $timeArr, index = $column)
    $column = $column + 1
    If($column > ArraySize(array = $timeArr))
    {
        $column = 1
    }
    $endTime = ArrayGet(array = $timeArr, index = $column)
    $cdTime = $endTime - $curTime
    If($cdTime <= 0)
    {
        $cdTime = 60 - $curTime + $endTime
    }

    StartTimer(
        rep = false, 
        event = $event,
        column = $column,
        timeArr = $timeArr,
        stateBase = $stateBase,
        time = ($cdTime * 1200) // 1
    )
}

SHOW_SELECT_ROLE
{
    $contents = [
        {
            normalImage = "set:select_role.json image:child",
            pushedImage = "set:select_role.json image:child",
            name = "child",
            event = "CHANGE_PLAYER_SCALE_CHILD",
            text = "ui_pick_child"
        },
        {
            normalImage = "set:select_role.json image:adult",
            pushedImage = "set:select_role.json image:adult",
            name = "adult",
            event = "CHANGE_PLAYER_SCALE_ADULT",
            text = "ui_pick_adult"
        }
    ]
    
    $fullName = "myplugin/selectRoleView"

    ShowCardOptionsView(entity = $obj1, contents = $contents, fullName = $fullName)
}


REQUEST_JOIN_FAMILY_YES
{
    If(not $obj1.requestFamilyPlayer)
    {
        Return()
    }

    $teamId = GetEntityTeam(entity = $obj1)
    If(not $teamId or $teamId == 0)
    {
        Return()
    }

    $playerCount = GetTeamInfo(teamId = $teamId, key = "playerCount")
    If(not $playerCount or $playerCount >= 4)
    {
        Return()
    }

    SetEntityTeam(entity = $obj1.requestFamilyPlayer, teamId = $teamId)
    CallTrigger(event = "REFRESH_TEAM_LIST", teamId = $teamId)
    ShowTip(entity = $obj1.requestFamilyPlayer, tipType = 2,textKey = "You Are In Family", time = 40)
    $obj1.requestFamilyPlayer = nil
}

REQUEST_JOIN_FAMILY_NO
{
    If($obj1.requestFamilyPlayer)
    {
        ShowTip(entity = $obj1.requestFamilyPlayer, tipType = 2,textKey = "Sorry, I Can't Accept You Request.", time = 40)
    }

    $obj1.requestFamilyPlayer = nil
}

INVITE_JOIN_FAMILY_YES
{
    If(not $obj1.inviteFamilyPlayer)
    {
        Return()
    }

    $teamId = GetEntityTeam(entity = $obj1.inviteFamilyPlayer)
    If(not $teamId or $teamId == 0)
    {
        Return()
    }

    $playerCount = GetTeamInfo(teamId = $teamId, key = "playerCount")
    If(not $playerCount or $playerCount >= 4)
    {
        Return()
    }

    SetEntityTeam(entity = $obj1, teamId = $teamId)
    CallTrigger(event = "REFRESH_TEAM_LIST", teamId = $teamId)
    ShowTip(entity = $obj1, tipType = 2,textKey = "You Are In Family", time = 40)
    $obj1.inviteFamilyPlayer = nil
}

INVITE_JOIN_FAMILY_NO
{
    If($obj1.inviteFamilyPlayer)
    {
        ShowTip(entity = $obj1.inviteFamilyPlayer, tipType = 2,textKey = "Sorry, I Don't Want Join Family.", time = 40)
    }

    $obj1.inviteFamilyPlayer = nil
}

CHANGE_PLAYER_SCALE_ADULT
{
    If($obj1.scaleBuff)
    {
        RemoveEntityBuff(entity = $obj1, buff = $obj1.scaleBuff)
        $obj1.scaleBuff = nil
    }

    If($obj1.boundingBuff)
    {
        RemoveEntityBuff(entity = $obj1, buff = $obj1.boundingBuff)
        $obj1.boundingBuff = nil
    }

    $obj1.scaleBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/scale_adult")
    $obj1.boundingBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/bounding_adult")
    $obj1.role = @adult

    If(IsPlayer(entity = $obj1))
    {
        UpdateUIData(player = $obj1, ui = "win_dressArchiveScale", data = 1)
    }
}

CHANGE_PLAYER_SCALE_CHILD
{
    If($obj1.scaleBuff)
    {
        RemoveEntityBuff(entity = $obj1, buff = $obj1.scaleBuff)
        $obj1.scaleBuff = nil
    }

    If($obj1.boundingBuff)
    {
         RemoveEntityBuff(entity = $obj1, buff = $obj1.boundingBuff)
         $obj1.boundingBuff = nil
    }

    $obj1.scaleBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/scale_child")
    $obj1.boundingBuff = AddEntityBuff(entity = $obj1, cfg = "myplugin/bounding_child")
    $obj1.role = @child
    
    If(IsPlayer(entity = $obj1))
    {
        UpdateUIData(player = $obj1, ui = "win_dressArchiveScale", data = 0.8)
    }
}

FUNC_CLOSE_GPS --params: destination
{
    If(not $destination || $destination == GetObjectVar(obj = $obj1, key = "gpsDestination"))
    {
        SetGuidePostion(entity = $obj1)
        RemoveSkill(entity = $obj1, name = "myplugin/close_gps")
        SetObjectVar(obj = $obj1, key = "gpsDestination", value = nil)
    }
}

FUNC_OPEN_GPS --params: destination
{
    SetGuidePostion(pos = $pos, entity = $obj1)
    AddSkill(entity = $obj1, name = "myplugin/close_gps")
    SetObjectVar(obj = $obj1, key = "gpsDestination", value = $destination)
    ShowTip(entity = $obj1, tipType = 3, textKey = "openning_gps")
}

FUNC_CLOSE_PROGRESS_FOLLOW_OBJ --params: pgName
{
    If(not $pgName || $pgName == GetObjectVar(obj = $obj1, key = "progress"))
    {
        ShowProgressFollowObj(
            entity = $obj1,
            isOpen = false
        )
        SetObjectVar(obj = $obj1, key = "progress", value = nil)
    }
}

FUNC_CLOSE_DETAILS_UI --params: uiName
{
    If(not $uiName || $uiName == GetObjectVar(obj = $obj1, key = "detailsUI"))
    {
        ShowDetails(entity = $obj1, isOpen = false)
        SetObjectVar(obj = $obj1, key = "detailsUI", value = nil)
    }
}

FUNC_STOP_TIMER --params: timerStr
{
    $timer = GetObjectVar(obj = $obj1, key = $timerStr)
    If(not $timer)
    {
        Return()
    }
    StopTimer(timer = $timer)
    SetObjectVar(obj = $obj1, key = $timerStr, value = nil)
}

FUNC_REMOVE_BUFF --params: buffStr
{
    $hadBuff = GetObjectVar(obj = $obj1, key = $buffStr)
    If(not $hadBuff)
    {
        Return()
    }
    RemoveEntityBuff(entity = $obj1, buff = $hadBuff)
    SetObjectVar(obj = $obj1, key = $buffStr, value = nil)
}

FUNC_STATE_ADD_INTERACTLIST --params: entities
{
    Foreach(array = @Generate_State_List, key = "stateBase")
    {
        $listName = Concat(p1 = $stateBase, p2 = "InteractList")
        Foreach(array = $entities, key = "entity")
        {
            If(not IsPlayer(entity = $entity) || not GetObjectVar(obj = $entity, key = Concat(p1 = "had", p2 = $stateBase)))
            {
                Continue()
            }
            $interactList = GetObjectVar(obj = $entity, key = $listName) or []
            Foreach(array = $entities, key = "cp")
            {
                $cpID = GetObjectID(object = $cp)
                If($cp == $entity || not IsPlayer(entity = $cp) || ArrayFind(array = $interactList, value = $cpID))
                {
                    Continue()
                }
                ArrayAppend(array = $interactList, value = $cpID)
            }
            SetObjectVar(obj = $entity, key = $listName, value = $interactList)
        }
    }
}

FUNC_LOAD_NEW_WORKS {
    $currentCount = 20
    $othersCount = 20
    $userIds = {}
    Foreach(array = GetAllPlayers(), key = "player")
    {
        $userId = GetPlayerUserID(player = $player)
        ArrayAppend(array = $userIds, value = $userId)
    }
    GetNewWorks(currentCount = $currentCount , othersCount = $othersCount, userIds = $userIds,  event = "LOAD_NEW_WORKS_RESPONSE" )
}

LOAD_NEW_WORKS_RESPONSE {
    $info = {}
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")

    If($code == 1)
    {
        $info = DictionaryGet(dict = $dictValue, key = "data")
    }

    Foreach(array = GetAllPlayers(), key = "player")
    {
        UpdateUIData(player =$player, ui = "works_appreciate", data = $info)
    }
}

FUNC_LOAD_EXCELLENT_WORKS {
    $count = 180
    GetExcellentWorks(count = $count, limit = 0, event = "LOAD_TOP_EXCELLENT_RESPONSE" )
}

LOAD_TOP_EXCELLENT_RESPONSE {
    $info = {}
    $dictValue = NewDictionary($response)
    $code = DictionaryGet(dict = $dictValue, key = "code")

    If($code == 1)
    {
        $info = DictionaryGet(dict = $dictValue, key = "data")
    }

    Foreach(array = GetAllPlayers(), key = "player")
    {
        UpdateUIData(player =$player, ui = "works_top", data = $info)
    }
}